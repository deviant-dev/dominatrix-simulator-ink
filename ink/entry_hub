=== entry_hub ===

-> select_gender_check ->

{ deviation == puppyplay:
    -> yuki_task_puppyplay
}

//Variable reset used by the Goddess monitoring scene to determine what type of interaction she has with the player
~ just_changed = none
~ setHub(None, "entry_hub", -> entry_hub)

{ inTutorial: -> tutorial_entry }
{ deviation == tutorial || deviation == demo: -> credits}

-> normal_entry


= normal_entry
~ main_entry_shortcut = true

/scene entry.hub
/music entry
/ambient rain

//first visit, player encounters Goddess and gets hint about needing more rewards first
//future visits, the door can be locked if they don't have 10 bloodstones, but they need to know why they can't enter - could be acheived with in game notification or whisper 

+ [move: Goddess] -> goddess_hub
+ [move: Yuki] -> yuki_hub
+ [move: Nega] -> nega_hub
+ [move: Headmistress] -> headmistress_hub
+ [move: Pragma] -> pragma_hub
+ [move: Furia] -> furia_hub
+ [move: Angel] -> angel_hub
+ [move: Treat] -> treat_hub
+ [move: Caliste] -> caliste_hub
+ [move: Credits] -> credits


= tutorial_entry

{
    - tutorial_step == at_yuki_intro: -> move_yuki_intro
    - tutorial_step == at_hm_intro: -> move_headmistress
    - tutorial_step == at_nega_intro || tutorial_step == at_hm_bonus: -> move_headmistress_or_nega
    - tutorial_step == at_goddess_outro: -> normal_entry
    - else:
        /logError Shouldn't be in the entry at this point. Skipping to entry.
        -> normal_entry
}


= move_yuki_intro

~ punishments = 1
~ rewards = 1
~ yuki_shortcut = true

-> select_gender_check ->

/scene entry.intro1
/music yuki
/ambient rain

{ web: 
    <blockquote><i>You are disoriented for a moment as everything turns black and the scene around you changes.</i></blockquote>

    <blockquote><i>You stand inside a large Victorian foyer with a sweeping staircase and several sets of doors leading off of it. The room is fairly well lit, but all of the doors are shrouded in shadows. Before you stands a lithe, young woman dressed in a maid's uniform. Beaming a bright smile, she greets you...</i></blockquote>
}

+ [move: Yuki] -> yuki_intro


= move_headmistress

~ punishments = 1
~ rewards = 1
~ hm_shortcut = true

/scene entry.TutorialHM
/music entry
/ambient rain

+ [move: Headmistress]
    {headmistress_intro: -> headmistress_hub | -> headmistress_intro }
+ [move: YukiIntro]
    -> yuki_hub.go_to_headmistress -> 
    /scene none
    -> move_headmistress

= move_headmistress_or_nega

~ punishments = 0
~ rewards = 1

/scene entry.TutorialHMOrNega
/music entry
/ambient rain

+ [move: Headmistress] -> headmistress_hub
+ [move: Nega] -> nega_intro



