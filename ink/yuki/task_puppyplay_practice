=== yuki_task_puppyplay_practice ===

- (start)

~ tries = 0
~ task_count = 0
~ task_attempts = 0
~ task_goal = 0


// ====== Begin Debug ======

//~ passes = 4
//~ level = 4
//-> revisit_test

// ====== End Debug =====


/perform idle_yuki
{shuffle:

    - /perform talk_consider happy 45
    I think we should practice your poses some more, puppy. #158

    - /perform talk_lean_forward amused 50
    Time to work on your puppy poses... #129
}

- (sit)

/perform do_point_down happy 55
<b>Sit</b>, puppy. #159

+ [pose:puppy_sit] 
    ~addObey()
    -> Yuki_GoodJob ->
    
+ [timeout] 
    ~addDisobey()
    -> Yuki_Timeout -> sit

// Only play this if this is our first time in practice.
- {yuki_task_puppyplay_practice == 1:

    /perform talk_one_finger amused 45
    Now, if you're good, this won't take long. #150

    /perform idle_unhappy excited 75
    And if you do well during practices, I'll give you a special treat! #151

    /perform idle_unhappy_stomp angry 55
    But if you do poorly, I'm going to punish you. #152
}

/perform do_snap_fingers skeptical 45
Be ready for anything you've already learned! #160

~ tries = 0

- (ready)


{cycle:

    - /perform idle_swaying skeptical 65
    Are you listening, puppy? #161

    - /perform idle_pleased happy 55
    Do you understand? #143
}

+ [yes] -> use_paws
+ [no]
    -- (use_paws)
    ~ tries++
    /perform react_chuckle amused 65
    Oh my gosh silly, answer with your paws! #144
    -> ready

+ [pose:puppy_yes]
    { tries == 0:
        /perform react_excited
        -> Yuki_GoodJob ->
        Maybe you don't need this practice after all! #148
    - else:
        -> Yuki_AcknowledgeYes ->
    }

+ [pose:puppy_no]
    -- (wrong_no)
    ~ tries++

    /perform react_chuckle amused 65
    Silly puppy, I think you meant to raise your <i>other</i> paw for <b>"yes."</b> #145
    -> ready
+ [timeout]
    ~ tries++
    -> Yuki_Timeout -> ready

- /perform do_snap_fingers angry 45
Pay attention! #165

{yuki_task_puppyplay_practice == 1:
    ~ task_goal = 5
- else:
    {cycle:
        - ~ task_goal = 7
        - ~ task_goal = 9
    }
}

/log Goal = {task_goal}

-(Practice_Loop)

-> test_next_pose ->

~ task_count++
/log Progress = {task_count} / {task_goal} (attempts: {task_attempts})
~ obeyed()
{task_count < task_goal: -> Practice_Loop}

{task_attempts == task_goal:
    ~ yuki_puppy_difficulty = 2
}

{task_attempts > task_goal + 1:
    ~ yuki_puppy_difficulty = 1
}


{task_attempts > task_goal + 2:
    -> Yuki_Puppyplay_Punishment_Earned ->
- else:
    -> Yuki_Puppyplay_Reward_Earned ->
}

->->


= test_next_pose
{shuffle:
    - -> test_pose("puppy_beg", -> prompt_beg_pose) ->->
    - -> test_pose("puppy_confused", -> prompt_confused_pose) ->->
    - -> test_pose("puppy_hungry", -> prompt_hungry_pose) ->->
    - -> test_pose("puppy_playful", -> prompt_playful_pose) ->->
    - -> test_pose("puppy_tired", -> prompt_tired_pose) ->->
    - -> test_pose("puppy_thirsty", -> prompt_thirsty_pose) ->->
    - -> test_pose("puppy_sorry", -> prompt_sorry_pose) ->->
    - -> test_pose("puppy_back", -> prompt_back_pose) ->->
    - -> test_pose("puppy_horny", -> prompt_horny_pose) ->->
}


// The pose test stitch with pose name and prompt stitch parameters.

// @param {string} pose The name of the pose to test. (e.g. 'puppy_beg')
// @param {divert} prompt The stitch that has the pose's prompt VO/performances.


= test_pose(pose, -> prompt)
~ tries = 0
- (retry)
-> prompt ->
~ tries++
~ task_attempts++
+ [pose:{pose} {yuki_puppy_difficulty > 1:no_assist}] -> Yuki_GoodJob ->->
+ { tries < 2 } [timeout] -> retry



// Prompts for each pose type used by the 'test_pose' stitch above.


= prompt_beg_pose

{cycle:

    - /perform tease_bend_over excited 55
    Show me you're ready to play, {player_name}. <b>"Beg"</b>! #01

    - /perform idle_eager happy 55
    Okay! <b>"Beg"</b> to have some fun. #02

    - /perform do_point_down amused 55
    Time to practice your puppy poses. Get into <b>"beg"</b>! #03

    - /perform do_bouncing_little excited 75
    Yay! Time for more puppy play. Can you <b>"beg"</b> for me? #04

    - /perform talk_bashful aroused 65
    We can do puppy play... if you <b>"beg"</b> first. #05
}

/perform idle_pleased amused 55
->->


= prompt_confused_pose


{cycle:

    - /perform react_shrug_long happy 45
    Show me the <b>"confused"</b> pose. #09

    - /perform talk_emphatic_yuki amused 65
    Are you <b>"confused"</b>? Show me! #10
}

/perform idle_yuki happy 55
->->


= prompt_hungry_pose


{cycle:

    - /perform talk_lean_forward happy 70
    Show me <b>"hungry"</b>. #13

    - /perform idle_eager excited 65
    Is my cute little puppy, <b>"hungry"</b>? #14
}

/perform idle_yuki amused 60
->->


= prompt_playful_pose


{cycle:

    - /perform talk_one_finger happy 60
    Let me see you be <b>"playful"</b>. #18

    - /perform idle_pleased excited 45
    Okay! Be my <b>"playful"</b> little puppy. #19
}

/perform idle_yuki excited 55
->->


= prompt_tired_pose



{cycle:

    - /perform do_inspect skeptical 45
    Perform <b>"tired"</b>, puppy. #21

    - /perform talk_bashful sad 55
    Are you <b>"tired"</b>, puppy? Let me see. #22
}

/perform idle_unhappy skeptical 60
->->


= prompt_thirsty_pose


{cycle:

    - /perform talk_lean_forward happy 70
    Can you let me know you're <b>"thirsty"</b>, puppy? #25

    - /perform do_inspect amused 55
    Hrm. Let's see <b>"thirsty"</b>, {player_name}. #26
}

/perform idle_pleased aroused 50
->->


= prompt_sorry_pose



{cycle:

    - /perform react_disappointed sad 45
    Whenever you've been a bad puppy, you need to show me you're <b>"sorry"</b>. #31

    - /perform talk_one_finger sad 50
    Perform the <b>"sorry"</b> pose. #32
}

/perform idle_yuki sad 55
->->


= prompt_back_pose

/perform talk_behold

{ cycle:

    - /perform talk_lean_forward excited 55
    Sometimes, I want my puppy on {isBoy:his|{isGirl:her|their}} <b>"back"</b>. #36

    - /perform do_point_down amused 50
    Show me how a good puppy lays on their <b>"back"</b>. #37
}

/perform idle_eager happy 50
->->


= prompt_horny_pose


{ cycle:

    - /perform idle_sassy aroused 60
    I know my naughty little puppy will get <b>"horny"</b> sometimes. You need to be able to tell me! #41

    - /perform idle_eager aroused 65
    Perform the <b>"horny"</b> pose. #42
}

->->


