=== yuki_task_puppyplay_practice ===

- (start)

~ setScene(task_scene, Yuki)
~ tries = 0
~ task_count = 0
~ task_attempts = 0
~ task_goal = 0


// ====== Begin Debug ======

//~ passes = 4
//~ level = 4
//-> revisit_test

// ====== End Debug =====


/perform idle_yuki
{shuffle:
    - I think we should practice your poses some more, puppy. #158
    - Time to work on your puppy poses... #129
}

- (sit)
/perform do_point_down
<b>Sit</b>, puppy. #159

+ [pose:puppy_sit] -> Yuki_GoodJob ->
+ [timeout] -> Yuki_Timeout -> sit

// Only play this if this is our first time in practice.
- {yuki_task_puppyplay_practice == 1:
    /perform talk_behold
    Now, if you're good, this won't take long. #150
    
    /perform idle_pleased
    And if you do well during practices, I'll give you a special treat! #151
    
    /perform talk_lean_forward
    But if you do poorly, I'm going to punish you. #152
}

/perform talk_one_finger
Be ready for anything you've already learned! #160

~ tries = 0

- (ready)
/perform idle_cocks_head_to_left
{cycle:
    - Are you listening, puppy? #161
    - Do you understand? #143
}

+ [yes] -> use_paws
+ [no]
    -- (use_paws)
    ~ tries++
    /perform react_chuckle
    Oh my gosh silly, answer with your paws! #144
    -> ready

+ [pose:puppy_yes]
    { tries == 0:
        /perform react_excited
        -> Yuki_GoodJob ->
        Maybe you don't need this practice after all! #148
    - else:
        /perform talk_one_hand
        -> Yuki_AcknowledgeYes ->
    }

+ [pose:puppy_no]
    -- (wrong_no)
    ~ tries++
    /perform idle_clasp_hands
    Silly puppy, I think you meant to raise your <i>other</i> paw for <b>"yes."</b> #145
    -> ready
+ [timeout] 
    ~ tries++
    -> Yuki_Timeout -> ready 

- /perform talk_behold
Pay attention! #165

{yuki_task_puppyplay_practice == 1:
    ~ task_goal = 5
- else:
    {cycle:
        - ~ task_goal = 7
        - ~ task_goal = 9
    }
}

-(Practice_Loop)

-> test_next_pose ->

~ task_count++

{task_count < task_goal: -> Practice_Loop}

{task_attempts == task_goal:
    ~ addReward()
    ~ yuki_puppy_difficulty = 2
}

{task_attempts > task_goal + 1:
    ~ yuki_puppy_difficulty = 1
}

{task_attempts > task_goal + 2:
    ~ yuki_puppy_difficulty = 1
    ~ addPunishment()
}

{yuki_task_puppyplay_practice == 1:
    {task_attempts > task_goal + 2:
        -> Yuki_Puppyplay_Punishment_Earned ->
    - else:
        -> Yuki_Puppyplay_Reward_Earned ->
    }
}

{task_attempts > task_goal + 2:
    -> Yuki_Puppyplay_Punishment_Earned ->
- else:
    -> Yuki_Puppyplay_Reward_Earned ->
}

->->

= test_next_pose
{shuffle:
    - -> test_pose("puppy_beg", -> prompt_beg_pose) ->->
    - -> test_pose("puppy_confused", -> prompt_confused_pose) ->->
    - -> test_pose("puppy_hungry", -> prompt_hungry_pose) ->->
    - -> test_pose("puppy_playful", -> prompt_playful_pose) ->->
    - -> test_pose("puppy_tired", -> prompt_tired_pose) ->->
    - -> test_pose("puppy_thirsty", -> prompt_thirsty_pose) ->->
    - -> test_pose("puppy_sorry", -> prompt_sorry_pose) ->->
    - -> test_pose("puppy_back", -> prompt_back_pose) ->->
    - -> test_pose("puppy_horny", -> prompt_horny_pose) ->->
}


// The pose test stitch with pose name and prompt stitch parameters.

// @param {string} pose The name of the pose to test. (e.g. 'puppy_beg')
// @param {divert} prompt The stitch that hase the pose's prompt VO/performances.

= test_pose(pose, -> prompt)
~ tries = 0
- (retry)
-> prompt ->
~ tries++
~ task_attempts++
+ [pose:{pose}] -> Yuki_GoodJob ->->
+ { tries < 2 } [timeout] -> retry



// Prompts for each pose type used by the 'test_pose' stitch above.

= prompt_beg_pose
/perform idle_pleased

{cycle:
    - Show me you're ready to play, {player_name}. <b>"Beg"</b>! #01
    - Okay! <b>"Beg"</b> to have some fun. #02
    - Time to practice your puppy poses. Get into <b>"beg"</b>! #03
    - Yay! Time for more puppy play. Can you <b>"beg"</b> for me? #04
    - We can do puppy play... if you <b>"beg"</b> first. #05
}

/perform idle_yuki
->->


= prompt_confused_pose
/perform react_shrug_long

{cycle:
    - Show me the <b>"confused"</b> pose. #09
    - Are you <b>"confused"</b>? Show me! #10
}

/perform idle_yuki
->->


= prompt_hungry_pose
/perform talk_thoughtful

{cycle:
    - Show me <b>"hungry"</b>. #13
    - Is my cute little puppy, <b>"hungry"</b>? #14
}

/perform idle_yuki
->->


= prompt_playful_pose
/perform talk_one_finger

{cycle:
    - Let me see you be <b>"playful"</b>. #18
    - Okay! Be my <b>"playful"</b> little puppy. #19
}

/perform idle_yuki
->->

= prompt_tired_pose
/perform do_inspect

{cycle:
    - Perform <b>"tired"</b>, puppy. #21
    - Are you <b>"tired"</b>, puppy? Let me see. #22
}

/perform idle_yuki
->->


= prompt_thirsty_pose
/perform talk_lean_forward

{cycle:
    - Can you let me know you're <b>"thirsty"</b>, puppy? #25
    - Hrm. Let's see <b>"thirsty"</b>, {player_name}. #26
}

/perform idle_yuki
->->

= prompt_sorry_pose
/perform react_disappointed

{cycle:
    - Whenever you've been a bad puppy, you need to show me you're <b>"sorry"</b>. #31
    - Perform the <b>"sorry"</b> pose. #32
}

/perform idle_yuki
->->


= prompt_back_pose
/perform talk_behold

{ cycle:
    - Sometimes, I want my puppy on {isBoy:his|her} <b>"back"</b>. #36
    - Show me how a good puppy lays on their <b>"back"</b>. #37
}

/perform idle_yuki
->->


= prompt_horny_pose
/perform idle_eager

{ cycle:
    - I know my naughty little puppy will get <b>"horny"</b> sometimes. You need to be able to tell me! #41
    - Perform the <b>"horny"</b> pose. #42
}

/perform idle_yuki
->->


= Good_Ending_Director

// Fill in shuffles with extra endings

// { shuffle:
    // - -> yuki_reward_puppyplay_milking ->->
    // - -> yuki_reward_puppyplay_lickfeet ->->
// }
->->
= Bad_Ending_Director

{ shuffle:
    // - -> yuki_punishment_puppyplay_cage ->->
    - -> yuki_punishment_puppyplay_spanking ->->
    }

->->





