=== yuki_reward_cunnilingus ===
-> select_gender_check ->
~ setScene(reward_scene, Yuki, "yuki_reward_cunnilingus", -> yuki_reward_cunnilingus)
-> scene_setup -> intro


= scene_setup
/scene atrium.couch_sitting
/music yuki-cunnilingus
/ambient none
/character Yuki sex_couch_openlegs_idle amused 65
->->


= intro

/perform sex_couch_openlegs_idle_eager aroused 60
You're in for such a treat today, {player_name}. #02

- (mouth)

/perform sex_couch_openlegs_talk_question excited 75
{shuffle:
    -I'm going to let you put that cute tongue of yours to work #03
    and make me cum. #04

    -I want you to make me cum with that little tongue of yours. #05

    -That mouth of yours is going to make me orgasm. #06
}

/perform sex_couch_openlegs_talk_question skeptical 55
Would you like that, {player_name}? #07

+ [yes]
    -> Yuki_AcknowledgeYes.EmoteOnly ->

+ [no]
    -> Yuki_AcknowledgeNo.EmoteOnly ->
    /perform sex_couch_openlegs_idle_tease sad 60
    If you don't want this reward, #08
    you can go to someone else. #09

    -- (leave)
    /perform sex_couch_openlegs_talk_question skeptical 60
    Are you sure you want to leave? #10

    ++ [yes]
        /perform sex_couch_openlegs_react_scoffs angry 45
        Alright then. #11
        Goodbye. #12
        -> exit

    ++ [no]
        /perform sex_couch_openlegs_react_thankful happy 55
        Phew. #13
        I thought you didn't like me. #14

    ++ [timeout] -> Yuki_Timeout.EmoteOnly -> leave
    ++ [distracted] -> Yuki_PayAttention.EmoteOnly -> leave

+ [timeout] -> Yuki_Timeout.EmoteOnly -> mouth

+ [distracted] -> Yuki_PayAttention.EmoteOnly -> mouth


- (move)

/perform sex_couch_openlegs_point_down amused 50
{shuffle:
    - Come here then. #01
    - Now come here. #22
}


+ [move:couch]
+ [timeout] -> Yuki_PayAttention.EmoteOnly -> move
+ [distracted]  -> Yuki_PayAttention.EmoteOnly -> move

- -> mode


= load_begin
-> scene_setup -> begin


= load_minigame
-> scene_setup -> minigame


= mode

{shuffle:
    - -> puppy_option
    - -> puppy_option
    - -> human_option
    - -> human_option
}


= puppy_option

I want you to be my puppy for this. #41

Okay? #42

+ [yes]
    ~ wants_puppy = true
    -> puppy_intro

+ [no]
    Alright. #43
    I guess you can be a human this time. #44
    -> human_intro

+ [timeout] -> Yuki_Timeout.EmoteOnly -> puppy_option

+ [distracted] -> Yuki_PayAttention.EmoteOnly -> puppy_option


= human_option

I know sometimes I like you to be a puppy, #45
but I want you to be a human for this. #46

Is that okay? #47

+ [yes]
    /perform sex_couch_openlegs_idle_eager aroused 75
    Human it is! #20
    -> human_intro

+ [no]

    /perform sex_couch_openlegs_idle_tease sad 65
    Alright. #48
    I guess you can be my puppy, after all. #49
    -> puppy_intro

+ [timeout] -> Yuki_Timeout.EmoteOnly -> human_option

+ [distracted] -> Yuki_PayAttention.EmoteOnly -> human_option


= puppy_intro

- (sit)

<b>"Sit"</b>, {player_name}. #50

+ [pose:puppy_sit]
    ~addObey()
    -> Yuki_GoodJob.EmoteOnly ->

+ [timeout]
    ~addDisobey()
    -> Yuki_Interested.EmoteOnly ->
    -> sit

--> take_reward


= human_intro
/perform sex_couch_openlegs_point_down amused 70
{shuffle:
    - Kneel for me. #51
    - Get down on your knees. #52
}

+ [pose:kneel] -> take_reward
+ [timeout] -> Yuki_Timeout.EmoteOnly -> human_intro


= take_reward

/perform sex_couch_openlegs_idle happy 75

-> Yuki_Take_Reward_Prompt ->

+ [pose:present_wrist_reward_kneel]
    /perform sex_couch_openlegs_react_thankful excited 50
    /wait 1
    ~removeReward(->load_begin)
    -> Yuki_AcknowledgeYes.EmoteOnly -> begin

+ [no]
    /perform sex_chair_crosslegs_idle_left
    -> Yuki_CheckIfInterested.EmoteOnly -> take_reward

+ [timeout]
    /perform sex_chair_crosslegs_idle_left
    -> Yuki_Timeout.EmoteOnly -> take_reward

+ [distracted]
    /perform sex_chair_crosslegs_idle_left
    -> Yuki_PayAttention.EmoteOnly -> take_reward


= begin
/character yuki sex_chair_crosslegs_idle_tease aroused 75

Now, don't worry. #25
I'll tell you exactly what to do to make me cum. #26

//only trigger the first time through
{not minigame:
    Since this is your first cunnilingus reward... #53
}

You're going to start slow and steady. #54
Don't want to tire you out too much! #55
Teehee! #56

//continue normally
/perform sex_couch_openlegs_react_thankful excited 65
Just follow directions #27
and you'll do great! #28

/emotion aroused 75
Once I get more comfortable #29

{wants_puppy:
    /emotion aroused 75
    you can start, puppy. #30
- else:
    /emotion aroused 75
    you can start, {player_name}. #31
}

-> minigame


= minigame
/scene atrium.cunnilingus
/perform cun_couch_idle aroused 69
+ [minigame:success] -> finish


= finish
/scene Atrium.Cunnilingus_Complete
/perform sex_couch_openlegs_idle amused 45

Wow. #32


/perform sex_couch_openlegs_react_thankful aroused 75
{wants_puppy:

    {shuffle:
        - That was amazing, puppy. #33
        - I'm so impressed, puppy. #34
        - You did so well, puppy! #35
    }
- else:
    {shuffle:
       - That was amazing, {player_name}. #36
       - I'm so impressed, {player_name}. #37
       - You did so well, {player_name}! #38
    }
}

/perform sex_couch_openlegs_talk_question skeptical 50
Did you enjoy your reward? #57

+ [yes]
    /perform sex_couch_openlegs_idle_eager happy 60
    Oh goody! #58
    I enjoyed it too. #59

+ [no]
    /perform sex_couch_openlegs_react_scoffs sad 45
    That makes me so sad! #60
    Maybe you just need more practice. #61

+ [timeout] -> Yuki_Timeout.EmoteOnly ->


- We're done for now! #62

Time to stand back up. #63

+ [pose:stand]

- /perform sex_couch_openlegs_idle amused 45

{wants_puppy:
    -> Yuki_Puppyplay_Reward_Complete.EmoteOnly ->
- else:
    -> Yuki_Reward_Complete.EmoteOnly ->
}

-> exit
