=== yuki_punishment_puppyplay_spanking ===

//this scene has the player crawl over to the couch and lay across it with their butt out. They get a spanking similiar to the Headmistress scene

{shuffle:
- ~ yuki_spanking_goal = 5
- ~ yuki_spanking_goal = 10
}

~ setScene(punishment_scene, Yuki, "yuki_punishment_puppyplay_spanking", -> yuki_punishment_puppyplay_spanking)
~ yuki_spanking_fails = 0
~ yuki_spanking_mistakes = 0

 -> select_gender_check ->

/scene atrium.close

/music yuki
/ambient off
/character yuki idle


//intro contains performances

-> Yuki_Puppy_Intro ->

- (intro_puppy_spanking)
/perform talk_lean_forward angry 35
You have been a very naughty puppy. #44
Haven't you? #45

/perform idle_unhappy_arms_crossed sad 50

+ [yes] 
    -> Yuki_PuppyPlay_Punishment_UsePaws ->
    -> intro_puppy_spanking 

+ [no] 
    -> Yuki_PuppyPlay_Punishment_UsePaws ->
    -> intro_puppy_spanking 
    
+ [pose:puppy_yes]
    /perform talk_one_finger angry 40
    That's right. #46
    You have! #47

+ [pose:puppy_no]
    /perform react_point_at_player_angry disgusted 40
    Don't you lie to me, you bad {isBoy: boy|{isGirl:girl|thing}}. #48
    I know you've been naughty! #49
    
+ [timeout] -> Yuki_Timeout ->

+ [distracted] -> Yuki_PayAttention ->

- 
/perform idle_unhappy_stomp sad 30
And I'm super disappointed in you, puppy. #50

/perform react_sad_kick_foot_yuki angry 30
I thought I trained you better than that! #51

/perform talk_bashful sad 50
I thought you wanted to make me happy! #52

/perform idle 
So now... #53

/perform talk_lean_forward angry 40
My naughty puppy is going to be spanked! #01

~ tries = 0
- (spanking_couch)
~ tries++

/perform do_point_left sad 40
Crawl over to that couch and lay across it. #02

{cycle:
    - Butt sticking out nice and high. #03
    - And stick out your butt! #04
    - Present your puppy bottom. #05
    }

/perform idle_yuki

+ [move:couch]
    Get comfortable. You're going to be there for a while. #06
+ { tries < 2 } [timeout] -> Yuki_Interested -> spanking_couch
+ { tries>= 2 } [timeout]
    -> Yuki_CheckIfInterested -> spanking_couch
//+ [distracted] -> Yuki_PayAttention -> spanking_couch

- 
/scene atrium.punishment_spanking 

You have been a naughty puppy. #07
You've obviously forgotten the rules. #08
<i>Disobey and be punished.</i> #09
<i>Obey and be rewarded!</i> #10

- (remember)
Don't you remember those rules, puppy? #11

+ [yes] 
    -> Yuki_PuppyPlay_Punishment_UsePaws ->
    -> remember

+ [no]
    -> Yuki_PuppyPlay_Punishment_UsePaws ->
    -> remember
    
+ [pose:puppy_yes]
    Don't you lie to me, puppy. #12
    Good {isBoy: boys|{isGirl:girls|pets}} don't earn punishments. #13

+ [pose:puppy_no]
    Obviously you don't. #14
    Or you wouldn't be here right now. #15

+ [timeout] -> Yuki_Timeout -> remember

// 5, 10
- {
- yuki_spanking_goal == 5:
    You're going to get five, big swats, puppy! #16
- yuki_spanking_goal == 10:
    You're going to get ten, big swats, puppy! #40
- else:
    /log bad swat count found
}

I want to hear you count them out. #17

/perform idle
This is going to hurt me more than it hurts you! #18
And don't forget to show me your reaction. #19

- (ready)
Are you ready puppy? #54

+ [yes] 
    -> Yuki_PuppyPlay_Punishment_UsePaws ->
    -> ready

+ [no]
    -> Yuki_PuppyPlay_Punishment_UsePaws ->
    -> ready
    
+ [pose:puppy_yes]
   Alright. Then here we go! #55

+ [pose:puppy_no]
    Well, then take a deep breath and get ready #56
    or get out. #57
    What will it be? #58
    --(big_puppy)
    Are you going to take your punishment like a big puppy? #59
    
    ++ [yes]
        -> Yuki_PuppyPlay_Punishment_UsePaws ->
        -> big_puppy

    ++ [no]
        -> Yuki_PuppyPlay_Punishment_UsePaws ->
        -> big_puppy
        
    ++ [pose:puppy_yes]
       -> Yuki_AcknowledgeYes ->
       That means we can get this over with #60
       and do fun stuff next time! #61
    
    ++ [pose:puppy_no]
        -> Yuki_AcknowledgeNo ->
        Get out of here. #62
        Come back when you're ready to behave! #63
        -> exit
    
    ++ [timeout] -> Yuki_Timeout -> big_puppy
    
+ [timeout] -> Yuki_Timeout -> ready

- (try_spank)

~ yuki_spanking_count = 0

/log Goal = {yuki_spanking_goal}

- (try_spank_react)
/wait 1 
/character yuki do_spank_player
~ yuki_spanking_count++

-> SayNumber(yuki_spanking_count) ->

+ [react-success]
    {yuki_spanking_count < yuki_spanking_goal:-> Yuki_Puppyplay_GoodSpank ->}
    
+ [react-fail]
    ~yuki_spanking_fails++
     -> Yuki_Puppyplay_BadSpank ->
    {yuki_spanking_fails == 1: -> perfect ->}
    {yuki_spanking_fails > 2: -> spank_failout}
    -> start_over ->
    -> try_spank

- /log Progress = {yuki_spanking_count} / {yuki_spanking_goal}

{yuki_spanking_count < yuki_spanking_goal: -> try_spank_react}

-> spank_success


 = start_over

{shuffle:
    - Now you have to start over! #20
    - Are you even trying? #41
    Start back at one. #21
    - I thought the Headmistress trained you to take a spanking?  #22
    Time to start again. #23
    }
->->   

= perfect

Listen puppy. #42

You have to perform every spanking perfectly to finish your punishment. #24
- (spank_ready)
Ready? #25

+ [yes] 
    -> Yuki_PuppyPlay_Punishment_UsePaws ->
    -> spank_ready
        
+ [no]
    -> Yuki_PuppyPlay_Punishment_UsePaws ->
    -> spank_ready
    
+ [pose:puppy_yes]
    -> try_spank
    
+ [pose:puppy_no]
    -> Yuki_CheckIfInterested -> spank_ready

+ [timeout] -> Yuki_Timeout -> spank_ready
    
->->

= spank_failout

/perform react_disappointed sad 50
-> Yuki_Badjob ->

Leave and don't come back until you're sorry! #26

-> exit


= spank_success

- /perform idle_pleased happy 50
All done! #43

- (proud)
/perform idle amused 25
Are you proud of yourself, puppy?  #27

+ [yes] 
    -> Yuki_PuppyPlay_Punishment_UsePaws ->
    -> proud

+ [no]
    -> Yuki_PuppyPlay_Punishment_UsePaws ->
    -> proud
    
+ [pose:puppy_yes]
    /perform idle_tease
    -> Yuki_GoodJob ->

+ [pose:puppy_no]
    /perform idle_tease
    Well, you should be! #28

+ [timeout] -> Yuki_Timeout -> proud



- (spank_success_test) /scene atrium.punishment_spanking_2
/perform idle_yuki 

{cycle:
    - Now, come here.  #29
    - Come here, puppy. #30
    - I <i>said</i> come here!  #31
}

+ [move:yuki]

- (sit)
- /perform do_point_down happy 25

{shuffle:
    - Sit. #64
    - Now sit. #65
    - Back down on your puppy bottom. #66
}

+ [pose: puppy_sit]
    -> Yuki_GoodJob ->
+ [timeout] -> Yuki_Interested -> sit


- ~ tries = 0
- (sorry)

~ tries++
/perform idle_unhappy

{cycle:
    - Bow your head, puppy, and show me you're sorry. #196
    - Show me you're sorry, puppy.  #32
    - I want to see you show me you're sorry! #33
}

+ [pose:puppy_sorry]
+ { tries <= 1 } [timeout] 
    If you aren't sorry, then you're going in your cage! #67
    -> sorry
    
+ { tries > 1} [timeout] -> cage

- /wait 5
/perform idle_yuki skeptical 50 
-> Yuki_Sit_Up ->

- (sorry_ask_again)
/perform idle_unhappy

Are you sorry for breaking the rules, puppy? #34

+ [yes] 
    -> Yuki_PuppyPlay_Punishment_UsePaws ->
    -> sorry_ask_again

+ [no]
    -> Yuki_PuppyPlay_Punishment_UsePaws ->
    -> sorry_ask_again
    
+ [pose:puppy_yes]
    /perform do_shake_finger_stern
    If you feel bad about it, you should have paid better attention! #198
//ending contains performances
    -> Yuki_Puppyplay_Punishment_Complete ->
    Now go. #35

+ [pose:puppy_no]
    /perform react_disappointed
    You're not sorry? #36
    I think somebody <i>wants</i> to be a bad dog. #199
    If you're not sorry, then you haven't paid off your punishment. #37
    You'll have to try next time if you want it removed. #38
    Now, get out of here! #39
//punishment marker will not be removed

+ [timeout] -> Yuki_Timeout -> sorry_ask_again


-
-> exit

= cage 

/perform react_shrug_short sad 30
You clearly haven't learned your lesson. #68

/perform do_point_left angry 50
Get in your cage and think about what a naughty {isBoy: boy|{isGirl: girl|pet}} you've been! #69

+[move:cage]
//punishment marker will not be removed
/wait 
/action cage_close

/perform do_shake_finger sad 40
I hope next time you come see me, #70
it won't be for a punishment. #71
/wait 5

/perform react_scoffs angry 30
Someone will let you out, eventually. #72

/scene None
/wait 3 

-> exit

= SayNumber(n)
// NOTE: Keep these on separate lines for the subtitle system. -deviant
{
    - n == 1: 
        One. #73
    - n == 2:
        Two. #74
    - n == 3:
        Three. #75
    - n == 4:
        Four. #76
    - n == 5:
        Five. #77
    - n == 6:
        Six. #78
    - n == 7:
        Seven. #79
    - n == 8:
        Eight. #80
    - n == 9:
        Nine. #81
    - n == 10:
        Ten. #82
    - else:
        /logError Can't say number '{n}'.
}
->->

