=== pragma_hub ===
-> select_gender_check ->

{pragma_intro_completed == false or deviation == pragma: -> pragma_intro }

~ setHub(Pragma, "pragma_hub", -> pragma_hub)
/scene none
/scene PragmasRoom.Hub
/music pragma-hub-1
/ambient pragma
/character pragma
/perform idle_clasp_hands skeptical 10

+ [move:Approach] -> apology_check(-> hello, -> exit)
+ [move:Exit] -> exit


= hello

/wait

{ pragma_regard >= 10:
/emotion happy 70
    Hello favored subject. #01
- else:
/emotion happy 20
    {shuffle:
        - Hello, Subject 33. #02
        - Greetings, imperfect one. #03
    }
}

-
{inChastity:
    Chastity status: active. #04
    Access to enhancement protocols restricted. #05

    Requisition the Goddess to toggle chastity status. #06
    Otherwise, continue for partial access. #07
}

Subject, do you seek purpose? #08

+ [yes] -> Pragma_AcknowledgeYes.EmoteOnly ->
+ [no] -> goodbye

- (wrists)
/perform idle_clasp_hands skeptical 50
{shuffle:
    - Display gemstones for calculation. #09
    - Subject will now present wrists. #10
}

+ [pose: present_wrists] 
+ [timeout] -> Pragma_Timeout.EmoteOnly -> wrists

- /emotion amused 50
{
    - punishments > 0: -> check_punishment
    - rewards >= 10: -> check_reward_mega
    - rewards > 0 : -> check_reward
    - else: -> check_task
}


= check_punishment

/perform do_tap_wrist disgusted 40
{punishments > 1:
    Subject Obsidian count exceeds one (1). #11 //(1) is silent 
    Activate punishment subroutine to reduce count. #12
- else:
/perform do_inspect angry 75
    Obsidian detected. #13
    Punishment procedure accessible. #14
}

/perform idle_clasp_hands skeptical 70
Is subject here to participate in a punishment experiment? #15
+ [yes] -> choose_punishment
+ [no]

/emotion disgusted 45
- No? #16

{
    - punishments < 10 && rewards < 10:
        -> check_task
    - punishments >= 10:
        Subject has an excess of Obsidians. #17
        Remit at least one (1) to qualify for a task. #18
    - rewards >= 10: 
        Error encountered in reward value: #19
        Total count too high for task algorithm. #20
        {punishments > 1:
            Milestone reward accessible #21
            If all punishments cleared. #22
        - else: 
            To access super-reward, #23
            subject must complete remaining punishment. #24
        }
}

-/emotion amused 50
Does subject wish to run a punishment algorithm? #25

+ [yes] -> choose_punishment
+ [no]

-
{inChastity == false: 
    Subject will write this instruction to memory. #26
    The Goddess can redact all current punishments #27
    if chastity is activated. #28
}

-> goodbye


= check_reward_mega

Subject has acquired a milestone reward count. #29


{pragma_regard < 10:
    The subject has insufficient hidden variable. #30
    A reward can still be accessed #31
    if the subject remits a single Bloodstone. #32
    -> check_reward
}

{inChastity:
    The subject is in chastity. #33
    Hidden reward is inaccessible. #34
    Requisition the Goddess to toggle chastity status. #35
    -> check_reward
}

Subject has shown intriguing signs of enhancement. #36
Subject will remit ten (10) Bloodstones. #37
Does the subject confirm? #38


+ [yes] -> choose_reward_mega
+ [no] -> check_reward


= check_reward
/music pragma-hub-2
/perform talk_question amused 40
Is the subject here to initiate a reward subroutine? #39

+ [yes] -> choose_reward
+ [no]

- No? #40
/emotion happy 50
Subject seeks refinement above gratification. #41

{
    - rewards < 10:
        -> check_task

    - rewards >= 10: 
        Maximum Bloodstone variable registered. #42
        Before initiating further tasks, #43
        reduce variable by running reward subroutine. #44
}
/emotion skeptical 60
Will the subject participate in a reward experiment? #45

+ [yes] -> choose_reward
+ [no]

-/emotion disgusted 50
    Curious response. Subject is encountering a computational error. #46

{ inChastity:
    Query Goddess to deactivate chastity status. #47
}

-> goodbye


= check_task

{ 
    - punishments == 0 && rewards == 0:
        Null values found in gemstones. #48
        The subject is in search of purpose. #49
    - punishments > 0:
        Obsidian variable below maximum threshold. #50
        Will subject participate in a task? #51
    - else:
        Subject, do you wish to be enhanced? #52
}

+ [yes] -> choose_task
+ [no] -> goodbye


= choose_punishment
/emotion excited 45
{shuffle:
- Activating punishment subroutine. #53
- Initiating punishment procedure. #54
}

{ shuffle:
    - -> pragma_punishment_placeholder
}


= choose_reward_mega
/emotion excited 100
{shuffle:
- Activating encrypted reward subroutine. #55
- Accessing secured reward subroutine. #56
}

{shuffle:
    - -> pragma_rewardmega_placeholder
}


= choose_reward
/emotion excited 35
{shuffle:
- Activating reward subroutine. #57
- Initiating reward subroutine. #58
}

{ inChastity:
/emotion angry 60
    Chastity status limits access to reward subroutines. #59
        {shuffle:
            - -> pragma_rewardmega_placeholder
            - -> pragma_rewardmega_placeholder
        }
    - else:
        {shuffle:
            - -> pragma_rewardmega_placeholder
            - -> pragma_rewardmega_placeholder
        }
}


= choose_task
/emotion happy 25
{shuffle:
- Initiating task flow. #60
- Launching procedure. #61
}

{ shuffle:
    // - -> pragma_task_dancing_doll
    - -> pragma_task_posing_doll
}


= goodbye
/emotion aroused 60
{shuffle:
    - Query routine complete. #62
    - Experiment concluded. #63
}


/emotion disgusted 50
{shuffle:
    - Terminating all active processes. #64
    - Powering down simulation. #65
}

/wait
-> exit



