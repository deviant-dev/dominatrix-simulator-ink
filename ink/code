
=== game_start ===

{ web: -> table_of_contents }

/log Starting game from the beginning.

// -> exit has the logic for module starting locations
{
    - deviation == unspeakable_toy:
        -> deviation_unspeakable_toy
    - deviation == puppyplay:
        -> yuki_hub
    - deviation == hand_signals:
        -> headmistress_hub
    - deviation == treat:
        -> treat_care
    - deviation == nega_dice:
        -> nega_hub
    - deviation == pragma:
        -> pragma_hub
    - deviation == furia:
        -> furia_hub
    - inTutorial == false:
        -> entry_hub
    - else:
        -> game_intro
}


=== game_restart ===
// Make sure deviation is set in case the story state was reset
// via 'RestartStory()' in StoryTeller.
~ setDeviation(deviation)
-> game_start


=== load_game ===
-> load_path


// For deviations that start after the main game intro's gender check.
=== post_intro_deviation_check ===
{
    - deviation == puppyplay:
        -> yuki_hub
    - deviation == hand_signals:
        -> headmistress_hub
    - deviation == exhibitionism:
        -> headmistress_hub
    - else:
        -> goddess_intro
}


=== exit ===
/scene None

{
    - deviation == unspeakable_toy:
        -> deviation_unspeakable_toy
    - deviation == hand_signals:
        /scene none
        -> headmistress_hub
    - deviation == exhibitionism:
        /scene none
        -> headmistress_hub
    - deviation == puppyplay:
        /scene none
        -> yuki_hub
    - deviation == demo:
        -> pause(3) ->
        -> game_outro
    - deviation == treat:
        -> treat_care
    - deviation == nega_dice:
        -> nega_hub
    - deviation == pragma:
        -> pragma_hub
    - deviation == furia:
        -> furia_hub
}

// Not in a deviation. Continue with normal game.

-> pause(1) ->

// If the player has never done the Ms Treatment intro, do the intro.
{not treat_intro:
    -> treat_intro
}

{just_changed != none:
    ~ profile_scene_count++
    {current_mistress != Treat:
        ~ treat_scenes_since_last_session ++
    - else:
        ~ treat_scenes_since_last_session = 0
    }
}

{inTutorial or current_mistress == Goddess or current_mistress == Treat or just_changed == none:
    /log Skipping monitoring
    -> entry_hub
- else:
    {shuffle:
        - -> goddess_monitoring
        - -> treat_monitoring
        - -> entry_hub
        - -> entry_hub
        - -> entry_hub
        // - -> entry_hub
        // - -> entry_hub
    }
}


=== pause(seconds) ===
+ [wait:{seconds}] 
- ->->


=== continue ===
+ [continue] ->->


=== tutorial_end ===
~ punishments = 0
~ rewards = 0
~ inTutorial = false
~ main_entry_shortcut = true
-> exit


=== function unlockShortcuts() ===
/log Unlocking shortcuts.
~ goddess_shortcut = true
~ yuki_shortcut = true
~ nega_shortcut = true
~ hm_shortcut = true
~ goddess_angry_shortcut = true
~ hm_bonus_shortcut = true
~ goddess_pleased_shortcut = true
~ main_entry_shortcut = true
~ return

=== function unlockAll() ===
~ goddess_hub_status = unlocked
~ inTutorial = false
~ unlockShortcuts()
~ return


=== select_gender_check ===
{hasChosen(): ->->}
{web:
    -> select_gender_web ->->
- else:
    -> select_gender ->->
}

=== select_gender ===
/scene entrydark.genderchoice
/ambient rain
/music entry

/log Showing gender choices.

- (options)

+ [choose-gender-male]
    ~ setGender("m")
    /notify text="<i>You now appear masculine.</i>" delay=10 #note08
    -> options

+ [choose-gender-female]
    ~ setGender("f")
    /notify text="<i>You now appear feminine.</i>" delay=10 #note09
    -> options

+ {hasChosen()} [move]

- ->->


=== function hasChosen() ===
~ return player_name != "none"

=== function save(-> load) ===
~ setLoadPath(load)
{not debug:
    /save
}

=== function setLoadPath(-> path) ===
~ load_path = path

=== function setGender(gender) ===
{
    - gender == "m":
        ~ isBoy = true
        ~ isGirl = false
        ~ hasPenis = true
        ~ player_name = "boy"
    - gender == "f":
        ~ isBoy = false
        ~ isGirl = true
        ~ hasPenis = false
        ~ player_name = "girl"
    - gender == "p":
        ~ isBoy = false
        ~ isGirl = false
        ~ hasPenis = true
        ~ player_name = "pet"
}

~ return


=== function addPunishment() ===
~ return addPunishmentWithPath(-> entry_hub)


=== function addPunishmentWithPath(-> path)
{ punishments >= 10:
    /logError Can't add a punishment. Already at 10.
    ~ return false
}

~ punishments++
~ total_punishments_earned++
~ just_changed = punishment_added
~ decreaseRegard()
~ save(path)

{web:
    <b>Punishment Added</b>
	<i>{getSummaryText()}</i>
- else:
    /log Punishment Added: {getSummaryText()}
    /notify title="<color=red>Punishment</color> Earned" text="{getName(current_mistress)}'s regard for you has <color=red>decreased</color>." delay = 3
}

~ return true


=== function removePunishment() ===
{ punishments <= 0:
    /logError Can't remove punishment. Already at 0.
    ~ return false
}

~ punishments--
~ just_changed = punishment_removed
~ increaseRegard()
~ save(-> entry_hub)

{web:
    <b>Punishment Removed</b>
	<i>{getSummaryText()}</i>
- else:
    /log Punishment Removed: {getSummaryText()}
    /notify title="Punishment Paid" text="{getName(current_mistress)}'s regard for you has <color=blue>increased</color>." delay = 3
}

~ return true


=== function removeBigPunishment() ===
{ punishments < 10:
    /logError Can't remove 10 punishments. Not at 10.
    ~ return false
}

~ punishments -= 10
~ just_changed = punishment_removed
~ increaseRegard()
~ save(-> entry_hub)

{web:
    <b>Punishment x10 Removed</b>
	<i>{getSummaryText()}</i>
- else:
    /log Punishment x10 Removed: {getSummaryText()}
    /notify title="Punishment x10 Paid" text="{getName(current_mistress)}'s regard for you has <color=blue>increased</color>." delay = 3
}

~ return true


=== function addReward() ===
~ return addRewardWithPath(-> entry_hub)

=== function addRewardWithPath(-> load) ===
{ rewards >= 10:
    /logError Can't add a reward. Already at 10.
    ~ return false
}

~ rewards++
~ total_rewards_earned++
~ just_changed = reward_added
~ increaseRegard()
~ save(load)

{web:
    <b>Reward Added</b>
	<i>{getSummaryText()}</i>
- else:
    /log Reward Added: {getSummaryText()}
    /notify title="Reward Earned" text="{getName(current_mistress)}'s regard for you has <color=blue>increased</color>." delay = 3
}

~ return


=== function removeReward(-> load) ===
{ rewards <= 0:
    /logError Can't remove a reward. Already at 0.
    ~ return false
}

~ rewards--
~ just_changed = reward_removed
~ increaseRegard()
~ save(load)

{web:
    <b>Reward Removed</b>
	<i>{getSummaryText()}</i>
- else:
    /log Reward Removed: {getSummaryText()}
    /notify title="Reward Paid" text="{getName(current_mistress)}'s regard for you has <color=blue>increased</color>." delay = 3
}

~ return true


=== function removeBigReward(-> load) ===
{ rewards < 10:
    /logError Can't remove 10 rewards. Not at 10.
    ~ return false
}

~ rewards -= 10
~ just_changed = reward_removed
~ increaseRegard()
~ save(load)

{web:
    <b>Reward x10 Removed</b>
	<i>{getSummaryText()}</i>
- else:
    /log Reward x10 Removed: {getSummaryText()}
    /notify title="Reward x10 Paid" text="{getName(current_mistress)}'s regard for you has <color=blue>increased greatly</color>." delay = 3
}

~ return true


=== function increaseRegard() ===
~ changeRegard(1)

=== function decreaseRegard() ===
~ changeRegard(-1)

=== function changeRegard(amount) ===
~ changeRegardOf(current_mistress, amount)

=== function increaseRegardTo(mistress) ===
~ changeRegardOf(mistress, 1)

=== function decreaseRegardTo(mistress) ===
~ changeRegardOf(mistress, -1)

=== function changeRegardOf(mistress, amount) ===
{
    - mistress == Goddess:
        ~ goddess_regard = MinMaxRegard(goddess_regard + amount)
    - mistress == Headmistress:
        ~ headmistress_regard = MinMaxRegard(headmistress_regard + amount)
    - mistress == Nega:
        ~ nega_regard = MinMaxRegard(nega_regard + amount)
    - mistress == Yuki:
        ~ yuki_regard = MinMaxRegard(yuki_regard + amount)
    - mistress == Pragma:
        ~ pragma_regard = MinMaxRegard(pragma_regard + amount)
    - mistress == Angel:
        ~ angel_regard = MinMaxRegard(angel_regard + amount)
    - mistress == Caliste:
        ~ caliste_regard = MinMaxRegard(caliste_regard + amount)
    - mistress == Furia:
        ~ furia_regard = MinMaxRegard(furia_regard + amount)
    - mistress == Treat:
        ~ treat_regard = MinMaxRegard(treat_regard + amount)
    - else:
        /logError Can't change {mistress}'s regard.
}

=== function MinMaxRegard(amount)
~ return MinMax(amount, 0, 10)

=== function MinMax(amount, min, max)
{
    - amount < min: ~ return min
    - amount > max: ~ return max
    - else: ~ return amount
}

// Fallback for getNegaDie1/2/3() external function
=== function getNegaDie1() ===
~ return getNegaDie()


=== function getNegaDie2() ===
~ return getNegaDie()


=== function getNegaDie3() ===
~ return getNegaDie()


=== function getNegaDie() ===
~ temp roll = RANDOM(1, 6)

{
    - roll == 1: ~ return -1
    - roll == 2: ~ return -1
    - roll == 3: ~ return -1
    - roll == 4: ~ return 0
    - roll == 5: ~ return 1
    - roll == 6: ~ return 1
    - else:
        /logError Roll out of range (1-6): {roll}
        ~ return 0
}


=== function rollToText(roll) ===
{
    - roll == -1: Skull
    - roll == 0: Blank
    - roll == 1: Cup
}


=== function setDeviation(d) ===

// Set the deviation.
~ deviation = d

// Set inTutorial to true only for tutorial, demo, and full game.
~ inTutorial = deviation == demo || deviation == tutorial || deviation == full_game

~ return


=== function setHub(mistress, name, -> load) ===
~ setScene(hub_scene, mistress, name, load)


=== function setScene(type, mistress, name, -> load) ===
~ current_mistress = mistress
~ scene_type = type
~ scene_name = name
~ checked_if_interested = false
~ resetObeyed()
~ has_plug = false
~ has_gag = false
~ wants_cei = false

~ temp mistressName = "{mistress}"

{ mistress == None:
    - ~ mistressName = "Entry"
}

{web:
    <b>{mistressName} {type}</b> #CLEAR
	<i>{getSummaryText()}</i>
- else:
    /log Scene: {name} / {getSummaryText()}
}

~ save(load)

=== function getSummaryText() ===
~ temp regardText = ""
~ temp deviationText = "/ Deviation: {deviation}"

// Set regard text if with a mistress.
{
    - current_mistress == Goddess:
        ~ regardText = "/ {current_mistress} Regard: {goddess_regard}"
    - current_mistress == Headmistress:
        ~ regardText = "/ {current_mistress} Regard: {headmistress_regard}"
    - current_mistress == Nega:
        ~ regardText = "/ {current_mistress} Regard: {nega_regard}"
    - current_mistress == Yuki:
        ~ regardText = "/ {current_mistress} Regard: {yuki_regard}"
    - current_mistress == Pragma:
        ~ regardText = "/ {current_mistress} Regard: {pragma_regard}"
    - current_mistress == Angel:
        ~ regardText = "/ {current_mistress} Regard: {angel_regard}"
    - current_mistress == Treat:
        ~ regardText = "/ {current_mistress} Regard: {treat_regard}"
    - current_mistress == Caliste:
        ~ regardText = "/ {current_mistress} Regard: {caliste_regard}"
    - current_mistress == Furia:
        ~ regardText = "/ {current_mistress} Regard: {furia_regard}"
}

// Set optional deviation-specific text
{(deviation == tutorial || deviation == full_game) && inTutorial:
    ~ deviationText += "/ tutorial @ {tutorial_step}"
}

~ return  "Punishments: {punishments} / Rewards: {rewards} {regardText} {deviationText}"


=== function getName(mistress) ===
{
    - current_mistress == Goddess:
        ~ return "The Goddess"
    - current_mistress == Headmistress:
        ~ return "The Headmistress"
    - current_mistress == Yuki:
        ~ return "Mistress Yuki"
    - current_mistress == Nega:
        ~ return "Mistress Nega"
    - current_mistress == Treat:
        ~ return "Ms Treatment"
    - current_mistress == Pragma:
        ~ return "Divine Engine Pragma"
    - current_mistress == Caliste:
        ~ return "Priestess Caliste"
    - current_mistress == Furia:
        ~ return "Enforcer Furia"
    - current_mistress == Angel:
        ~ return "Lady Angelica"
    - else:
        /logWarning Mistess not found: {mistress}
        ~ return "Mistress Unknown"
}



=== function obeyed() ===
{web:
	<i>Checking Obeyed: Obey = {obey} / Disobey = {disobey} / Obeying = {obey - disobey > 0:true|false}</i>
- else:
    /log Checking Obeyed: Obey = {obey} / Disobey = {disobey} / Obeying = {obey - disobey > 0:true|false}
}

~ return obey - disobey > 0


=== function resetObeyed() ===
~ obey = 0
~ disobey = 0


=== function addObey() ===
~ obey++

{web:
	<i>Obey Increased: Obey = {obey} / Disobey = {disobey} / Obeying = {obey - disobey > 0:true|false}</i>
- else:
    /log Obey Increased: Obey = {obey} / Disobey = {disobey} / Obeying = {obey - disobey > 0:true|false}
}


=== function addDisobey() ===
~ disobey++

{web:
	<i>Disobey Increased: Obey = {obey} / Disobey = {disobey} / Obeying = {obey - disobey > 0:true|false}</i>
- else:
    /log Disobey Increased: Obey = {obey} / Disobey = {disobey} / Obeying = {obey - disobey > 0:true|false}
}


=== RandomCommentLoop(-> commentTunnel, minTimes, maxTimes, minWait, maxWait)
~ temp count = 0
~ temp max = RANDOM(minTimes, maxTimes)
/log Comments looping {max} times.
- (repeat)

-> commentTunnel ->
+ [wait {RANDOM(minWait, maxWait)}]
~ count++
{ count < max: -> repeat | ->-> }

=== RandomCommentLoopWithNo(-> commentTunnel, -> noTunnel, minTimes, maxTimes, minWait, maxWait)
~ temp count = 0
~ temp max = RANDOM(minTimes, maxTimes)
/log Comments looping {max} times.
- (repeat)

-> commentTunnel ->
+ [wait {RANDOM(minWait, maxWait)}]
+ [no] -> noTunnel
- ~ count++
{ count < max: -> repeat | ->-> }

=== RandomCommentLoopWithYes(-> commentTunnel, -> yesTunnel, minTimes, maxTimes, minWait, maxWait)
~ temp count = 0
~ temp max = RANDOM(minTimes, maxTimes)
/log Comments looping {max} times.
- (repeat)

-> commentTunnel ->
+ [wait {RANDOM(minWait, maxWait)}]
+ [yes] -> yesTunnel
- ~ count++
{ count < max: -> repeat | ->-> }


=== RandomCommentLoopWithPuppyBackYes (-> commentTunnel, -> yesTunnel, minTimes, maxTimes, minWait, maxWait)
~ temp count = 0
~ temp max = RANDOM(minTimes, maxTimes)
/log Comments looping {max} times.
- (repeat)

-> commentTunnel ->
+ [wait {RANDOM(minWait, maxWait)}]
+ [pose:puppy_back_yes] -> yesTunnel
- ~ count++
{ count < max: -> repeat | ->-> }




=== take_photo ===

{shuffle:
    - /perform do_camera_shoot_1
    - /perform do_camera_shoot_2
    - /perform do_camera_shoot_3
}

->->


