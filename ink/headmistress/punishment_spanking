=== headmistress_punishment_spanking ===

~ setScene(punishment_scene, Headmistress, "headmistress_punishment_spanking", -> headmistress_punishment_spanking)
-> start


= start

TODO: Support shuffling swats and equipment

{inTutorial:
    ~ headmistress_spanking_target_count = 10
    -else:
    {shuffle:
    - ~ headmistress_spanking_target_count = 10
    - ~ headmistress_spanking_target_count = 15
    - ~ headmistress_spanking_target_count = 20
    }
}

// {shuffle:
// - ~headmistress_spanking_implement = "paddle"
// - ~headmistress_spanking_implement = "hand"
// - ~headmistress_spanking_implement = "crop"
// }

// Just use the hand for now.
~ headmistress_spanking_implement = "hand"
~ headmistress_spanking_break_count = 0
~ headmistress_spanking_count = 0
~ headmistress_spanking_mistakes = 0

-> ask_consent


= ask_consent

/music headmistress-training
/character headmistress talk_question skeptical 55
/lookat player

Do you accept that you must be punished? #92
/wait 1
/perform idle_clasp_hands skeptical 70

- (ask_punish_ready)

+ [yes]
    ~ addObey()
    /character headmistress react_thankful happy 60
    Your acceptance is most becoming. #93
    -> get_on_desk

+ [no]
    ~ addDisobey()
    /character headmistress idle_skeptical angry 75
    Foolish, little slave. #94

    It will be over quickly and your compliance will please the Goddess. #95

    /perform idle_clasp_hands angry 45

    -- (continue_punishment)
    Shall we continue with your punishment? #96

    ++ [yes]
        ~ addObey()
        /perform talk_serious
        Magnificent. Let us begin. #97
        -> get_on_desk

    ++ [no]
        ~ addDisobey()
        /character headmistress react_scoffs angry 45
        Make haste from my sight! #98

        /perform react_shrug_long angry 75
        You have squandered my time and my attentions. #99

        /perform talk_lean_forward angry 100
        I will not suffer fools. #100

        {not inTutorial:
            You've just earned yet another punishment. #101
            ~ addPunishment()
            ~ headmistress_apologize = true
        }

        /perform do_get_the_fuck_out angry 100
        Now leave. #102
        -> exit

    ++ [timeout] -> Headmistress_Timeout -> continue_punishment

    ++ [distracted] -> Headmistress_PayAttention -> continue_punishment

+ [timeout]
    -- (accept_timeout)
    /perform idle_tease aroused 65
    You have had enough time to think. #103

    /perform idle_skeptical skeptical 45
    Your punishment must begin. #104

    /perform idle_clasp_hands skeptical 70
    Do you agree? #105
    -> ask_punish_ready

+ [distracted] -> Headmistress_PayAttention -> accept_timeout


= get_on_desk

/scene Theatre.DeskApproach
/music headmistress-punishment
/character headmistress
/lookat player
// /equip {headmistress_spanking_implement}
// /perform circle_desk

{inTutorial:
    /perform talk_behold excited 55
    Punishments are administered with the slave perched upon my desk. #05
- else:
    For your transgressions, #01
    a spanking is in order. #02
}

/character headmistress talk_question happy 55
Alight forthwith! #08

- (prompt)
/perform idle_clasp_hands happy 55

+ [move:get_on_desk]
    /perform idle_clasp_hands happy 70
    Pleasingly executed. #09

+ [timeout]
    -- (get_on_desk_timed_out)
    /perform talk_one_finger sad 65
    Our guests are waiting. Tut. Tut. #12
    -> prompt

+ [distracted]
    -> Headmistress_PayAttention -> prompt

- (kneel)
/perform do_point_down aroused 60
Now, kneel, slave. #225
/wait 1
/perform idle_clasp_hands aroused 70

~ tries = 0
- (kneel_tries)
~ tries++

+ [pose:kneel]
+ { tries < 2 } [timeout] -> kneel_tries
+ { tries >= 2 } [timeout] -> Headmistress_NoAction -> kneel

-{tries == 1:
    ~ addObey()
    /perform do_arm_raise excited 65
    Observe the eagerness with which our chattel attempts to comply. #13
    /perform react_nod_little happy 65
    This behavior pleases Her. #14
- else:
    ~ addDisobey()
    /perform talk_annoyed sad 55
    Note the hesitation. A sure sign of poor breeding. #15
    /perform react_scoffs disgusted 70
    Such offends the Goddess and must be stricken from the specimen. #16
}

- {inTutorial: -> punish_intro | -> punish_continue}


= punish_intro

- /perform talk_one_finger amused 65
The muscles of the ass must remain relaxed between swats. #44

/perform talk_behold aroused 70
This provides an exceptionally pleasing display when they clench #45

/perform idle_pleased happy 45
in response to the impact of my admonishments. #46
//perform tap_paddle

/character headmistress do_smooth_skirt excited 65
But enough of this pretense. #48

/wait
/character crowd murmur
/ambient crowd

/character headmistress idle_clasp_hands aroused 75
We aren't here just for you. #49

/perform talk_serious amused 65
We have our entire class to enlighten! #50

/perform idle_pleased aroused 75
Let us show our audience #51
the fine display you have for us. #52


-> punish_continue


= punish_continue
/music headmistress-punishment-crowd
/character crowd cheers_loud
-> Theater_RaiseCurtains ->
/character headmistress do_arm_raise happy 65
Welcome esteemed guests! #53

/perform idle_clasp_hands amused 75
Our subject today has earned a spanking. #54
-> Headmistress_Crowd_Punishment ->
/character headmistress

/perform do_arm_raise excited 55
{headmistress_spanking_implement == "paddle":
    I will use a paddle made of mahogany. #55
    /perform talk_behold
    An excellent, hard wood ideal for leaving #56
    bright red welts on the flesh of disobedient slaves. #57
}

{headmistress_spanking_implement == "hand":
    My own hand will do today. #58
    /perform talk_behold aroused 65
    I want to relish the crisp strike of my hand #60
    and the quivering sound of flesh meeting flesh. #61
}

{headmistress_spanking_implement == "crop":
    For today's punishment, I have selected #62
    a slender and rigid crop. #63
    /perform talk_behold
    The pattern of stinging pain that will burst forth #64
    from the skin is exotic to behold! #65
}
/character crowd oos
/wait 2

-> Headmistress_Crowd_Punishment ->
- /character headmistress do_arm_raise excited 55
Listen to that excitement, {player_name}. #67
/perform talk_one_finger aroused 70
your ass will provide a fine demonstration today. #68

- (fortunate)
/perform react_shrug_long happy 45
Are they not fortunate to have one such as you to observe? #69

+[yes]
    ~ addObey()
    /perform talk_consider aroused 45
    Perform well and your punishment marker will be removed. #70

+[no]
    ~ addDisobey()
    /perform talk_annoyed
    It is a shame you do not value the learning of others. #71

+ [timeout] -> Headmistress_Timeout -> fortunate
+ [distracted] -> Headmistress_PayAttention -> fortunate

- /perform idle_sassy amused 50
Friends and students... #72
As always, purrs, moans, and words of encouragement #73
are permitted during the punishment lesson. #74

/character crowd oos

/character headmistress talk_question amused 55
What say you, {isBoy: toad|strumpet}? #79
/wait 1
/character headmistress idle_clasp_hands excited 50
- (aroused)

{shuffle:

    -/perform talk_lean_forward excited 65
    Do you feel their eyes on you? #82

    -/perform talk_consider aroused 45
    Are you throbbing in anticipation? #83

    -/perform idle_skeptical skeptical 45
    Do you want to touch yourself? #84

    -/perform talk_one_finger aroused 75
    Does pain turn you on? #85
}

+ [yes]
    ~ addObey()

    /character crowd cheers
    /character headmistress idle_skeptical amused 45
    So eager for your debasement. #86

    /perform react_nod_little happy 55
    Most pleasing. #87

+ [no]
    ~ addDisobey()
    /perform talk_consider angry 45
    /character crowd boos
    /character headmistress
    A disappointing response for our guests. #88
    /perform talk_serious
    //Rectify this immediately, slave. #226
    //line no longer fits scene - what to do with it?

+ [timeout] -> Headmistress_Timeout -> aroused
+ [distracted] -> Headmistress_PayAttention -> aroused

- /perform talk_question amused 65
Our guests will relish your punishment so much more if your {hasPenis: rod is rigid|slit is slick}. #03

- (get_aroused)

/perform talk_behold excited 55
Shall we appease them? #59

+ [yes]
    -> Headmistress_AcknowledgeYes ->
    ~headmistress_masturbated_for_crowd = true

+ [no]
    -> Headmistress_Unhappy ->
    /perform talk_one_finger skeptical 65
    Sexual excitation makes for a more delightful punishment. #111
    -> Headmistress_Resume ->

+ [timeout] -> Headmistress_Timeout -> get_aroused

+ [distracted] -> Headmistress_PayAttention -> get_aroused

- /perform idle_clasp_hands amused 55
Rub yourself until your {hasPenis: shaft is hard|slit is wet}. #227
-> Headmistress_Crowd_Slut ->

+ [wait 3]

- -> Headmistress_masturbation_comments ->

+ [wait 3]

-Pray continue. #228

+ [wait 3]

- -> Headmistress_masturbation_comments ->

+ [wait 3]

- -> Headmistress_masturbation_comments ->

+ [wait 3]

-/perform do_inspect amused 70
A little more, I think. #229

+ [wait 3]

- -> Headmistress_masturbation_comments ->

- (accept_punishment)

+ [continue]
    -> Headmistress_Crowd_GoodJob ->

- (please)

/character headmistress
{cycle:
    - /perform talk_behold excited 40
    What say you, slave, will your {hasPenis: rod|folds} please our guests? #230

    -/perform idle_pleased amused 50
    Will our esteemed guests be enthralled #80
    by {hasPenis:your turgid bouncing cock?|the glistening of your cunt?} #81

    -/perform talk_question angry 35
    Are you {hasPenis: hard|wet}? #04
}

+ [yes]
    ~ addObey()
    /perform idle_pleased excited 45
    Excellent. Then let us continue. #231

+ [no]
    ~ addDisobey()
    /perform idle_unhappy sad 60
    A disappointment. We shall continue anyways. #232

+ [timeout] -> Headmistress_Timeout -> please

+ [distracted] -> Headmistress_PayAttention -> please

- /perform do_smooth_skirt happy 55
And finally, we come to the heart of the matter. #91



- {inTutorial: -> headmistress_train_spanking(false) -> spank_continue }

/character headmistress react_thankful amused 50
Because I am generous, #234

/perform idle_pleased happy 65
I will offer you a chance to practice first. #106

- (practice)
/perform idle_tease aroused 60
Would you welcome that, {isBoy: mongrel|moppet}? #107
~ headmistress_difficulty = 1

+[yes]
    -> headmistress_train_spanking(false) -> spank_continue

+[no]
    /perform do_inspect amused 50
    An expert, are we? We will see about that. #108
    -> spank_continue

+ [timeout] -> Headmistress_Timeout -> practice

+ [distracted] -> Headmistress_PayAttention -> practice


= spank_continue

/perform do_point_down amused 45
Back onto your hands and knees, slave. #244
/wait 1
/perform idle_clasp_hands amused 55

+ [pose:hands_and_knees]
+ [timeout] -> Headmistress_NoAction -> spank_continue

- /perform idle_pleased happy 50
Let us begin in earnest! #245
/character crowd cheers_loud

+ [wait]

- (spank_begin)

/scene Theatre.HeadmistressAtBack

// /character headmistress walk_to_back_of_desk

/character crowd murmur

/character headmistress idle skeptical 40

// 10, 15, 20
{
- headmistress_spanking_target_count == 10:
    Prepare yourself for ten swats, {player_name}. #127
- headmistress_spanking_target_count == 15:
    Prepare yourself for fifteen swats, {player_name}. #216
- headmistress_spanking_target_count == 20:
    Prepare yourself for twenty swats, {player_name}. #217
- else:
    /logError bad swat count found
}

- (spank)
/character headmistress do_spank_player aroused 75

+ [react-success]
    ~ headmistress_spanking_count++
    ~ headmistress_spanking_break_count++

    {headmistress_spanking_break_count %2 == 0:
        -> Headmistress_GoodSpank ->
    }

    {headmistress_spanking_break_count >= 6:

        // Check mistakes for obey or disobey adjustment
        {headmistress_spanking_mistakes > 0:
            ~ addDisobey()
        - else:
            ~ addObey()
        }

        // Reset the mistake counter.
        ~headmistress_spanking_mistakes = 0

        -> SpankBreak ->
    }
    {headmistress_spanking_count < headmistress_spanking_target_count: -> spank}
+ [react-fail] -> Headmistress_BadSpank -> spank

- -> SpankingFinished


= SpankingFinished
/scene Theatre.HeadmistressAtFront
/character headmistress idle_pleased amused 75

- (feel_better)
Ahhhh. Now, don't you feel better, {player_name}? #128

+[yes]
    /perform idle_tease aroused 60
    ~ addObey()
    That is because you desire #129

    /perform talk_lean_forward aroused 80
    to be an obedient little toy. #130

+[no]
    /perform idle_tease disgusted 45
    ~ addDisobey()
    Too bad. You should have thought of that sooner. #258

+ [timeout] -> Headmistress_Timeout -> feel_better

- /perform talk_behold happy 55
Your ass has blossomed into an objet d'art. #133

/perform idle_pleased amused 65
Consider one demerit paid in full. #145
~ removePunishment()
~ has_learned_spanking = true
/character crowd cheers_loud
/character headmistress

- {obeyed():
    -> GoodEnding
- else:
    -> BadEnding
}


= GoodEnding

/perform react_thankful amused 75
You comported yourself quite well. #131

/perform react_shrug_long happy 65
What a crowd pleaser! #136

/perform idle_clasp_hands amused 45
You should be proud of your performance. #132
/character crowd cheers
/character headmistress
-> FinalEnding


= BadEnding

/perform idle_clasp_hands angry 45
You may have finished enduring your punishment, #140
but your overall performance was quite unsatisfactory. #141
You have a long ways to go. #249
-> FinalEnding


= FinalEnding

~ tries = 0
- (stand_straight)
~ tries++

/perform talk_one_finger skeptical 65
Stand at attention! #257

-> continue ->

+ [pose:raised_chin]
+ { tries < 2 } [timeout] -> stand_straight

+ { tries >= 2 } [timeout] -> Headmistress_NoAction -> stand_straight


- (leave)
{obeyed():
        /perform react_thankful amused 65
        You may descend and return to Mistress Yuki-Onna with my compliments. #139
        /wait 1
        /perform idle_clasp_hands amused 70
    - else:
        /perform idle_skeptical angry 45
        I give you leave to depart. #146
}

+ [move]
+ [timeout] -> Headmistress_NoAction -> leave
//There needs to be a separate knot/stitch for the player not leaving, the NoAction dialogue doesn't feel right

// If in the tutorial, send player off to be tempted by her directly.
- {inTutorial:
    ~ tutorial_hm_intro_complete = true
    -> nega_intro
- else:
    -> exit
}




// Tunnels


= SpankBreak

~ headmistress_spanking_break_count = 0

- /scene Theatre.HeadmistressAtFront


- {cycle:
    -/perform idle_hand_on_hip amused 65
    You may rest back on your heels, slave. #251

    -/perform idle_clasp_hands happy 65
    Sit upright for your fans, slave. #252

    -/perform idle_skeptical happy 55
    Raise yourself up and sit upon your heels, slave. #253
    }

~ tries = 0
- (kneel_break)
~ tries++

+ [pose:kneel]
+ { tries < 2 } [timeout] -> kneel_break

+ { tries >= 2 } [timeout] -> Headmistress_NoAction -> SpankBreak

-
/wait 3
{inTutorial:
    { cycle:
        // - -> break01 ->
        // - -> break02 ->
        - -> break03 ->
        //- -> break04 ->
    }
- else:
    { shuffle:
        - -> break01 ->
        - -> break02 ->
        - -> break03 ->
        //- -> break04 ->
    }
}


+ [continue]

- {cycle:
    -/perform talk_lean_forward amused 80
    Back on your hands and knees, slave. #254

    -/perform do_point_down aroused 60
    Resume your spanking position, slave. #255

    -/perform talk_question excited 40
    Present your ass, {isBoy: runt|{isGirl:strumpet|runt}}. #256
}

+ [pose:hands_and_knees]
+ [timeout] -> Headmistress_NoAction -> SpankBreak

- /scene Theatre.HeadmistressAtBack
->->


= break01

/perform talk_thoughtful
Have you been keeping count, {isBoy: toad|strumpet}? #10

+[yes]
    ~ addObey()
    /perform react_nod_little amused 65
    Let's see how well. #18

    /perform idle_clasp_hands skeptical 70
    Are we up to seven now? #21

    ++[yes]
        /perform talk_one_finger angry 55
        Tsk. Tsk. #22

        /perform talk_two_fingers angry 35
        We are only at six.  #23

        /perform do_snap_fingers disgusted 60
        Time to start over! #24

        /perform idle_skeptical angry 65
        I suggest you remember to count this time, foible. #26
        ~ headmistress_spanking_count = 0
        -> position

    ++[no]
        /perform talk_lean_forward amused 65
        Paying attention, are we? #27
        I hope you know we are only at six. #28
        -> Headmistress_punishment_resumes ->
        ->->

+[no]
~ addDisobey()
    /perform talk_behold disgusted 75
    Do you hear that, my students? #31

    /perform talk_question sad 55
    This one has no breeding or experience. #32

    /perform talk_annoyed angry 65
    It fails to count. #33

    /perform do_snap_fingers amused 55
    We shall start over! #34

    /perform talk_lean_forward skeptical 55
    This flaw I shall address in the future. #35
    ~ headmistress_spanking_count = 0
    -> Headmistress_punishment_resumes ->
    -> position

+ [timeout] -> Headmistress_Timeout -> break01

+ [distracted] -> Headmistress_PayAttention -> break01

- (position)

- {cycle:
    -/perform talk_lean_forward amused 80
    Back on your hands and knees, slave. #254

    -/perform do_point_down aroused 60
    Resume your spanking position, slave. #255

    -/perform talk_question excited 40
    Present your ass, {isBoy: runt|{isGirl:strumpet|runt}}. #256
}

+ [pose:hands_and_knees]
+ [timeout] -> Headmistress_NoAction -> SpankBreak

- -> Headmistress_GoodJob ->
-> spank_begin

= break02

/perform talk_serious skeptical 70
Every now and then, the object of correction #175
must be given a moment to compose themself. #176

/character crowd murmur
/character headmistress talk_one_finger amused 65
However, this time should not be squandered with mere rest. #177

/perform tease_bend_over aroused 75
Reach down and stroke your {hasPenis: shaft|folds}. #178

/character crowd gasps
/character headmistress

/perform idle_sassy aroused 50
Punishments must be associated with sensual excitation. #179

- (repeat)

Does it feel good to touch yourself, {isBoy: toad|strumpet}? #180
//need moans or some other sexual sound during this scene
+[yes]
    ~ addObey()
    /perform react_nod_little excited 45
    As it should. #181
    You may continue until I instruct you otherwise. #182

+[no]
    ~ addDisobey()
    /perform react_scoffs skeptical 70
    What a shame. Do it anyways. #183

+ [timeout] -> Headmistress_Timeout -> repeat

+ [distracted] -> Headmistress_PayAttention -> repeat

- /perform idle_tease aroused 45
+[wait:5]
- -> Headmistress_masturbation_comments ->

+[wait:5]
- -> Headmistress_masturbation_comments ->

+[wait:5]
- -> Headmistress_punishment_resumes ->

->->


= break03

/perform do_smooth_skirt amused 55
Rise and present your ass that my students may inspect your progress thus far. #36

+ [pose:present_ass]
    -> Headmistress_AcknowledgeYes ->

+ [timeout] -> Headmistress_NoAction -> break03

/character crowd gasps
/character headmistress

- /perform talk_one_finger excited 75
Remember to intersperse strikes with caresses. #184

/perform talk_behold happy 55
The inflamed flesh is delightfully sensitive #185
and should be titillated as much as possible. #186

/perform idle_tease aroused 85
The tantalizing drag of fingers on welted skin #187
regularly sends shivers through the subject. #188

- (repeat)

/perform idle_sassy excited 56
Would you enjoy that, {player_name}? #189

+ [yes]
    /perform idle_unhappy disgusted 45
    ~ addObey()
    Sorry, shivers are reserved for reward spankings only. #190

+ [no]
    /perform idle_tease amused 55
    ~ addDisobey()
    Good. Then you will be pleased to know that #191
    such kind treatment is reserved for reward spankings only! #192

+ [timeout] -> Headmistress_Timeout -> repeat

+ [distracted] -> Headmistress_PayAttention -> repeat

- -> Headmistress_punishment_resumes ->

->->


= break04

/perform talk_lean_forward happy 55
Perhaps our little foible here needs a carrot #193
dangled in front of {isBoy: him|{isGirl:her|them}}. #194

/character crowd murmur
/character headmistress talk_behold excited 50
Great rewards are bestowed upon the most worthy. #197

/perform talk_question skeptical 70
What do you think, {isBoy: runt|poppet}? #198

- (treatment)

/perform idle_tease aroused 55
Shall I show you the kind of treatment #199
good boys and girls earn? #200

+[yes]
    ~ addDisobey()
    /perform tease_caress_breasts_large amused 60
    -- (soft)
    Do you imagine they are soft, {player_name}? #201

    ++[yes]
        ~ addObey()
        /perform tease_caress_breasts_large aroused 85
        Mmmm. Very soft indeed. #202

    ++[no]
        ~ addDisobey()
        /perform idle_unhappy angry 65
        --- (coarse)
        Do you imagine they are coarse and uneven, #203
        just like your hindquarters? #204

        +++[yes]
            ~ addDisobey()
            /character crowd gasps
            /character headmistress idle_unhappy_stomp disgusted 75
            A brazen response. #205

            /perform react_point_at_player_angry angry 80
            Another punishment has been added to your band. #206
            ~ addPunishment()

        //player should either receive another punishment stone or an additional 5 swats

        +++[no]
            ~ addDisobey()
            /perform react_shrug_long amused 45
            As I surmised. #207

            /perform react_scoffs skeptical 50
            You are a buffoon who does not know to think before acting. #209

        +++ [timeout] -> Headmistress_Timeout -> coarse

        +++ [distracted] -> Headmistress_PayAttention -> coarse

    ++ [timeout] -> Headmistress_Timeout -> soft

    ++ [distracted] -> Headmistress_PayAttention -> soft

+[no]
    ~ addObey()
    /perform idle_tease amused 55
    My my. A surprisingly chaste response #210
    from one whose {hasPenis: shaft|mound} is throbbing so fully. #212

    /perform idle
    /perform idle_skeptical amused 65
    But a delightful one. #213

    /perform talk_lean_forward amused 50
    You have not earned a reward, #214
    so why bestow a taste upon you. #215

+ [timeout] -> Headmistress_Timeout -> treatment

+ [distracted] -> Headmistress_PayAttention -> treatment


- -> Headmistress_punishment_resumes ->

->->
