VAR embarassed = false

=== headmistress_reward_exhibitionism ===
-> select_gender_check ->

~ setScene(reward_scene, Headmistress, "headmistress_reward_exhibitionism", -> headmistress_reward_exhibitionism)
/scene TheatreOpen.Ready
/music headmistress
/ambient crowd
/character headmistress idle_tease happy 50

{shuffle:
    - For your reward, you shall pleasure yourself for the crowd. #01
    - Your reward will be to titillate the crowd with your orgasm. #02
    - You will have the reward of masturbating for your audience, {isBoy: runt|moppet}. #03
}
/perform do_arm_raise
/character crowd cheers_loud
~ Headmistress_RaiseCurtains()


/wait 2

{shuffle:
    - /character headmistress react_shrug_long  happy 40
    We have a full compliment of viewers today. #04
    /character headmistress react_thankful aroused 50
    Eager students and connoisseurs of the flesh. #05
    - /character headmistress react_shrug_long excited 50
    Every opportunity to perform for an audience must be siezed. #06
    /character headmistress talk_thoughtful angry 50
    This is a true test of your willing obedience. #07
}

- 
/wait
/perform talk_consider skeptical 30
{shuffle:
    - You purpose is to serve with your pleasure, {player_name}. #08
    - Your existence is always dependent upon on your performance, {player_name}. #09
    - Delight and remain pleasing, {player_name}. #15
}

- (excited)
/wait 
/perform talk_question amused 50
Are you excited to be rewarded in this way, {isBoy: heathen|harlot}? #16

/perform idle_pleased happy 50

+[yes]
    /character crowd oos
    ~ addObey()
    /character headmistress idle happy 50
   -> Headmistress_AcknowledgeYes ->
   --/character headmistress react_thankful
    {shuffle:
        - Such hedonistic exhibitionism will please <i>Her</i>. #17
        - Your audience shall delight in feasting their eyes upon your body. #18
        - Displaying your orgasm for the benefit of others is your duty. #19
    }
    /wait 2
    -> nervous_tease
  
+ [no]
    /perform idle
   -> nervous_consent_check
  
+ [timeout] -> Headmistress_Timeout -> excited
+ [distracted] -> Headmistress_PayAttention -> excited
 

= nervous_consent_check

/perform react_shrug_long amused 75
Do you tremble with nervous embarassment, foible? #20

/perform idle neutral

+ [yes]
    /character crowd jeers
    /character headmistress react_thankful amused 
    ~ embarassed = true
    Ahhh. Such delicate sensibilities. #21
    
    /character headmistress idle neutral
    Nevertheless. This is your reward. #22
    -- (continue_consent)
    /perform talk_question scared 30
    Do you desire to continue thusly? #23

    ++ [yes]
        /character crowd oos
        ~ addObey() 
        /character headmistress idle_tease aroused 50
        Oh. This will be a treat. #24
        Your embarrassment will only enhance the succor of your release. #25
        
        -> ready
    
    ++ [no]
        /character crowd boos
        /character headmistress do_smooth_skirt angry 50
        If your discomfort overwhelms you,  #26
        /perform react_scoffs
        then this reward is not for you. #27
        You are dismissed, bauble. #28
        
        /character headmistress idle_clasp_hands
        Keep your reward and return when you are ready. #29
        -> exit

    ++ [timeout] -> Headmistress_Timeout -> continue_consent
    ++ [distracted] -> Headmistress_PayAttention -> continue_consent
    
+ [no]
    /perform do_smooth_skirt angry 50
    If your discomfort overwhelms you,  #26
    /perform react_scoffs
    then this reward is not for you. #27
    /perform talk_question neutral
    Do you desire to continue thusly? #23
    /perform idle
        
    ++ [yes]
    /character crowd cheers_short
     ~ addObey() 
    /character headmistress react_nod_big happy 50
    -> Headmistress_AcknowledgeYes ->
    /perform idle_clasp_hands
    
    -> Headmistress_Resume ->
    -> ready
    
    ++ [no]
        /character crowd boos
        /character headmistress react_scoffs angry 50
        You are dismissed, bauble. #28
        
        /perform idle_clasp_hands neutral
        Keep your reward and return when you are ready. #29
        -> exit

    ++ [timeout] -> Headmistress_Timeout -> continue_consent
    ++ [distracted] -> Headmistress_PayAttention -> continue_consent

+ [timeout] -> Headmistress_Timeout -> nervous_consent_check
+ [distracted] -> Headmistress_PayAttention -> nervous_consent_check


= nervous_tease

/perform react_shrug_long amused 75
Do you tremble with nervous embarassment, foible? #20
 
/perform idle neutral
 
+[yes]
    /character crowd cheers_short
    /character headmistress react_thankful aroused 75
     ~ embarassed = true
    How utterly delightful! #33
    /perform talk_behold
    This will quench <i>our</i> refreshment even moreso. #34
    
    -> ready
   
 +[no]
    /character crowd cheers_short
    /character headmistress do_arm_raise happy 75
    A true performer, students! #30
    Our subject enjoys being on display. #31
    
    -> ready
    
+ [timeout] -> Headmistress_Timeout -> nervous_tease
+ [distracted] -> Headmistress_PayAttention -> nervous_tease


= load_gave_reward
/scene TheatreOpen.Ready
/music headmistress
/ambient crowd
-> prepare


= prepare

/character headmistress idle_clasp_hands skeptical 50
You have a choice, slave. #37
/perform talk_serious
You may perform upright or on your knees. #38
/perform talk_question scared 50
Which will you choose? #39

/perform idle neutral

+ [pose:present_stand]
        /character crowd oos
        /character headmistress do_quick_bow neutral
        {isBoy: He|{isGirl: She|They}} shall perform for you standing. #40
        
        /character headmistress talk_behold
        Such an excellent position for displaying {isBoy: his|{isGirl:her|their}} full physique! #41
    
+ [pose:present_kneel]
        /character crowd oos
        /character headmistress do_arm_raise amused 50
        {isBoy: He chooses|{isGirl: She chooses|They choose}} to perform on {isBoy: his|{isGirl: her|their}} knees. #42
       
        /character headmistress react_thankful angry 50
        Such a heady position of submission and subservience! #43
 
+ [timeout] -> Headmistress_NoAction -> prepare

// NOTE: The [distracted] choice shouldn't be used when posing as players often have to look away to get into poses. - deviant
// + [distracted] -> Headmistress_PayAttention -> prepare
 
- (instructions)
/perform idle_clasp_hands neutral
You will be allowed to cum today, slave, #44
/character crowd gasps
/character headmistress talk_one_finger skeptical 50
but you must seek approval first. #45

/perform talk_emphatic_one_hand neutral
When your orgasm seeks escape, <b>"nod"</b> your head for permission. #46

/perform idle_cocks_head_to_left skeptical 50
Do you understand, {isBoy: simpleton|ninny}? #47

/perform idle neutral

+ [yes]
    /character crowd cheers_short
    /character headmistress react_thankful excited 50
    Extraordinary! #48
    /perform tease_caress_breasts_large aroused 75
    Then we shall delight in witnessing your {hasPenis: seed|cream} paint the floor! #49
    
+ [no]
    /character crowd boos
    -- (instructions_check)
    /character headmistress talk_annoyed angry 50
    You will communicate when you are ready to cum. #50
    /perform react_nod_little
    <b>"Nod"</b> your head "yes" to ask permission to orgasm. #51
    /perform react_scoffs
    Cum when I give you leave. #52
    
    /perform talk_question angry 75
    Is that clear? #53

/perform idle neutral

    ++ [yes]
        /character crowd cheers_short
        /character headmistress do_arm_raise happy 50
        Extraordinary! #48
        /perform talk_behold aroused 50
        Then we shall delight in witnessing your {hasPenis: seed|cream} paint the floor! #49
        
    ++ [no] -> Headmistress_CheckIfInterested -> instructions_check
    ++ [timeout] -> Headmistress_Timeout -> instructions_check
    ++ [distracted] -> Headmistress_PayAttention -> instructions_check
    
+ [timeout] -> Headmistress_Timeout -> instructions
+ [distracted] -> Headmistress_PayAttention -> instructions

- (tease)
//check to see if they are embarassed or not. select tease text for their 'state'
{embarassed: 
    -> tease_embarassed
    - else: 
    -> tease_slut
}

- (tease_slut)
/wait
/character headmistress talk_question excited 50
{shuffle:
    - Time to show us just how much of a slut you are. #56
    - We expect quite a performance from a self-proclaimed whore. #57
    - Show us your finest exhibitionist frigging, {isBoy: muttonmonger|trollop}! #58
}

- (tease_embarassed)
/wait
/character headmistress tease_caress_breasts_large aroused 75
{shuffle:
    - We are about to feast upon your burning visage, {isBoy: duckling|mouse}! #59
    - Your shame shall be our succor! #60
    - It is time to defile yourself for our pleasure, tribute. #61
 }
 /character crowd oos

-> begin

= ready

/wait 
/character headmistress talk_behold happy 50
Are you ready, {isBoy: toad|strumpet}? #62

/perform idle neutral

+ [yes]
    /character crowd cheers_short
    /character headmistress react_head_toss_casual
    Exquisite. #63
    
    
+ [no]
    /character crowd gasps
    /character headmistress talk_question skeptical 50
    We have a delay! #64
    
    -> Headmistress_CheckIfInterested -> ready
    
+ [timeout] -> Headmistress_Timeout -> ready
+ [distracted] -> Headmistress_PayAttention -> ready

- (give_reward)

/character Headmistress do_hand_out neutral
{shuffle:
    - Time to turn over your Bloodstone, speck. #65
    - Present your wrist that I may remove your Bloodstone. #66
    - Thrust your wrist forward to spend one of your Bloodstones. #67
}

/perform idle neutral

+ [pose:present_wrist_reward]
+ [no] -> Headmistress_CheckIfInterested -> give_reward
+ [timeout] -> Headmistress_NoAction -> give_reward
+ [distracted] -> Headmistress_PayAttention -> give_reward

- /perform do_swipe_hand happy 50
-> Headmistress_AcknowledgeYes->
~ removeReward(-> load_gave_reward)
--> prepare


= begin

- /notify title="<color=red>Pro Tip!</color>" text="Early Access: Currently, this scene is meant for you to perform <b>in real life</b>! <br><br>Future: An in-game masturbation scene is coming soon!!<br><br>Understood?" #note01

+ [yes]

- /character headmistress idle_clasp_hands neutral
-> Headmistress_AcknowledgeYes ->

You may {hasPenis: wrap your hand around your rod|slip your fingers between your nether lips} and reward yourself. #68

-> Headmistress_AcknowledgeYes ->

- (masturbate)

+ [ wait 2]

-> RandomCommentLoopWithYes(-> masturbation_comment, -> cum, 3, 6, 4, 7) ->

-> Headmistress_GoodJob ->

-{shuffle:
    --> question ->
    --> command ->
    --> keep_going ->
    }

- (cum)
+ [wait 3]

/character headmistress talk_lean_forward amused 75
{shuffle:
    - Do you tremble with the need to release, {isBoy: heathen|wretch}? #69
    - Are you pulsing with the desire to orgasm, slave? #70
    - Is it time to {hasPenis: explode|squirt}, trinket? #71
}   

-/perform idle neutral

+ [yes]
    /character crowd gasps
    -> orgasm
    
+ [no]
    /character crowd boos
    /character headmistress do_arm_raise excited 50
    Then continue masturbating yourself for our enjoyment! #72
    -> masturbate
        
+ [timeout] -> Headmistress_Timeout -> cum
+ [distracted] -> Headmistress_PayAttention -> cum



= orgasm

/character headmistress
/wait 2 

{shuffle:
    - /perform talk_behold amused 75
    - /perform do_point_down excited 75
    - /perform react_thankful aroused 75
    }
    
{shuffle:
    - You may release your pent up passion, slave. #73
    - You may {hasPenis: spew your load|cream the floor}, whore. #74
    - I give you leave to cum, {isBoy: manwhore|soiled dove}. #75
}

/perform idle_clasp_hands neutral
/character crowd cheers
/character Headmistress

- /wait 5

{inTutorial:
    ~ tutorial_hm_bonus_complete = true
}   

- (finish)

/perform idle_pleased scared 30
{stopping:
    - Are you spent, {player_name}? #76
    - Have you finished releasing, {isBoy: toad|strumpet}? #77
    - Such a hedonist! Are you quite through with your orgasm? #78
}
/character headmistress idle neutral


+ [yes]
    /character crowd claps
    /character headmistress react_nod_little
   -> Headmistress_AcknowledgeYes ->
   
+ [no]
    We shall wait for you to finish! #35
    -- (reminder)
    "Nod" your head when you are spent. #36
    
    ++ [yes]
    /character crowd claps
    /character headmistress react_nod_little
    -> Headmistress_AcknowledgeYes ->
    --> praise

    ++ [timeout 10] -> reminder
    
+ [timeout] -> reminder

- (praise)
/character headmistress talk_behold happy 75
- {shuffle:
    - Such a bold performance, foible! #79
    - A fine performance, slave. #143
}

/character headmistress do_inspect sad 30
I sense that you are spent with that reward. #80

- (enjoy)
/perform idle_tease aroused 75
Did you enjoy yourself? #81

/perform idle neutral

+ [yes]
    /character crowd oos
    /character headmistress react_thankful aroused 50
    Pleasure breeds pleasure. #82
    This is good. #83

+ [no]
    /character crowd jeers
    /character headmistress react_shrug_short sad 50
    A pity. #84
    /perform react_scoffs happy 30
    Nevertheless, your performance fed us well, {isBoy:speck|poppet}. #85
    
+ [timeout] -> Headmistress_Timeout -> enjoy
+ [distracted] -> Headmistress_PayAttention -> enjoy
    
-
{inTutorial: 
    /perform do_you_leave neutral
    I give you leave to... #86
    -> surprise_interrupt
}

/perform do_you_leave neutral
I give you leave to depart. #87
/character crowd cheers

-> exit

= question

/wait 2

{embarassed:
    /perform idle_tease amused 50
    {shuffle:
        - Are you mortified to perform before your fans, slave? #88
        - Is your skin moist with perspiration from being seen by so many? #89
        - Are your knees weak with nervousness from performing for others, foible? #90
    }
    - else:
        /perform talk_behold excited 50
        {shuffle:
            - Are you having fun performing for our guests?  #91
            - Enjoying your reward, {player_name}? #92
            - Does a thrill of excitement run through you at being watched by so many? #93
        }
}

/perform idle neutral

+ [yes]
    /character crowd cheers_short
    /character headmistress react_nod_little
    -> Headmistress_AcknowledgeYes ->

    {embarassed:
        /perform talk_one_hand happy 30
         {shuffle:
            - We can tell, little one. #94
            - Your torment is our delight! #95
        }
    - else:
        /perform talk_behold excited 50
        {shuffle:
            - Your exhibitionist's soul is proudly on display. #96
            - A true performance artist. #97
        }
    }
      Then surely we must continue! #98
      
+ [no]
    /character crowd jeers
    /character headmistress react_scoffs angry 50
    -> Headmistress_Unhappy ->
    
+ [timeout] -> Headmistress_Timeout -> question
+ [distracted] -> Headmistress_PayAttention -> question
    
-->->

= keep_going
/perform talk_thoughtful skeptical 50
{ cycle:
    - Would you like to keep going, {isBoy:toad|strumpet}? #10
    - Does our little {isBoy:runt|poppet} need more time? #11
    - Are you going to ask our guests to continue waiting? #12
    - Still working that {hasPenis:shaft|slit}? #13
    - Our {isBoy:toad|strumpet} needs more time. Yes? #14
} 

+ [yes]
    /character crowd oos
    /character headmistress react_scoffs excited 50
    Then keep going! #99
    -> masturbate

+ [no]
    /character crowd gasps
    /character headmistress idle_clasp_hands scared 30
    Are you ready to cum, then, slave? #100
    
        ++ [yes]
            /character crowd cheers
            /character headmistress
            -> orgasm
            
        ++ [no]
            /perform react_shrug_long disgusted 50
            Yet you wish to stop. #101
            Do you want to continue with your reward, slave? #102
            
            +++ [yes]
                /perform idle_cocks_head_to_left amused 50
                -> Headmistress_Resume ->
                -> masturbate
                
            +++ [no]
                /character crowd boos
                /wait
                /character headmistress react_nod_big neutral
                Then I give you leave to depart, foible. #103
                /perform talk_one_finger
                One reward has been deducted from your wrist. #104
                ~removeReward(-> entry_hub)
                -> exit
            
+ [timeout] -> Headmistress_Timeout -> keep_going
+ [distracted] -> Headmistress_PayAttention -> keep_going

->->

= command

/wait 2
{shuffle:
    - /perform do_arm_raise excited 50
    - /perform talk_behold happy 50
    - /perform tease_caress_breasts_large aroused 50
    - /perform idle_tease amused 50
    }
    
{shuffle:
    - Pause and {hasPenis: cup and raise your sac|spread your folds} that all may enjoy the view. #105
    - {hasPenis: Squeeze harder as you stroke|Slide a finger into yourself as you rub your slit}. #32
    - Moan loudly as you {hasPenis: stroke|finger} yourself. #106
    - Pinch your nipples with your free hand, {isBoy: manwhore|harlot}. #107
    - Sway your hips for the crowd, strumpet! #108
}

{shuffle:
    - /character crowd gasps
    - /character crowd oos
    - /character crowd cheers_short
}
    
/character Headmistress
/wait 4

- (display)
/perform talk_consider scared 30
Does it feel good to be put on display, foible? #109

+ [yes]
    /perform talk_behold amused 50
    {embarassed:
        How hard this must be for you then, slut.  #110
        To enjoy being seen even as you flush with shame. #111
       - else: 
            As it should for one who enjoys whoring themself before an audience. #112
    }
    

+ [no]
    /perform react_disappointed disgusted 50
    What a shame. #113
    This is your reward, after all. #114
    
+ [timeout] -> Headmistress_Timeout -> display
+ [distracted] -> Headmistress_PayAttention -> display


-->->

= masturbation_comment

TODO: replace Headmistress crowdcomment (obsolete) with general

{embarassed:
    { shuffle:
        - -> Headmistress_masturbation_comments ->
        - -> embarassment_comment ->
        - -> Headmistress_Crowd_Embarrassed ->
        - -> Headmistress_Crowd_General ->
    }
    - else:
    { shuffle:
        - -> Headmistress_masturbation_comments ->
        - -> slut_comment ->
        - -> Headmistress_Crowd_Slut  ->
        - -> Headmistress_Crowd_General ->
    }
}

->->

= embarassment_comment

{shuffle:
    - /perform talk_one_finger excited 50
    - /perform do_inspect happy 50
    - /perform react_shrug_long skeptical 50
    - /perform idle_pleased amused 50
    - /perform idle_tease aroused 50
    }
    
{shuffle:
    - Note the cheeks flush with distress, ladies and gentlemen! #115
    - Such a blushing harlot. #116
    - {isBoy: His|{isGirl:Her|Their}} shame is palpable! #117
    - Enjoy how mortified {isBoy: he is|{isGirl:she is|they are}}, students. #118
    - Let us examine whether {isBoy: he grows|{isGirl:she grows|they grow}} flustered in {isBoy: his|{isGirl:her|their}} performance! #119
    - Subjecting <i>Her</i> slaves to such demeaning rewards is so gratifying! #120
    - Such self-conscious debasing delivers so much passion! #121
}

->->

= slut_comment

{shuffle:
    - /perform talk_one_finger excited 50
    - /perform do_arm_raise happy 50
    - /perform tease_caress_breasts_large amused 50
    - /perform idle_pleased skeptical 50
    - /perform idle_tease aroused 50
    }
    
{shuffle:
    - We have the privilege of witnessing a true pervert debase {isBoy:himself|{isGirl:herself|themself}}! #122
    - Such a captivating whore for our attention! #123
    - Observe how a deviant reprobate thrives before an audience! #124
    - What debaucherous perversion we feast upon. #125
    - Enjoy how unabashedly our harlot {hasPenis: strokes the shaft|rubs the slit}. #126
    - Unfettering the passions of <i>Her</i> slaves is pure ecstasy! #127
    - This {isBoy: muttonmonger|painted hussy} is exactly where {isBoy: he belongs|{isGirl: she belongs|they belong}}. #128
}

->->

/character headmistress
->->

= surprise_interrupt

TODO: Vibrate controllers with rumble?
/sound goddess_interruption
/ambient none
/wait 2
/perform talk_behold aroused 75

Ahhh. <i>She comes</i>. #140
/perform react_thankful excited 50
What a treat you have earned, trinket. #141

/perform idle
/music goddess
/wait 1
/scene None
I will see you again, moppet. #142
-> goddess_monitoring_intro.pleased
