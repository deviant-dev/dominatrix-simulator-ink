//should comment on how many rewards / punishments they have and notice when they have a mega reward


=== headmistress_hub ===

-> select_gender_check ->

~ setHub(Headmistress, "headmistress_hub", -> headmistress_hub)

{inTutorial && tutorial_step != at_hm_intro:
    ~ tutorial_step = at_hm_bonus
    ~ hm_bonus_shortcut = true
    ~ punishments = 0
    ~ rewards = 1
}

{deviation == exhibitionism:
    ~ punishments = 0
    ~ rewards = 1
}

/scene Theatre.Hub
/music headmistress
/ambient none
/character headmistress idle_clasp_hands amused 45

+ [move:Exit] -> exit
+ [move:Approach] -> apology_check(-> hello, -> exit)


= hello

/wait

{ headmistress_regard >= 10:
    /perform react_thankful happy 55
    I am pleased to see you, bauble. #01
- else:
    {shuffle:
        - /perform talk_behold amused 45
        Greetings, {isBoy: peon|{isGirl:moppet|peon}}. #02

        - /perform talk_one_hand skeptical 75
        Welcome back, flawed one. #03

        -/perform idle_skeptical excited 40
        I see you cannot stay away, trinket. #04

        -/perform idle_bored amused 55
        You continue to return. #05
    }
}

{
    // Skip tasks and go straight for hand signals if in hand_signals deviation
    - deviation == hand_signals: -> check_task
    - punishments > 0: -> check_punishment
    - rewards >= 10: -> check_reward_mega
    - rewards > 0 : -> check_reward
    - else: -> check_task
}


= check_punishment

/character headmistress talk_lean_forward skeptical 60
Are you here to pay your punishment? #06
+ [yes] -> choose_punishment
+ [no]

- No? #07

{
    - punishments < 10 && rewards < 10:
        -> check_task
    - punishments >= 10:
        << You have too many punishments for a task, I'm afraid. >>
    - rewards >= 10:
        << You have too many rewards for a task, I'm afraid. >>
        << Pay off those punishments and you might get yourself something nice. >>
}

-> goodbye


= check_reward_mega

<< Oh my. A full band of rewards, hmm? >>

{headmistress_regard < 10:
    << It's too bad you haven't pleased me more or I might have something special for you. >>
    << But I can still give you a reward if you spend just one. >>
    -> check_reward
}

{inChastity:
    << Too bad you are in chastity or we could truly enjoy ourselves. >>
    -> check_reward
}

<< And you've pleased me quite a lot. >>
<< Give me all 10 and I'll see you get something very special indeed. >>
<< Would you like that? >>

+ [yes] -> choose_reward_mega
+ [no] -> check_reward


= check_reward

/character headmistress idle_pleased amused 50
Are you here to receive your reward? #08

+ [yes] -> choose_reward
+ [no]

- /perform react_shrug_short scared 66
No?  #09

/perform talk_either_or disgusted 54
Abstaining from pleasure, I see. #10

/perform do_inspect amused 40
How restrained. #11

{
    - rewards < 10:
        -> check_task

    - rewards >= 10:
        << You have too many rewards for a task, I'm afraid. >>
        << An over-achiever, hmm? >>
        << You'll just have to endulge yourself before performing any more tasks. >>
}

{ inChastity:
    << Why don't you see the Goddess about having that chastity device removed? >>
}

-> goodbye


= check_task

{ inTutorial && (deviation == tutorial || deviation == full_game): -> goodbye }
{ deviation == exhibitionism: -> goodbye }

{ punishments == 0 && rewards == 0:
    /perform do_shake_finger amused 55
    No rewards or punishments, huh? #12

    /perform do_arm_raise excited 60
    You must be here to serve me! #13
- else:
    /perform idle_pleased amused 40
    Shall I give you a task then? #14
}
/perform idle_pleased amused 

+ [yes]
    -> Headmistress_AcknowledgeYes ->
    -> choose_task

+ [no] -> goodbye


= choose_punishment

-> transition ->

// Eventually we'll have the game randomly pick here.
{ shuffle:
    - -> headmistress_punishment_spanking -> exit
}
//-> headmistress_punishment_flogging -> exit


= choose_reward_mega

<< Let's get intimate. >>
{shuffle:
    - -> headmistress_rewardmega_placeholder
}

= choose_reward

/character headmistress
/emotion happy 65

{shuffle:
    - /perform react_thankful aroused 70
    Then a reward you shall have. #15

    -/perform do_smooth_skirt happy 45
    It is so pleasing when trinkets have earned a reward. #16

    -/perform react_head_toss_casual skeptical 55
    Rewards are preferable to punishments. #17
}

-> transition ->

{inTutorial: -> headmistress_reward_exhibitionism }
{deviation == exhibitionism: -> headmistress_reward_exhibitionism }

// Eventually we'll have the game randomly pick here.
{shuffle:
    // - -> headmistress_reward_cunnilingus
    - -> headmistress_reward_exhibitionism
}


= choose_task

-> transition ->

// Always do the hand signal task in the hand_signals deviation.
{deviation == hand_signals: -> headmistress_task_handsignals -> exit}

// Eventually we'll have the game randomly pick here.
{ shuffle:
    - -> headmistress_task_handsignals ->
}

- -> exit


= transition

{shuffle:
    - /perform do_clap_clap happy 50
    Places, everyone! #20
    - /perform do_clap_clap happy 50
    On your marks! #21
}
->->


= goodbye

/character headmistress react_resigned sad 70
Then I have nothing for you. #18

/perform do_you_leave_right disgusted 65
Begone. #19
/wait 1

-> exit
