TODO: Comment on how many rewards / punishments the player possesses
TODO: Modify flow when the conditions for a mega_reward/_punishment are fulfilled




=== headmistress_hub ===
-> select_gender_check ->
~ setHub(Headmistress, "headmistress_hub", -> headmistress_hub)
{inTutorial && tutorial_step != at_hm_intro:
    ~ tutorial_step = at_hm_bonus
    ~ hm_bonus_shortcut = true
    ~ punishments = 0
    ~ rewards = 1
}

{deviation == exhibitionism:
    ~ punishments = 0
    ~ rewards = 1
}

/scene Theatre.Hub
/music headmistress
/ambient none
/character headmistress idle_clasp_hands amused 45

+ [move:Approach] -> apology_check(-> hello, -> exit)
+ [move:Exit] -> exit


= hello

/wait

{ headmistress_regard >= 10:
    /perform talk_behold happy 55
    I am pleased to see you, bauble. #01
- else:
    {shuffle:
        - /perform do_quick_bow amused 45
        Greetings, {isBoy: peon|{isGirl:moppet|peon}}. #02

        - /perform talk_one_hand skeptical 75
        Welcome back, flawed one. #03

        -/perform do_summon_player excited 40
        I see you cannot stay away, trinket. #04

        -/perform idle_clasp_hands amused 55
        You continue to return. #05
    }
}

{
    // Skip tasks and go straight for hand signals if in hand_signals deviation
    - deviation == hand_signals: -> check_task
    - punishments > 0: -> check_punishment
    - rewards >= 10: -> check_reward_mega
    - rewards > 0 : -> check_reward
    - else: -> check_task
}


= check_punishment

/character headmistress talk_lean_forward skeptical 60
Are you here to pay your punishment? #06
+ [yes] -> choose_punishment
+ [no]

- No? #07

{
    - punishments < 10 && rewards < 10:
        -> check_task
    - punishments >= 10:
        /perform react_dismissive angry 30
        Tut tut! #22
        You have far too many punishments, to perform a task. #23
    - rewards >= 10:
        /perform do_smooth_skirt amused 40
        Silly creature. #24
        You cannot perform a task #25
        when your wrist is full of rewards. #26
        The only choice you have is to #27
        pay off a punishment. #28
}

TODO: Add double-check for punishment

-> goodbye


= check_reward_mega
/perform talk_gesture happy 75
How delightful. #29
A full bracelet of Bloodstones! #30

{headmistress_regard < 10:
/perform react_thankful sad 60
    Sadly, you haven't pleased me enough to redeem them all. #31
    Visit me more often to earn enough regard #32
    to spend them all at once. #33
    /perform do_smooth_skirt amused 50
    You may, however, enjoy a simple reward, if you wish. #34
    -> check_reward
}

{inChastity:
    /perform talk_lean_forward amused 65
    Alas, you are in chastity. #35
    Utter pleasure must wait for another time. #36
    Perhaps a smaller reward will do. #37
    -> check_reward
}

/perform idle_clasp_hands happy 50
You have been performing quite admirably. #38

/perform idle_hand_out excited 50
Hand over all your Bloodstones, #39
and you shall be greatly rewarded. #40

/perform idle_tease aroused 30
What say you, poppet? #41

+ [yes] -> choose_reward_mega
+ [no] -> check_reward


= check_reward

/character headmistress idle_pleased amused 50
Are you here to receive your reward? #08

+ [yes] -> choose_reward
+ [no]

- /perform react_shrug_short scared 66
No?  #09

/perform do_inspect amused 40
Abstaining from pleasure, I see. #10
How restrained. #11

{
    - rewards < 10:
        -> check_task

    - rewards >= 10:
        /perform idle_skeptical skeptical 50
        Well, you have too many rewards #42
        for a task, I'm afraid. #43
        An over-achiever, hmm? #44
        You'll just have to indulge yourself before performing any more tasks. #45
}

{ inChastity:
    /perform react_scoffs disgusted 30
    Seek the Goddess if you wish #46
    to have your chastity device removed. #47
}

-> goodbye


= check_task

{ inTutorial && (deviation == tutorial || deviation == full_game): -> goodbye }
{ deviation == exhibitionism: -> goodbye }

{ punishments == 0 && rewards == 0:
    /perform do_shake_finger amused 55
    No rewards or punishments, huh? #12

    /perform do_arm_raise excited 60
    You must be here to serve me! #13
- else:
    /perform idle_pleased amused 40
    Shall I give you a task then? #14
}
/perform idle_pleased amused 

+ [yes]
    -> Headmistress_AcknowledgeYes ->
    -> choose_task

+ [no] -> goodbye


= choose_punishment

-> transition ->

// Eventually we'll have the game randomly pick here.
{ shuffle:
    - -> headmistress_punishment_spanking -> exit
}
//-> headmistress_punishment_flogging -> exit


= choose_reward_mega
/perform tease_caress_breasts_large aroused
Now then, shall we get comfortable? #48
{shuffle:
    - -> headmistress_rewardmega_placeholder
}


= choose_reward

/character headmistress
/emotion happy 65

{shuffle:
    - /perform do_arm_raise amused 70
    Then a reward you shall have. #15

    -/perform idle_pleased
    It is so pleasing when trinkets have earned a reward. #16

    Rewards are preferable to punishments. #17
}

-> transition ->

{inTutorial: -> headmistress_reward_exhibitionism }
{deviation == exhibitionism: -> headmistress_reward_exhibitionism }

// Eventually we'll have the game randomly pick here.
{shuffle:
    - -> headmistress_reward_exhibitionism
    - -> headmistress_reward_cunnilingus

}


= choose_task

-> transition ->

// Always do the hand signal task in the hand_signals deviation.
{deviation == hand_signals: -> headmistress_task_handsignals -> exit}

// Eventually we'll have the game randomly pick here.
{ shuffle:
    - -> headmistress_task_handsignals ->
}

- -> exit


= transition

{shuffle:
    - /perform do_clap_clap happy 50
    Places, everyone! #20
    - /perform do_clap_clap happy 50
    On your marks! #21
}
->->


= goodbye

/character headmistress react_resigned sad 70
{shuffle:
    - I have nothing for you. #49
    - Then, I have nothing for you. #18
}

/perform do_you_leave_right disgusted 40
Begone. #19
/wait 1

-> exit
