=== treat_monitoring ===
-> select_gender_check ->

/scene Entry.TMonitoring
/music treat
/character treat

+ [move:Approach]

-

//greet player
{ shuffle:

    -/perform do_inspect happy 60
    Hello {player_name}. #01

    -/perform do_wave_small happy 65
    Hello there. #02
    //General Kenobi, you are a bold one

    -/perform react_thankful amused 55
    Ah. There you are. #03
}

//skip the monitoring if player has been visiting her
//if they haven't, then she reminds them to come see her

//reminder to have a treatment
{treat_session_reminders < 2:
    ~ treat_session_reminders++
    -> monitor
- else:
    ~ treat_session_reminders = 0
    -> force_yes
}


= monitor

//check for type of scene
{
    - just_changed == reward_removed:
        -> reward_monitor

    - just_changed == punishment_removed:
        -> punishment_monitor

    - else:
        -> task_monitor
}


//comment on reward
= reward_monitor
{ shuffle:

    -/perform talk_one_finger happy 65
    Rewards are delightful... #04

    /perform talk_behold aroused 70
    But they are much more enjoyable #05
    when your mind is in the right place! #06

    -/perform talk_one_hand happy 65
    I see you've been pleasing to the Mistresses. #07

    /perform react_thankful aroused 60
    That's good. #08

    /perform talk_one_finger amused 70
    But you mustn't neglect your mind. #09

    -/perform talk_one_finger amused 60
    You may have received a reward, #10

    /perform talk_lean_forward skeptical 70
    but you must also complete your therapy. #11
}

-> reminder


//comment on punishment
= punishment_monitor
{ shuffle:

    -/perform talk_lean_forward skeptical 55
    Getting in trouble I see. #12

    /perform talk_one_finger scared 60
    We must work on your mindset. #13

    -/perform talk_lean_forward skeptical 55
    I'm disappointed to see you are getting into trouble. #14

    /perform talk_one_finger scared 60
    Therapy will help you overcome that. #15

    -/perform talk_lean_forward skeptical 55
    Being punished will take its toll on you. #16

    /perform talk_one_finger scared 60
    We must keep your mind healthy. #17
}

-> reminder


//comment on task
= task_monitor
{ shuffle:

    -/perform do_smooth_skirt amused 65
    I see you are keeping busy fulfilling tasks. #18

    -/perform talk_behold aroused 60
    Serving the Mistresses is an important duty. #19
}

{shuffle:

    -/perform do_snap_fingers skeptical 65
    But... #20

    -/perform talk_gesture amused 60
    Nevertheless... #21

    -/perform talk_thoughtful aroused 55
    However... #22
}

//comment on importance of mental health
{shuffle:

    -/perform do_arm_raise amused 70
    We must keep your mind sharp, as well! #23

    -/perform do_shake_finger skeptical 65
    We must maintain your mental hygiene! #24

    -/perform do_shake_finger_stern sad 30
    Your mental health must be maintained! #25
}


-> reminder


= reminder

//comment that she's been waiting
{ shuffle:

    -/perform react_thankful amused 65
    I've been waiting for you to come see me. #26

    -/perform do_summon_player happy 55
    It's time for you to have a session. #27

    -/perform talk_behold skeptical 65
    It's important you visit me regularly. #28

    -/perform react_thankful skeptical 60
    I expect you to come to your sessions. #29
}

- (visit)

/perform idle_sassy amused 70
Do you want to visit me now? #30

+ [yes]
    -> Treat_AcknowledgeYes ->

    -- (force_yes)
    /perform do_stand_to_right happy 65
    Come to my office. #31
    -> treat_hub.choose_task

+ [no]
    -> Treat_Unhappy ->
    -> warning

+ [timeout] -> Treat_Timeout -> visit

+ [distracted] -> Treat_PayAttention -> visit


= warning

{shuffle:

    -/perform idle_unhappy_stomp sad 55
    If you don't come see me on your own, #32
    then I will have to force you to. #33

    -/perform idle_unhappy_arms_crossed sad 50
    Eventually, I will require you come see me. #34

    -/perform idle_bored skeptical 65
    I will give you a little more time. #35
    And then I will summon you. #36
}

//say Goodbye
{ shuffle:

    -/perform do_smooth_skirt skeptical 45
    Remember what I've said. #37

    /perform do_shoo_player skeptical 65
    You may go. #38

    -/perform do_blow_kiss amused 55
    Goodbye {player_name}. #39

    /perform idle_sassy amused 65
    You may continue what you were doing. #40
}

-> entry_hub
TODO: complete and hook up entry.explore

->->
