=== Treat_GoodJob ===
-> DoEmoteOnly(false)


= EmoteOnly
-> DoEmoteOnly(true)


= DoEmoteOnly(emoteOnly)

{ shuffle:
    - {emoteOnly:/emotion|/perform idle_sassy} happy 50
    There's a good {player_name}. #01

    - {emoteOnly:/emotion excited 50|/perform idle_tease} aroused 65
    What a good little {player_name}. #02

    - {emoteOnly:/emotion|/perform idle_clasp_hands} happy 50
    You're doing so well. #03

    - {emoteOnly:/emotion|/perform talk_consider} happy 50
    Very nice! #04

    - {emoteOnly:/emotion|/perform react_thankful} happy 45
    Keep up the good work! #05

    - {emoteOnly:/emotion|/perform idle_sassy} aroused 65
    Most pleasing. #06

    - {emoteOnly:/emotion|/perform idle_tease} happy 50
    Someone is being a good {player_name}. #07

    - {emoteOnly:/emotion|/perform react_thankful}  aroused 40
    Nicely done. #08

    - {emoteOnly:/emotion|/perform do_inspect} aroused 60
    Impressive. #09

    - {emoteOnly:/emotion|/perform talk_consider} amused 55
    That's it. #10
}
->->




=== Treat_AcknowledgeYes ===
-> DoEmoteOnly(false)


= EmoteOnly
-> DoEmoteOnly(true)


= DoEmoteOnly(emoteOnly)


{emoteOnly:/emotion happy 70|/perform react_nod_little} amused 55
{shuffle:
    - How delightful. #01
    - Very good. #02
    - Alright then. #03
    - Good. #04
    - Splendid. #05
    - Wonderful. #06
    - Lovely. #07
}
->->




=== Treat_Unhappy ===
-> DoEmoteOnly(false)


= EmoteOnly
-> DoEmoteOnly(true)


= DoEmoteOnly(emoteOnly)


{shuffle:
    - {emoteOnly:/emotion|/perform react_disappointed} sad 65
    How disappointing. #01

    - {emoteOnly:/emotion|/perform react_whatever} angry 50
    Really? #02

    - {emoteOnly:/emotion|/perform talk_annoyed} sad 65
    I see. #03

    - {emoteOnly:/emotion|/perform react_resigned} sad 55
    How unfortunate. #04

    - {emoteOnly:/emotion|/perform react_dismissive} skeptical 70
    So be it. #05

    - {emoteOnly:/emotion|/perform react_scoffs} angry 40
    Very well. #06
}
->->




=== Treat_Timeout ===
-> DoEmoteOnly(false)


= EmoteOnly
-> DoEmoteOnly(true)


= DoEmoteOnly(emoteOnly)


{ shuffle:
    - {emoteOnly:/emotion|/perform idle_clasp_hands} amused 45
    I asked you a question. #01

    - {emoteOnly:/emotion|/perform talk_emphatic_one_hand} angry 55
    Hurry up. I'm waiting. #02

    - {emoteOnly:/emotion|/perform talk_lean_forward} skeptical 35
    Come now. Don't be rude. #03

    - {emoteOnly:/emotion|/perform do_shielding_eyes} skeptical 65
    Hello? Are you still there? #04

    -{emoteOnly:/emotion|/perform talk_question} sad 65
    Are you going to answer me? #05
}

->->




=== Treat_PayAttention ===
-> DoEmoteOnly(false)


= EmoteOnly
-> DoEmoteOnly(true)


= DoEmoteOnly(emoteOnly)

{ cycle:
    - {emoteOnly:/emotion|/perform do_shake_finger_stern} angry 80
    Look at me when I'm speaking to you, {player_name}. #01
    /wait 1

    - {emoteOnly:/emotion|/perform do_shake_finger} skeptical 65
    Tsk. Tsk. Pay attention! #02
    /wait 1

    - {emoteOnly:/emotion|/perform do_summon_player} angry 40
    I'm over here. Stop looking around. #03
    /wait 1

    - {emoteOnly:/emotion|/perform talk_one_finger} angry 40
    I expect you to pay attention when I'm speaking. #04
    /wait 1

    - {emoteOnly:/emotion|/perform idle_unhappy} skeptical 60
    Therapy requires attentiveness. #05
    /wait 1
}

// Added in waits, as the lines felt rushed when transitioning

+ [pay-attention]
+ [timeout] -> Treat_PayAttention

// TODO: Make # of cycle elements equal or change to cycle/shuffle
- { cycle:
    - {emoteOnly:/emotion|/perform talk_consider} happy 60
    Where was I? Oh yes... #06
    /wait 1

    - {emoteOnly:/emotion|/perform react_scoffs} amused 60
    That's better. Let's continue. #07
    /wait 1

    - {emoteOnly:/emotion|/perform idle_clasp_hands} happy 40
    Good {player_name}. #08
    /wait 1

    - {emoteOnly:/emotion|/perform talk_annoyed} amused 30
    Let's try that again. #09
    /wait 1

    - {emoteOnly:/emotion|/perform idle_unhappy} amused 30
    As I was saying... #10
    /wait 1
}
->->




=== Treat_BadJob ===
-> DoEmoteOnly(false)


= EmoteOnly
-> DoEmoteOnly(true)


= DoEmoteOnly(emoteOnly)


/emotion angry 50

{ shuffle:
    - {emoteOnly:/emotion|/perform react_disappointed} sad 65
    I'm disappointed. #01

    - {emoteOnly:/emotion|/perform do_shake_finger_stern} angry 55
    I know you can do better. #02

    - {emoteOnly:/emotion|/perform talk_consider} skeptical 70
    That's not quite right. #03

    -{emoteOnly:/emotion|/perform react_scoffs} angry 45
    You are testing me. #04

    - {emoteOnly:/emotion|/perform talk_annoyed} sad 70
    I expect more. #05
}
->->




=== Treat_CheckIfInterested ===
-> DoEmoteOnly(false)


= EmoteOnly
-> DoEmoteOnly(true)


= DoEmoteOnly(emoteOnly)


{checked_if_interested: -> check_terse}

~checked_if_interested = true

{emoteOnly:/emotion|/perform idle_unhappy} sad

You are the one who came to see me. #01
- (check_again)

{emoteOnly:/emotion|/perform talk_behold} skeptical

Are you willing to continue? #02

{emoteOnly:/emotion|/perform idle}

+ [yes] -> IsInterested(emoteOnly)
+ [no]
+ [timeout] -> check_again

- (check_terse)

/emotion skeptical

Really? #03

/emotion angry 50

If you are unwilling to continue you, #04
then you should leave. #05

My time is precious, after all. #06

{emoteOnly:/emotion|/perform talk_annoyed} skeptical 65

So I'll ask one last time. #07

{emoteOnly:/emotion|/perform talk_lean_forward} angry 50
Do you want to continue? #08

+ [yes] -> IsInterested(emoteOnly)
+ [no] -> NotInterested(emoteOnly)


= IsInterested(emoteOnly)

{emoteOnly:/emotion|/perform idle} happy 70
Excellent. #09

->->


= NotInterested(emoteOnly)

{emoteOnly:/emotion|/perform talk_annoyed} angry 65
Then you must leave. #11
Return to me when you are ready. #12

-> exit


=== Treat_Masturbation_Comments ===

//instructions to player for how to touch themself

//Do not insert animations, ONLY emotions
/character treat 
/emotion happy 70

{shuffle:
    - /emotion aroused 60
        Caress yourself. #03
    - Glide your fingers. #05
    - /emotion aroused 60
        Let your fingers slide over your flesh. #08
    - Give it a gentle squeeze. #09
    - Cup yourself with your hand. #10
    - /emotion amused 70
        Savor the warmth of your skin. #11
    - Notice the moisture {hasPenis: ooze from your tip|build between your folds}. #12
    - Take a moment to lightly pinch your nipple. #13
    - Drag your hands gently across your skin. #14
    - /emotion amused 70
        Let the warmth draw your fingers. #15
    - Hold yourself. #16
    - /emotion aroused 70
        Feel how wonderful your hand is on your body. #17
    - Savor the touch of your skin. #18
    - Let your other hand gently caress your chest. #19
    - /emotion aroused 70
        Don't forget, let your other hand caress your body as you enjoy yourself. #22
    - Can you feel how much you throb? #24
    - Take your time and explore every aspect of your genitals. #25
    - Touching yourself is such a special time. #26
    - Feel how the blood rushes to fill your genitals getting them ready for me. #27
    - /emotion amused 70
        Show me how much you want to feel me touch you. #28
    - /emotion aroused 70
        Mmm. I can't wait to feel you against me. #29
    - Keep going as you work yourself #30
        into a state of arousal for me. #31
    - Maybe you want to squeeze yourself a little? #32
    - You should do whatever makes you feel good right now. #33
    - /emotion amused 70
        You can lick your fingers if that helps. #34
    - /emotion excited 60
        I like watching you arouse yourself. #35
    - /emotion amused 70
        I bet it feels good to touch yourself for me. #37
    - I love watching your hands drag across your skin. #38
    - /emotion amused 70
        The sight of you touching your body turns me on. #39
    - Mmmm, I can see the blood rush into your genitals. #40
    - Mmmm, I can't wait until you're aroused, and ready for me to ride you. #41
    - /emotion aroused 70
        I could just watch you touch yourself all day. #42
    - Mmmm, that's it. Really enjoy your body right now. #43
    - There's nothing I want more than to feel my body against yours. #44
    - I can't wait until our skin is touching. #45
}

->->

=== Treat_Pleasure_Comments ===

//comments about how aroused Ms. Treatment is

/character treat
/emotion aroused 70

{shuffle:
    - You look lovely, {player_name}. #01
    - /emotion happy 70
        Mmmm... I'm enjoying this. #04
    - /emotion happy 60
        You have the potential to be very special. #03
    - Your pleasure is my pleasure. #06
    - Relax as you give in to pleasure. #09
    - Mmm. Just like that. #10
    - /emotion excited 70 
        Oh yes. Watching you excites me. #11
    - There are so many things I want to show you. #12
    - /emotion amused 70
        You should use these techniques with the Headmistress. #13
    - /emotion amused 70
        Lovely Yuki would be panting to see you like this. #14
    - I can feel my breasts tingling all the way down to my vagina. #20
    - Watching my pets, makes me feel so good. #21
    - I just yearn to see my pets happy like this. #22
    - /emotion aroused 85
        Watching you in pleasure makes me throb. #23
    
    
}

->->

=== Treat_Seduction_Comments ===

//comments of Ms. Treatment seducing the player
//Do not insert animations, ONLY emotions

/character treat
/emotion aroused 90

{shuffle:
    - /emotion happy 80
        Your body is delightful. #01
    - /emotion aroused 70
        Let me hear you moan, darling. #02
    - Seeing you move is breathtaking. #04
    - I can't stop watching you. #05
    - I quiver watching you. #07
    - The sound of your pleasure is enticing. #08
    - My skin is tingling at the sight of you. #09
    - You are breathtaking. #10
    - /emotion amused 80
        You are a sexy little minx. #13
    - /emotion amused 65
        I knew my pet was a sexy little {player_name}. #14
    - There's nothing more intoxicating than a wonderfully, pleasure-filled submissive. #15
    - /emotion happy 80
        I'm so glad you're here with me right now sharing this special moment. #16
    - Let go and give in to how it feels. #23
    - Savor this moment. #28
    - /emotion 75
        Savor all the sensations you're feeling. #29
}

->->

=== Treat_Riding_Comments ===

//comments made during the riding scene
/character treat
/emotion aroused 80

{cycle:
    - I know you want to stick your face in my breasts and cuddle. #01
    - {hasPenis: Riding you is so satisfying.| Grinding against you is so satisfying.} #02
    - All my folds are quivering as I rub against you. #04
    - /emotion aroused 90
        This is such a special moment, us together, touching like this. #05
    - I'm trembling with how wonderful it feels to be against you. #06
    - You've <b>earned</b> this special treat, little one. #07
    - Our bodies, mmmm. They mesh so well together. #08
    - I hope this feels as good to you, as it does to me, sweetling. #09
    - That's it, keep moving with me and enjoy it just as much as I do. #10
    - Your body is such a wonderful vessel for pleasure. #12
    - I don't want this to stop. #13
    - Let's enjoy this for as long as we can. #14
    - That's right, in and out, in and out. #15
    - That's right, back and forth, back and forth. #16
    - You were made for this. #17
    - Oh yes, just like that. #18
        So good. #19
    - I don't want to stop doing this with you, sweetling. #20
    - /emotion aroused 90
        Oh yes! #21
        More! #22
    - /emotion aroused 90
        Oh the pleasure... ahhh! #23
    - Mmmm, your body is perfect! #24
    - These are my favorite rewards! #25
    - I'm so glad you wanted this reward. #26
    - Ahhhh, ohhh... Mmmm. Yes. #27
    - Oh, let's ride the pleasure together! #28
    - Give your body and your pleasure to me {player_name}! #29
    - 
    
}

->->

=== Treat_Reward_Complete ===
-> DoEmoteOnly(false)


= EmoteOnly
-> DoEmoteOnly(true)


= DoEmoteOnly(emoteOnly)

{shuffle:
    - {emoteOnly:/emotion|/perform idle_sassy} happy 65
    - {emoteOnly:/emotion|/perform idle_pleased} happy 50
    - {emoteOnly:/emotion|/perform idle_tease} aroused 70
}

{shuffle:
    - I adore giving you rewards. #01
    - Rewarding you makes me happy. #02
    - I hope you relished your reward. #03
}

{shuffle:
    - Keep up the good work! #04
    - I want you to keep earning them. #05
    - Make sure to keep behaving. #06
}

{shuffle:
    - {emoteOnly:/emotion|/perform talk_lean_forward} amused 45
    - {emoteOnly:/emotion|/perform idle} happy 65
    - {emoteOnly:/emotion|/perform idle_skeptical} aroused 55
}

{shuffle:
    - You may go for now, #07
        but come see me again soon. #08
    - Time to go for now. #09
    - Goodbye, little one. #10
    - It is time for you to go now. #98
        You are needed elsewhere. #99
}
->->




=== Treat_Take_Reward_Prompt ===
// don't use performances
/character treat
{shuffle:
    - Present your wrist. #01
    - This will cost you one Bloodstone. #02
    - Time to hand over your Bloodstone. #03
    - Give me your wrist, {player_name}. #04
}
->->




=== Treat_Take_Reward(-> load) ===

- (give_reward)

/character treat idle_hand_out happy 75

-> Treat_Take_Reward_Prompt ->

+ [pose:present_wrist_reward]
    /perform do_swipe_hand excited 50
    /wait 1
    ~removeReward(load)
    -> Treat_AcknowledgeYes.EmoteOnly ->->

+ [no] -> Treat_CheckIfInterested -> give_reward
+ [timeout] -> Treat_Timeout -> give_reward
+ [distracted] -> Treat_PayAttention -> give_reward
