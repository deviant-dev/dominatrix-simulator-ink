=== furia_hub ===
-> select_gender_check ->

{furia_intro_completed == false and deviation != furia: -> furia_intro }

~ setHub(Furia, "furia_hub", -> furia_hub)
/scene none
/scene FuriasRoom.Hub
/music furia-hub-1
/ambient furia
/character furia
/perform idle_unhappy skeptical 30

+ [move:Approach] 
    // Special deviation jump directly to the looping scene.
    {deviation == furia: 
        ~ punishments = 0
        ~ rewards = 1
        -> furia_reward_pegging
    }
    -> apology_check(-> hello, -> exit)

+ [move:Exit] -> exit


= hello
/music furia-hub-2
/wait

{ furia_regard >= 10:
    ¡Fantástico! #01
    My cariño arrives! #02
- else:
    {shuffle:
        - You return, {isBoy: puto| {isGirl: perra| gusano}}. #03
        - You return, slave. #68
        - Oh mira, the {player_name} is back. #04
        }
}

-
{inChastity:
    I see you are in chastity. #05
    There are things I can do to you while in chastity, #06
    but I can do more if you are released. #07
    
    If you want to be freed, #08
    you should visit the Goddess. #09
}

Do you want to serve me? #10

+ [yes] -> Furia_AcknowledgeYes ->
+ [no] -> goodbye

- (wrists)
/perform idle_present_wrists skeptical 50
{shuffle:
    - Present your wrists. #11
    - Show me your stones. #12
}

+ [pose: present_wrists] 
+ [timeout] -> Furia_Timeout -> wrists

- /perform idle_fake_annoyed amused 50
{
    - punishments > 0: -> check_punishment
    - rewards >= 10: -> check_reward_mega
    - rewards > 0 : -> check_reward
    - else: -> check_task
}


= check_punishment

/character furia
{punishments > 1:
    You have more than one punishment due. #13
- else:
    You have an Obsidian punishment stone. #14
}

{shuffle:
    - I am eager to break you down, slave. #15
    - I yearn to punish you. #16
}

Will you submit? #17

+ [yes] -> choose_punishment
+ [no]

- No? #18

{
    - punishments < 10 && rewards < 10:
        -> check_task
    - punishments >= 10:
        You have too many punishments to do anything else. #19
        You must deal with them! #20
    - rewards >= 10:
        Well, I cannot give you a task, greedy slave. #21
        You have hoarded rewards! #22
        All punishments must be paid #23
        and one reward redeemed #24
        if you want a task. #25
        So... #26
}

- Do you wish to serve a punishment? #27

+ [yes] -> choose_punishment
+ [no]

-

-> goodbye


= check_reward_mega

A full wrist of Bloodstones. #28
Y sin castigos. #29
Worthy of a special reward. #30

{furia_regard < 10:
    But a fruitless waste of time for you! #31
    You are not preciosista to me yet. #32
    You may have a simple reward and be grateful. #33
    -> check_reward
}

{inChastity:
    Pero, qué decepción. #34
    You are in chastity. #35
    You must fix that first. #36
    All I can give you for now is a simple reward. #37
    -> check_reward
}

Yield all 10 bloodstones, #38
and you will experience... #39
Me. #40

Is that what you desire, {player_name}? #41

+ [yes] -> choose_reward_mega
+ [no] -> check_reward


= check_reward

/character furia
Do you want a reward, peque? #42

+ [yes] -> choose_reward
+ [no]

- No? #43

{
    - rewards < 10:
        -> check_task

    - rewards >= 10:
        Well, that is all you may have. #44
}

Now, idiota. #45
Do you wish for a reward? #46

+ [yes] -> choose_reward
+ [no]

- You cling to those miserable crystals like a fool. #47

-> goodbye


= check_task

/perform talk_consider skeptical 25
{ 
    - punishments == 0 && rewards == 0:
        You have no gems, slave. #48
        I can give you a task to earn one. #49
    - punishments > 0:
        I would rather torture you, #50
        but still, I can shape you with a task. #51
    - else:
        You may serve me with a task. #52
}

Would you like that? #53

+ [yes] -> choose_task
+ [no] -> goodbye


= choose_punishment

{shuffle:
- Your cries of anguish will be como música to my ears. #54
- Ay, mongrel. You know your place. #55
}

{ shuffle:
    - -> furia_punishment_placeholder
}


= choose_reward_mega

{shuffle:
- This will be almost as much fun as a punishment, no? #56
- You've earned some fun, {isBoy:ansioso|{isGirl: ansiosa|eager one}}. #57
}

{shuffle:
    - -> furia_rewardmega_placeholder
}


= choose_reward

{shuffle:
- Well chosen. #58
- ¡Que bueno! #59
}

{ inChastity:
    With you in chastity, #60
    our choices are limited. #61
    {shuffle:
        - -> furia_reward_placeholder
    }
- else:
    {shuffle:
        - -> furia_reward_pegging
    }
}


= choose_task

{shuffle:
- I am eager to unravel you. #62
- How eagerly you debase yourself for my approval. #63
}

{ shuffle:
    - -> furia_task_gloryhole_training
}


= goodbye
{shuffle:
    - I have nothing more to say to you. #64
    - You waste my time. #65
}

{shuffle:
    - Get out. #66
    - Leave me. #67
}
/wait
-> exit
