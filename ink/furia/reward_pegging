=== furia_reward_pegging ===

-> select_gender_check ->
~ setScene(reward_scene, Furia, "furia_reward_pegging", -> furia_reward_pegging)
-> intro

= intro
/scene FuriasRoomPlatform.Intro01
/music Furia
/ambient Furia
/character Furia idle amused 50

TODO: Dev - set up scene placement, create pegging anims (3 - thrust, repetitive, orgasm), Furia with perfection, 'react' mechanic for thrust, slap, swat


TODO: Send to Grace to revise language so that Furia is encouraging the player to let go and wallow in being a masochistic slut. She is trying to always train their mind to give into their most degrading side. The language should not be just belittling them or putting them down, but doing so because she KNOWS that's what they want to experience.

//interruptions in pegging would be verbal humiliation, slaps (react), and swats (react)

//pegging is a combination of smooth penetration punctuated by hard slaps that the player must 'react' to

// (intro_check) [consent check] and (intro)
// (like_cock) 

  // randomly select 1 {cycle} block
// (permission) [player is allowed to masturbate]
// pegging 
  // game should randomly select 2 pegging sections (1, 2 or 3) and intersperse with breaks (face slapping, ass swatting, masturbation)
// (furia_pleasure) is the final section, Furia cums in their ass (furia_cum)

  //(furia_cum) will shuffle orgasm lines 
// (finish) is the conclusion 

Dirty sluts like you work me up. 
Get me hard. 
And deseparate to fuck.

- (intro_check)

Do you want to be fucked, {isBoy: querido|querida}?

+ [yes]
    ~ addObey()
    Bueno.
    That is exactly what I am going to do to you.

  
+ [no]
    ~ addDisobey()
    Qué vergüenza!
    What a shame!
    
    This is the reward I am offering.

    -- (double_check)
    Do you want this reward? 

    ++ [yes] 
        ~ addObey()
        -> Furia_AcknowledgeYes ->

    ++ [no]
      ~ addDisobey()
        Oh, perdóname.  
        I see you are not ready for a reward. 
        Some meat needs to be beaten longer to tenderize it.

        // She snaps her whip.

        ¡Vamos!
        -> exit 
    
    ++ [distracted] 
        ~ addDisobey()
        -> Furia_PayAttention -> double_check

    ++ [timeout]
        ~ addDisobey()
        -> Furia_Timeout -> double_check

+ [distracted] 
    ~ addDisobey()
    -> Furia_PayAttention -> intro_check

+ [timeout]
    ~ addDisobey()
    -> Furia_Timeout -> intro_check

- -> Furia_Give_Reward ->
~ removeReward(-> load_begin)
-> begin


= load_begin
/scene FuriasRoomPlatform.Intro01
/music furia
/ambient furia
-> begin


= begin
 /character furia idle
 
- (move_to_table_check)

Get on the table.

+ [move:ClimbOntoTable]

+ [timeout] 
    ~ addDisobey()
    -> Furia_Timeout -> move_to_table_check 

//Furia cracks her whip as she walks around the player (on their knees) analyzing her work. 

-
/scene FuriasRoomPlatform.Intro02

- (knees)

Now kneel.

+ [pose:present_kneel]
    ~ addObey()

+ [no] 
    ~ addDisobey()
    -> Furia_CheckIfInterested -> knees

+ [timeout]
    ~ addDisobey()
    -> Furia_Timeout -> knees 

- 
Before I grace your ass with my cock,
you must show me how worthy you are to receive it. 

//player must rub face in Furia's crotch to 'beg' for it to be used on them 

- (worship)
My shaft is precioso.
Vicious.
A force to be reckoned with.
And I do not share it with just anyone.
Show me you are worthy enough to touch it.

- (beg)

Rub your face in my crotch and beg for me to release it.
Can you do that, {isBoy: el codicioso|la codiciosa}?

+ [yes]
    ~ addObey()
    -> Furia_AcknowledgeYes ->
  
+ [no]
    ~ addDisobey()
    I cannot reward you if you do not beg.
    -> Furia_CheckIfInterested -> beg

+ [timeout] 
    ~ addDisobey()
    -> Furia_Timeout -> beg

+ [distracted] 
    ~ addDisobey()
    -> Furia_PayAttention -> beg

- -> rub_face_in_crotch ->

Enough.
Let me see your hungry face, slut.

+ [pose:kneel]


- (beg_check)
{shuffle:
    - Are you hungry for my cock in your ass, {isBoy:puto|puta}?
    - Do you think you've earned having my cock up your ass?
}

+ [yes]
    ~ addObey()
    {shuffle:
        - I agree. 
            You are ready. 
            -> tease 
        - Pathetic creature.
            You haven't begged hard enough.
            -> rub_face_in_crotch -> tease
    }

+[no]
    -- (beg_check_2)
    /perform talk_behold angry 65
    ~ addDisobey()
    Mmm. Interesante.
    Do you want to beg harder?
    
    ++ [yes]
        Then get to it!
        -> rub_face_in_crotch -> tease
        
    ++ [no]
        Then get the fuck out of here!
        -> exit
        
    ++ [timeout]
        ~ addDisobey()
        -> Furia_Timeout -> beg_check_2
    
    ++ [distracted] 
        ~ addDisobey()
        -> Furia_PayAttention -> beg_check_2

+ [timeout]
    ~ addDisobey()
    -> Furia_Timeout -> beg_check

+ [distracted]
    ~ addDisobey()
    -> Furia_PayAttention -> beg_check


- (tease)
Enough begging.
Get on your back.

+ [pose:fuck_doll]
    ~ addObey()
    -> Furia_GoodJob ->

+ [no]
    ~ addDisobey()
    -> Furia_AcknowledgeNo -> Furia_CheckIfInterested -> tease

+ [timeout]
    ~ addDisobey()
    -> Furia_Timeout -> tease

- Now, time for me to get a bit more comfortable.

- (tease2)
/scene FuriasRoomPlatform.Intro03
/perform furia_pegging_idle_ready

//Player gets on their back on a padded leather table, their knees bent and legs in the air
Are you excited for your reward, {isBoy:cerdito|cerdita}?

+ [yes]
    ~ addObey()
    Good, good. You'll do nicely. 
    Mi esposa de prisión. 
    You're looking… ripe. 
    All raw and whimpering, you fucking tease.
    I'll warn you now. 
    This may hurt… 
    and you will like it. 

+ [no]
    ~ addDisobey()
    -> Furia_AcknowledgeNo -> Furia_CheckIfInterested -> tease2

+ [timeout]
    ~ addDisobey()
    -> Furia_Timeout -> tease2

- (permission)
It's time for the main event. 
Touch yourself whenever you like, 
Shove that grubby little hand between your legs. 
If you cum, I don't care. 
I'm not going to stop until <i>I'm</i> satisfied. 

- (learn)
Do you want to please me, {isBoy: perdito|perdita}? 

+ [yes]
    ~ addObey()
    ->Furia_AcknowledgeYes->

+ [no]
    ~ addDisobey()
    -> Furia_AcknowledgeNo -> Furia_CheckIfInterested -> learn

+ [timeout]
    ~ addDisobey()
    -> Furia_Timeout -> learn

- (pegging1)

//Furia walks around the player, fondling her cock

{shuffle:
    - I bet your tight, little hole will fight me. 
    - I bet your sloppy, gaping hole will let me right in. 
    - Let's find out how well your ass can take my rod. 
}

I'm going to spread that filthy hole wide,
Bust it open if I have to. 
Whatever it takes to get this thick cock balls deep. 

//Furia laughs loudly and slaps the players ass. 

Time to use this bitch up!

//Furia slaps the player's ass and penetrates them. 
/perform furia_pegging_idle_slow

-> react_to_thrusts ->
// -> RandomCommentLoop(-> pegging_comments, 3, 3, 2, 4) ->

- (pegging2)
I never tire of this game. 
I call it a reward for you, but oh, little gusano,
It's all for Mami. 

Your pain is my pleasure. 
And you <i>will</i> suffer, {isBoy: cerdito|cerdita}. 
Your Mistress demands it. 
The House demands it. 

- (question1)
Does your ass hurt, slut?

+ [yes]
    Oh, you poor baby.
    I'd say I feel sorry for you,
    but that would be a total lie.

+ [no]
    Ahhh. You're a little fuckslut.
    I love it!

+ [timeout]
    ~ addDisobey()
    -> Furia_Timeout -> question1

+ [distracted]
    ~ addDisobey()
    -> Furia_PayAttention -> question1
-    

-> react_to_thrusts ->
// -> RandomCommentLoop(-> pegging_comments, 3, 3, 2, 4) ->

- (pegging3)
Dungeon air is chilly on those hard nipples, eh? 
I bet your {hasPenis:little pene|needy coño} feels so cold
as I breed your ass on this bench. 

To be honest, I couldn't give two shits
whether you cum eighteen times or not at all. 
You are <i>my</i> prison bitch, 
and I <i>will</i> take my pleasure from you. 

- (question2)
Are you orgasming as I use you, {isBoy: chico|{isGirl: chica|mascota}}?

+ [yes]
    Now that's a delicious fuck toy!
    
+ [no]
    Your loss.
    My pleasure!

+ [timeout]
    ~ addDisobey()
    -> Furia_Timeout -> question2

+ [distracted]
    ~ addDisobey()
    -> Furia_PayAttention -> question2
    
-

-> react_to_thrusts ->
// -> RandomCommentLoop(-> pegging_comments, 3, 3, 2, 4) ->



- (furia_pleasure)

You make me so honry lying there.  
Broken. Bruised. 

I'm gonna cum so hard in your ass, 
you'll be dripping with it all night.

/perform furia_pegging_idle_fast

-> RandomCommentLoop(-> cumming_comment, 3, 3, 2, 4) ->


- (furia_cum)

//finish each orgasm with some panting or moaning in pleasure 

{shuffle:
    - Ahhh! Filling your ass with my seed is delicioso!
    - Take every drop of my seed in that filthy asshole. 
    - Oh, by the House, your ass is drinking my cum! Ahh! 
}

/wait 2

So, fucking good!

- (finish)
//Furia pulls away from the player and snaps her fingers, transforming back into her clothed appearance

Stand up, slut!

I'm done using you.

+ [pose:present_stand]
    ~ addObey()

+ [timeout]
    ~ addDisobey()
    -> Furia_Timeout -> finish

-
Oh good.
You can still use your legs.

- (ground)
Now, get down on the floor before me.

+ [move]
    ~ addObey()

+ [timeout]
    ~ addDisobey()
    -> Furia_Timeout -> ground

-
Mmmm. You're wobbling a bit.
Let's see the destruction I've wrought.

- (bend_over)
Turn around and bend over.

+ [pose:present_ass_reverse_spread]
    ~ addObey()

+ [timeout]
    ~ addDisobey()
    -> Furia_Timeout -> bend_over

- 
Wow. Look at that gaping hole.

That looks painful. 
I can see cum starting to leak out of you.

I am pleased with my work here today.

- (turn_around)
Turn around, {isBoy: querido|querida}.

+ [pose:present_stand]

+ [timeout]
    ~ addDisobey()
    -> Furia_Timeout -> turn_around


Well, {isBoy: chico|{isGirl: chica|mascota}},

- (satisfied)
Did you enjoy that reward?

+ [yes]
    -> Furia_AcknowledgeYes ->
    This is how good service is rewarded in this House. 
    How I condition my servants to debase themselves faithfully.  
    The ones whose minds bend a certain way...
    Come back when you need more. 
    -> exit
 
+ [no]
    -> Furia_AcknowledgeNo ->
    Huh. Well, it's all the same to me. 
    Maybe next time your mind will welcome the experience.
    You may go.
    -> exit

+ [timeout]
    -> Furia_Timeout -> satisfied


= rub_face_in_crotch

- (rub)

TODO: Need anim of Furia gesturing to her crotch

{shuffle:
    - Shove your face into my cock and whimper for it. 
    - Get over here and keep begging for me to fuck you. 
}

//game looks for player's head to enter and leave the area near their crotch

+ [touch-start:crotch]
    ~ addObey()

+ [timeout] 
    ~ addDisobey()
    -> Furia_Timeout -> rub

- {shuffle:
    - Mmm...
    - Oh yes, slut.
    - That's it, {isBoy:puto|{isGirl:perra|gusano}}.
}

-> BeggingCommentLoop(3, 3, 2, 4) ->->


= react_to_thrusts

/character furia furia_pegging_idle_ready aroused 75
~ furia_thrust_count = 0
~ furia_thrust_target_count = 5
~ furia_thrust_mistakes = 0

{shuffle:
    - Now I'm going to slam into you. 
        You better moan and jerk that head, piggy.
    - Time for me to go hard.
        Prepare to be 
}

- (thrust_again)
/character furia furia_pegging_react_thrust aroused 75

+ [react-success]
    ~ furia_thrust_count++

    -> pegging_comment ->

    {furia_thrust_count < furia_thrust_target_count: -> thrust_again}
+ [react-fail] 
    ~ furia_thrust_mistakes++
    -> bad_thrust_comment -> thrust_again

- ->->


= pegging_comment
{shuffle: 
    - By the House, you're a tight fit. 
    - That's right, worm. 
        Squeeze that virgin asshole around my big cock.
    - I bet you love it when my balls slap your ass.
    - Look at that pathetic little ass. 
        Filled to bursting. 
    - The more you moan, 
        the more I want to slam into you.
    - Cry if you want. 
        It makes me even harder. 
    - I love breaking and breeding prison bitches like you. 
    - (Furia moans).
    - (Furia moans).
    - (Furia moans).
    - (Furia moans).
    - (Furia moans).
    - I bet you don't often get a view like this. 
    - Scream all you like, worm. 
        It is music to my ears. 
    - What a spectacle! 
    - Feel your Mistress's big cock pound into your ass. 
    - Cum, bitch. 
        I'll just keep pounding you.
    - Listen to yourself grunt and moan like the whore you are. 
    - Moan, cunt!
    - Let me hear how much you're enjoying this.
    - Louder, slut! 
        I want to hear you cry and moan.
    - That's right. 
        Let the feeling take you. 
    - You're not loud enough, bitch!
    - Using your ass is exactly what I needed to relax today.  
    - You cannot resist your Mistress's cock. 
        You don't even want to.
    - Focus on feeling my cock pound into your tight ass. 
    - Let the pain turn to pleasure. 
    - The House knows <i>I'm</i> having fun! 
    - Fuck, you're s tight! 
    - I can feel you tighten and twitch on my cock. 
    - I'm gonna use you up, slut! 
TODO:   - (add a few more lines to this section)
}


->->


= bad_thrust_comment

-> Furia_BadJob ->

{shuffle:
    - When I slam in to you, moan and throw that head back.
    - Let me see you jerk when you hear my balls slap your ass, piggy.
    - Show me you feel.
        Jerk that head back when I thrust, {isBoy: puto|{isGirl: puta|animal}}.
}

->->


= BeggingCommentLoop(minTimes, maxTimes, minWait, maxWait)
~ temp count = 0
~ temp max = RANDOM(minTimes, maxTimes)
/log Comments looping {max} times.
- (repeat)

-> begging_comment ->

+ [wait {RANDOM(minWait, maxWait)}]
    ~ count++
    { count < max: -> repeat | ->-> }
+ [touch-end:crotch]

- (not_touching)
-> Furia_BadJob ->

- (not_touching_2)
{shuffle:
    - I'm not done with that face yet.
    - Get that muzzle in here, {isBoy:puto|puta}.
    - Did I say you could stop?
    - Stick that face back in there, worm.
}

+ [touch-start:crotch] -> repeat

+ [no]
    ~ addDisobey()
    Only good little piggies who beg get rewarded.
    -> Furia_CheckIfInterested -> not_touching_2

+ [timeout] 
    ~ addDisobey()
    -> Furia_Timeout -> not_touching_2

+ [distracted] 
    ~ addDisobey()
    -> Furia_PayAttention -> not_touching_2


= begging_comment

{shuffle:
    - Grind your face deeply.
    - I want to hear you moan as you beg.
    - Suck in the smell of my lust.
    - I can barely feel you, slut.
    - I'm not sure you really want me to fuck you. 
    - Look at the hungry beggar before me. 
    - Wallow in the smell of my fuckshaft.
    - Burrow deeply, little piggy.
    - That's it. Lose yourself in my crotch.
    - Whimper and beg for my rod, {isBoy:puto|puta}.
    - Your begging is intoxicating.
    - Beggars are dirty sluts who want to be used. 
    - I can feel the heat of your breath washing over me. 
    - Oh yah. Hold your breath as you shove your face in there. 
    - Beg harder.
    - Show me how much you want my cock.
    - Your bobbing head belongs between my legs. 
    - Look at that tight ass wiggle as you debase yourself.
}

->->

= cumming_comment
{shuffle:
    - Fuck, mi {isBoy: puto|puta}. 
        I'm getting close! 
    - Your tight ass is about to suck the cum right out of me!
    - Tighten that ass so I can cum, slut!
    - I can't hold out much longer!
    - My cock is about to explode!
    - Almost there, {isBoy: cerdito|cerdita}.
    - Your ass is about to be my cum dump!
    - Yes, yes, yes! I'm going to explode!
}

->->