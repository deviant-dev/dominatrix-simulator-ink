=== furia_task_gloryhole_training ===

-> select_gender_check ->
~ setScene(task_scene, Furia, "furia_task_gloryhole_training", -> furia_task_gloryhole_training)

/scene FuriasRoom.Gloryhole
/music furia-gloryhole-training
/ambient Furia
/character Furia

//this scene will get progressively more challenging for the player
//First level training -
    //during the first pass, the player is:
        // introduced to needing to pleasure a cock with their mouth.
    //during the second pass, the player must: pleasure a pussy with their mouth.
    //during the third pass, the player must: pleasure a cock with their hand.
    //during the fourth pass, the player must: pleasure a pussy with their fingers.
//Second level training -
//random selection of one of the four tasks above, for a random number of times from 1-4, and for variable durations.
//Third level training -
    //player must: pleasure two cocks or two pussies at one time with their hands with a variable duration.
    //scene with one submissive laying down on the mattress in one of the cells.
//Furia points to the empty cell with the gloryholes. Cell door is open.
//there is a move point available to the player to teleport into the room

- (take_spot)

To be reborn, you must be broken first. #01

/perform do_point_left amused 50
Go into that cell. #02

/perform idle_skeptical amused 65
Right next to the holes in the wall. #03

+ [move] -> Furia_GoodJob ->

/scene FuriasRoom.Gloryhole_Position

-> begin


= begin

/perform idle_tease amused 75
Your task is to serve with your mouth. #04

//intro dialogue for 'first time' through the scene
{ furia_intro_completed == false:
    /perform talk_gesture aroused 65
    You must become a perfect cum vessel #05
    for the cock in front of you. #06
- else:
    You must please whatever cock or pussy #07
    is presented to you. #08
}
//end of intro dialogue

- (understand)
/music furia-gloryhole-training-2
/perform talk_question skeptical 70
¿Comprendes?<br/>(Understand?)#09

+ [yes]
    -> Furia_AcknowledgeYes ->

+ [no]
    /perform react_disappointed angry 70
    You must agree before you can be transformed. #10

    -- (agree)
    Do you agree to this task? #11
    
    ++ [yes] -> Furia_AcknowledgeYes -> instructions

    ++ [no]
        -> leave

    ++ [timeout] -> Furia_BadJob -> Furia_Timeout -> agree

+ [timeout] -> Furia_Timeout -> understand


- (instructions)
/perform idle_unhappy angry 50
This task will demand a lot from you. #15

You will lick, suck, and gobble #16
until they cum their brains out. #17

/perform talk_lean_forward skeptical 70
You must follow my instructions to succeed. #18

/perform idle_fake_annoyed angry 65
If you fail, you will be given a punishment stone. #19

- (ready)
/perform talk_question skeptical 70

{shuffle:
    - ¿Estás {isBoy:listo|lista}?<br/>(Are you ready?) #20
    - Are you ready? #21
}

+ [yes]
    -> Furia_AcknowledgeYes -> cell

+ [no] 
    -> Furia_AcknowledgeNo ->
    We must begin or you must go. #22
    
    -- (agree2)
    Are you ready? #23
    
    ++ [yes] -> Furia_AcknowledgeYes -> cell

    ++ [no]
        -> leave

    ++ [timeout] -> Furia_BadJob -> Furia_Timeout -> agree2

+ [timeout] -> Furia_Timeout -> ready

- (cell) 
Then get into position. #27

- (position)
/perform do_point_down amused 60
/music furia-gloryhole-training-3
On your knees with your mouth positioned before the hole. #28

{shuffle:
    - It is eager for your attention. #29
    - It begs for you. #30
}

+ [pose:kneel] -> Furia_GoodJob ->
+ [timeout] -> Furia_NoAction -> position

- (release)

/perform talk_lean_forward excited 55
{shuffle:
    - Open your mouth. #31
    - Mouth wide open. #32
}

/perform idle_fake_annoyed amused 65
    {shuffle:
    - Your training begins now, {isBoy: puto| {isGirl: perra| gusano}}. #33
        Do not disappoint me. #34

    - Wallow in the service you provide. #35
        Let all your inhibitions fade away {isBoy:pequeño|{isGirl:pequeña|criatura}}. #36
}

-> minigame

//1 cock pops through the hole
//this scene will eventually have a a random number of cocks or vaginas from 1-5, but for the first time, it will only be one.

//mini game to have player slide their head forward and back over cock until progress 'halo' is fulfilled
//ambient track of moans, grunts, sighs of Pleasure
//cock twitches and orgasm sound


= minigame
/scene FuriasRoom.GloryholeMinigame
/music Furia
/ambient Furia
/perform idle_fake_annoyed aroused 69

// This minigame's target has a penis
~ mg_withPenis = true

// This minigame is on another character
~ mg_performOther = true

// Skip 2 'continue' comments before saying another
~ mg_commentDelay = 3

+ [minigame:success] -> finish_success
+ [minigame:failure] -> finish_failure


= leave
/perform idle_fake_annoyed angry 30
Then we cannot continue. #12
I demand strict obedience from my subjects. #13
/perform do_shoo_player angry 55

- (leave_repeat)
{shuffle:
    - {isBoy || isGirl:
            Vete ya, {isBoy:chico.<br/>(Go away, boy.)|chica.<br/>(Go away, girl.)} #14
        - else:
            -> leave_repeat
        }
    - Vete ya.<br/>(Go away.) #26
    - Vete ya, pues.<br/>(Go away, then.) #56
}

-> exit


= finish_failure
/scene FuriasRoom.GloryholeMinigame_Complete
/wait

¡Qué decepción!<br/>(What a disappointment.) #37
That was a very poor performance. #38

- (stand)
{shuffle:
    - Levántate.<br/>(Get up.) #39
    - Stand. #40
}

+ [pose:stand] 
+ [timeout] -> Furia_Timeout -> stand

-
{shuffle:
    - I will expect much more from you next time. #41
    - I expect to hear more suction, #50
        and gagging next time. #51
}

You have earned a punishment stone. #42

~addPunishment()
/wait 1

Next time, try harder. #43
You may go. #44

/wait 
-> exit


= finish_success
/scene FuriasRoom.GloryholeMinigame_Complete
/wait

-> Furia_GoodJob ->
{shuffle:
    - You have pleased me, for now. #45
    - You have pleased me, por ahora. #52
    - ¡Buen trabajo!<br/>(Good work.) #53
        Make sure to clean their member #54
        of any residual seed and spit. #55
        /wait 2
}

/wait 1
- (rise)
Rise. #46

+ [pose:stand]
+ [timeout] -> Furia_Timeout -> rise

- You have earned a reward stone. #47
~addReward()

/perform idle_skeptical excited 70
Come back again and we will continue your training. #48
We have much work ahead of us. #49

/wait 
-> exit
