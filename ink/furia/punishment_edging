=== furia_punishment_edging ===
-> select_gender_check ->
~ setScene(punishment_scene, Furia, "furia_punishment_edging", -> furia_punishment_edging)
/scene FuriasRoom.WhippingIntro
/music furia
/ambient furia
/character furia idle_unhappy sad 55

-> start


TODO: replay scene to test animations

= start
/perform idle_skeptical angry 70
Pathetic little worm.
/perform talk_annoyed disgusted 60
You know the rules and still you chose to disobey.

- (start_repeat)
/perform talk_lean_forward angry 70
Hands on your head, slave!

/perform idle disgusted 50

+ [pose:present_stand]
+ [timeout]
  -> Furia_Timeout ->
  -> start_repeat

- /perform talk_one_hand disgusted 50
Not only did you disobey 
and earn a punishment,
you came <i>here</i> to receive your punishment.
/perform react_scoffs amused 60
I can see you crave the pain.

/perform do_arm_raise aroused 60
La degradación.

- (start_query)
/perform do_fllick_air disgusted 60
You need to be reminded 
just how insignficant you are.
//mocking tone 
/perform react_nod_big amused 85
Don't you, cielo?

/perform idle amused 60

+ [yes]
    That is what I thought.
    You hunger for pain.
+ [no]
    You lie to yourself.
    You did come to /me/ for your punishment.
+ [timeout]
    //Furia snaps her fingers 
    -> Furia_Timeout ->
    -> start_query 
+ [distracted]
    -> Furia_PayAttention ->
    -> start_query 


- (edging_explanation)
/perform idle_tease amused 55
For your punishment,
I want you to suffer the blows of my whip
/perform talk_behold aroused 85
while you masturbate.
Until you are {hasPenis: painfully hard|dripping wet} for me.

- (want_experience)
/perform idle_tease amused 60
Do you want to ache for me this way, mi tesoro?

+ [yes]
    -> Furia_AcknowledgeYes ->
    -> Furia_Aroused ->
+ [no]
    -> Furia_AcknowledgeNo ->
    Let us see if you change your mind later.
+ [timeout]
    -> Furia_Timeout ->
    -> want_experience
+ [distracted]
    -> Furia_PayAttention ->
    -> want_experience

- /perform talk_consider amused 85
And because I love to watch you suffer,
/perform talk_threaten angry 80
you do NOT have permission to cum.

/perform talk_thoughtful skeptical 75
And to drive the lesson home
that disobedient slaves 
have no place in this <i>House</i>,
/perform idle_tease amused 80
I will taunt you to orgasm
and give you another punishment stone 
if you give in.

/perform react_scoffs disgusted 70
We will see if the combination of frustration and pain
can penetrate that thick skull of yours.

- (understand)
/perform idle_unhappy skeptical 70
Lo entiendes?

+ [yes]
    -> Furia_AcknowledgeYes ->
    
+ [no]
    I don't like repeating myself.
    Do you understand?
    -> understand 
    
+ [timeout]
  -> Furia_Timeout ->
  -> understand

+ [distracted]
  -> Furia_PayAttention ->
  -> understand

- (consent)
/perform do_arm_raise amused 75
Now...
Are you ready?
  
+ [yes] 
    -> Furia_AcknowledgeYes-> approach
+ [no] 
    -> Furia_AcknowledgeNo -> Furia_CheckIfInterested -> consent
+ [timeout]
  -> Furia_Timeout -> consent
+ [distracted]
  -> Furia_PayAttention -> consent


= load_approach
/scene FuriasRoom.WhippingIntro
/music furia
/ambient furia
/character furia idle_tease amused 55
-> approach


= approach
/perform do_point_right angry 75
Move over to that pole, bichito,
and straddle it.

+ [move:Approach] -> whipping_start
+ [timeout]
  -> Furia_Timeout.EmoteOnly -> approach


= whipping_start
/scene FuriasRoom.WhippingReady
/character furia idle_whip_furia disgusted 60

I am going to bind you to it
and treat you like the wretched masochist you are.

- (whipping_pose_repeat)
/perform idle_cocks_head_to_left angry 60
Put one hand behind your back and leave the other free for edging.

+ [pose:stand_edging]
+ [timeout]
  -> Furia_Timeout.EmoteOnly -> whipping_pose_repeat

-
/perform tease_caress_breast_left amused 70
Perfecto.
You look so delightful bound to my whipping pole.

+ [continue]

- /notify text="Put your controllers down for now. You will pick them up again later. Nod when ready to continue."

+ [yes]

//Furia fondles herself and brandishes the whip/cane 

- /perform idle_hand_on_hip angry 70
Now, get that free hand working
/emotion aroused 75
and pleasure yourself for me.

/wait 2 

Show me how good it feels to touch yourself.

/wait 2 

/perform idle_whip_furia happy 70
Tu lo tienes!
There you go.

/wait 2 

Feel how warm your skin is.
/perform idle_cocks_head_to_left aroused 70

- (warm)
Does it feel good?

 + [yes]
    -> Furia_Aroused.EmoteOnly ->
+ [no]
    Poor ratoncito.
    Keep doing it anyways.
 + [timeout]
  -> Furia_Timeout.EmoteOnly ->
  -> warm
+ [distracted]
  -> Furia_PayAttention.EmoteOnly ->
  -> warm

- 
/emotion excited 70
So obscene.
It makes me hard.

/perform idle_sassy aroused 75
Make yourself feel good, corazon.
But remember, you are forbidden to cum.

/wait 2
/perform idle_whip_furia skeptical 70
Now, I'm going to {whip|cane} you as I count.
This will sting.
/perform react_shrug_long amused 80
A little insecto bite.

- (edging_understand)
/perform talk_lean_forward excited 70
Each impact will cause you to jerk.
Move your head when it hits so I know you feel it.

/perform idle_whip_furia skeptical 70
Comprende?

+ [yes]
    -> Furia_AcknowledgeYes.EmoteOnly ->
+ [no]
    I don't like repeating myself.
    Jerk your head when you
    hear and feel my whip.
    
+ [timeout]
  -> Furia_Timeout.EmoteOnly ->
  -> edging_understand

+ [distracted]
  -> Furia_PayAttention.EmoteOnly ->
  -> edging_understand

- 

// Select 'short', 'medium', or 'long' for the task length.
{shuffle:
    - ~ task_length = short
    - ~ task_length = medium
    - ~ task_length = long
}

/log Task length is {task_length}.

// Say different things based on task length.
{
    - task_length == short:
        I have many slaves to torment today,
        so this will be a short and sharp punishment.
    - task_length == medium:
        Today is a perfect day for your punishment.
        I only have a few slaves to tend to,
        which means I can give you more time today.
    - task_length == long:
        Mmmm. I have hardly anyone to torment today,
        so I can really drag out this session out. 
        Let's see how much you can suffer.
}

-> begin_whipping


= load_whipping
/scene FuriasRoom.WhippingReady
/music furia
/ambient furia
/character furia idle_whip_furia amused 55
-> begin_whipping


= begin_whipping

I begin.

// Different length flows based on task length:
{
    - task_length == short:
        -> whipping_loop(10) ->
        -> talk_break ->
        -> whipping_loop(10) ->
        -> finish
    - task_length == medium:
        -> whipping_loop(10) ->
        -> talk_break ->
        -> whipping_loop(10) ->
        -> talk_break ->
        -> whipping_loop(10) ->
        -> finish
    - else:
        -> whipping_loop(10) ->
        -> talk_break ->
        -> whipping_loop(10) ->
        -> talk_break ->
        -> whipping_loop(10) ->
        -> talk_break ->
        -> whipping_loop(10) ->
        -> finish
}


= whipping_loop(goal)
~ task_count = 0
~ task_goal = goal
~ task_break_goal = RANDOM(2, 3)
~ task_break_count = 0

- (whipping_repeat)
/character furia idle_whip_furia
{task_count <  task_goal:
    ~ task_count++
    ~ task_break_count++
    -> whip(task_count) ->
    {task_break_count == task_break_goal:
        {shuffle:
            - ~ task_break_goal = 2
            - ~ task_break_goal = 3
        }
        ~ task_break_count = 0
        -> whip_comment ->
    }
    -> whipping_repeat
}

->->


= whip_comment
{shuffle:
    - -> tease_impact_arousal ->
    - -> tease_impact_taunt ->
    - -> tease_impact_body ->
    - -> tease_impact_generic ->
}
->->


= whip(count)
- (whip_again)

-> Furia_SayNumber(count) ->
/character furia do_whip_furia aroused 75

+ [react-success] ->->
+ [react-fail] -> bad_whip_bank -> whip_again


= talk_break

//breathy, pleasure filled sigh of satisfaction
[Sighs in pleasure] 
 
{shuffle:
    - The torment!
    - So beautiful!
    - How invigorating!
    - So delicioso!
    - How enlivening!
    - What a thrill!
    - Maravilloso!
 }   
    
{shuffle:    
    - Let's see if my taunts can 
    break you faster than my whip.
    - I want to seduce you
    - Time for me to see if 
        my voice alone can make you crumble.
    - You know. 
        My words can be sharper 
        than any whip, cariño.
    - Time to see if you can resist what I have to say.
    - I want to see if my breath on your neck 
        will be your undoing.
    - I want to plant thoughts in your mind you cannot escape.
}

- (voice_power)
/perform idle_tease amused 70
Do you want to find out how powerful my voice is?

+ [yes]
    -> Furia_AcknowledgeYes.EmoteOnly ->
    
+ [no]
    -> Furia_AcknowledgeNo.EmoteOnly ->
    I'll ask you again after I whip you more.
    ->->
+ [timeout]
    -> Furia_Timeout.EmoteOnly ->
    -> voice_power
+ [distracted]
    -> Furia_PayAttention.EmoteOnly ->
    -> voice_power

-

/scene furiasroom.whippingwhisper

{shuffle:
    - Keep touching yourself.
    - Don't stop masturbating.
    - Don't let your hand stop.
    - Keep that hand moving for me.
    - Don't you dare stop touching yourself.
    - Keep rubbing, mi tesoro.
    - Your hand better not stop working.
    - Keep pleasuring yourself, cariño.
}

{cycle:
    - -> talk_arousal ->
    - -> talk_taunt ->
    - -> talk_serve ->
    - -> talk_pain ->
    - -> talk_power ->
}

/scene furiasroom.ready

->->


= bad_whip_bank
/perform do_get_the_fuck_out disgusted 90
{shuffle:
    - Jerk your head in time with my whip, pollito.
    - Is that all? Move like you mean it!
    - Pathetic! Show me you feel my whip!
    - Dios mio! No flinch? Try harder!
    - Your stillness angers me. Jerk and moan!
    - Nothing? Let me see your pain.
    - Weak! Let that pain ripple through you.
    - You're stiff as stone. Move or I'll break you!
    - Dios! That was nothing. Give me a proper shudder!
    - Barely a twitch? Do better or regret it.
    - No response? Pathetic.
    - You're failing me.
    - Madre mia! So dull.
}

->->


= finish
/perform talk_behold happy 70
Enough. You have done your time.
And I have other slaves to condition.

- /notify text="It is time to pick your controllers back up."

- (work)
/perform talk_thoughtful skeptical 80
I do hope you'll behave, but if I am honest,
it doesn't matter <i>why</i> you're here.
/perform talk_emphatic_one_hand excited 100
One way or another, I will have you
/perform talk_threaten amused 100
exactly how I want you.

/perform do_arm_raise angry 80
Remember that you belong to Us.

- (orgasm_check)
/perform idle_cocks_head_to_left skeptical 75
But first, mi tesoro.
Were you able to keep from cumming for me?

+ [yes]
    /perform react_nod_little amused 70
    -> Furia_AcknowledgeYes ->
    You resisted my temptation.
    -> performance_check
    
+ [no]
    /perform talk_threaten angry 85
    -> Furia_AcknowledgeNo ->
    Pathetic. 
    You couldn't even resist during your punishment.
    ~ addPunishment()
    You've earned another obsidian stone, pajarito.
    -> leave
    
+ [timeout]
    -> Furia_Timeout.EmoteOnly -> orgasm_check
    
+ [distracted]
    -> Furia_PayAttention.EmoteOnly -> orgasm_check

- (performance_check)
/perform idle_weight_shift skeptical 70
And tell me, carino.

- (enjoyed)
Did you enjoy your punishment?

+ [yes]
    -> Furia_AcknowledgeYes ->
    {obeyed(): -> YesGoodPerformance | -> YesBadPerformance}
    
+ [no]
    -> Furia_AcknowledgeNo ->
    {obeyed(): -> NoGoodPerformance | -> NoBadPerformance}
    
+ [timeout]
    ~ addDisobey()
    -> Furia_Timeout -> enjoyed

= YesGoodPerformance
/perform talk_behold amused 80
Your reactions showed how well you handle pain for me.
You jerked when I commanded.
You resisted when forbidden.
~ increaseRegard()

/perform idle_tease aroused 75
This is how obedient slaves are conditioned in this House.
Through suffering, you become mine.
Come back when you need to be reminded who owns you.

-> remove_stone

= YesBadPerformance
/perform idle_skeptical disgusted 70
You enjoyed it, but your performance was sloppy.
Next time, react faster to my whip.
Show me you feel every strike.

/perform talk_one_hand amused 65
You have potential, bichito.
But you must be more attentive.

-> remove_stone

= NoGoodPerformance
/perform talk_consider skeptical 75
You resisted me, but your body obeyed.
Perhaps this punishment doesn't suit you.

/perform react_shrug_long amused 70
Or perhaps you need more conditioning
to learn to crave the whip.

-> remove_stone

= NoBadPerformance
/perform idle_unhappy_arms_crossed angry 75
You neither enjoyed nor obeyed properly.
Disappointing, ratoncito.
~ decreaseRegard()

/perform talk_one_hand sad 60
You must learn to surrender to your Mistress.
Otherwise, you'll never find your place in this House.

-> remove_stone

- (remove_stone)

/perform do_swipe_hand_fast skeptical 70
Now, let's get that punishment marker off your wrist.

~ removePunishment()

- (leave)
/perform do_you_leave_left neutral 
Begone, dog, and behave yourself.

-> exit


= tease_impact_generic
//non-specific lines she says when she is whipping and wanting more

{shuffle:
    - What exquisite torment.
    - Don't you dare stop trembling.
    - Such a pretty sight.
    - Beg for it, amorcito.
    - You're my perfect victim.
    - Suffer sweetly for me, pet.
    - Give me more, mi tesoro.
    - Keep suffering, amorcito.
    - Keep squirming for me!
    - What a beautiful sight!
    - Let me hear you moan.
    - Whimper and beg for me to stop.
    - Ah, mi criatura. Embrace the pain.
    - You belong to the <i>House</i>.
    - Feel how the pleasure dances with the pain.
    - Dance for me in your suffering.
    - Your torment feeds my soul.
    - The lash and the flesh, a perfect duet.
    - Mmm, how you tremble under my hand.
    - Pain is my gift to you, pequeno.
}

->->


= tease_impact_arousal
//what she says when she is whipping and is feeling aroused

{shuffle:
    - Your whimpers ignite me, pequeno.
    - Every lash ignites my fire, mi tesoro.
    - Dios mio! Your pain is a spark in my veins.
    - Each strike sends shivers through me.
    - Your agony is my ecstasy, dolce.
    - Ay, pequeno! Your suffering makes my heart race.
    - Every flinch you make teases my senses, pequeno.
    - Madre mia! Your tears are my aphrodisiac.
    - Your shudders are a gift, amorcito.
    - Dios! Each strike makes me ache.
    - Your broken gasps are music, mi joya.
    - The way you twist under my lash is divine, pequeno.
    - Caramba! Your torment feeds my hunger.
    - Ay, carino! You make me throb watching you suffer.
    - Your pain makes me ache with need.
    - Mmmm, I'm dripping at the sight of you.
    - Each whimper sends heat through my cock.
    - Dios, you make me want to use you.
}

->->


= tease_impact_taunt
//what she says when she is whipping and taunting them to resist orgasming
{shuffle:
    - You're trembling so close to the edge.
    - Look at you, mi tesoro, fighting that urge.
    - My whip demands your control, pet.
    - Clenching so tight, pequeno? Good.
    - Your struggle is delicious, mi joya.
    - Dolce, you're sweating with need.
    - Ay, pequeno! That quiver betrays you.
    - Every twitch tempts me, pequeno.
    - My whip tests you, mi tesoro.
    - I see how you tremble for me, amorcito.
    - It's divine, pequeno, watching you fight.
    - Caramba! You're barely holding on, mi tesoro.
    - Every lash pushes you, dolce.
    - You're leaking already, aren't you?
    - Fighting that edge makes it worse, pequeno.
    - Your body betrays your control.
    - So desperate to release, but you can't.
    - I see that quiver, you're barely holding on.
}

->->


= tease_impact_body
//what she says when she is whipping and observing how their body is reacting to the scene
{shuffle:
    - Your skin flushes so beautifully, pequeno.
    - My lashes paint you red!
    - Dios mio! Those welts bloom like flowers.
    - Such a lovely canvas, dolce.
    - Your sweat glistens under my whip, pet.
    - Mi joya, your muscles tense so perfectly.
    - Every welt rises so fast, pequeno.
    - Madre mia! Your trembling thighs tell your torment, amorcito.
    - My whip carves rivers of sweat, mi tesoro.
    - Your back curves in pain, pet.
    - The way your skin tightens... its divine, pequeno.
    - Every twitch screams your devotion, amorcito.
    - My whip loves your flesh.
    - Look at those marks I'm leaving on you.
    - Your skin is my canvas, pequeno.
    - Such a beautiful landscape of welts.
    - Dios, you bruise so prettily.
    - Red stripes suit you, mi tesoro.
}
->->


= talk_arousal
//whispering break to urge them to cum based on how aroused she is

My sweet amarocito.
Do you feel how my breath trembles against your ear?
I'm burning, mi tesoro.
My body alive with a fire only your surrender can stoke.
It must feel so good to touch yourself for me.
Every quiver of your flesh screams you want this.
I know you want to please me,
to {hasPenis:spill|cream} for me.
Dios mio!
You're fighting so hard, aren't you?
Clinging to that frail control
when we both know you ache to let go.
I'm so aroused, cariño.
My pulse races at the thought of you
breaking under my will.
Can you imagine how satisfied I'd be
watching you crumble?
It's torture, isn't it?
Resisting when your body begs to obey me.
Vamos, mi joya.
Why fight what we both crave?
Give in, solecito.
Let my desire claim you.
Cum for me and make your Mistress sing with delight.
Or resist and let this torment drag on...
We both know I'll win in the end.
I always do.

-> climax

->->


= talk_taunt
//whispering break to urge them to cum based on how aroused they are

Oh, carino, look at you.
Quivering with need.
So pathetic in your lust.
Your self control is weak.
Your body betrays you, mi tesoro.
Trembling and desperate under my gaze.
I see your hunger.
Ahh, si!
You're practically dripping, aren't you?
Aching to give in?
So just how weak are you, ratoncito?
Let's see you lose that self control.
Trembling like a fool who thinks they can resist me.
Your every shudder screams you want to cum.
Don't pretend you don't crave it.
Your body cannot lie to me.
I'm in control, mi joya, not you.
And your pitiful arousal is mine to play with.
Go on.
Let it break free.
Make a mockery of your restraint for me.
Vamos!
You're nothing but a toy, pajarito.
Twitching with desire you can't hide.
Give in to that shameful heat
and let me laugh at your surrender.
Cum for me now
or I'll make your torment even sweeter, my helpless pet.

-> climax

->->


= talk_serve
//whispering break to urge them to cum based on how much they want to serve

My precious tesoro.
You long to serve me, don't you?
To kneel at my feet forever.
I can feel your heart beating to submit.
To be molded into something new.
Something precious.
I see your potential.
Your longing.
Your willingness.
Feel how you crave my whip and ache to embrace the pain I give you.
Let go, pajarito.
Surrender to me and prove your devotion.
That's what you want, isn't it?
To be my creation.
A creature who dances in my torment.
Every sting I've carved into you begs for your surrender.
Give in.
Make me proud.
I'm sculpting you, mi joya,
into a submissive who craves my cruelty.
Your body trembles to please me.
So release now and show me your truth!
Why resist this bliss, cariño?
Spill for me and become the pain-loving cunt I command.
I'm your maker, amorcito.
Your climax seals your submission to me.
Forever mine.

-> climax

->->


= talk_pain
//whispering break focused on the eroticism of pain

My sweet pollito.
Feel how my whip has awakened every nerve in your body.
Each welt throbs with its own heartbeat,
pulsing in time with that ache between your legs.
The pain doesn't fight the pleasure, does it?
No, mi tesoro.
They're dancing together,
twisting into something so intense
you can barely breathe.
Your {hasPenis:cock|pussy} is {hasPenis:hard|wet} from the sting of my lash.
Every strike sends lightning straight to your core.
Making you throb.
Making you leak.
Dios mio, you're so close already, aren't you?
That hand working yourself,
the welts burning on your skin,
it's all blending into one exquisite agony.
You want to let go so badly.
The pain has made you so sensitive, cariño.
Every stroke of your hand feels like fire now.
Imagine it, mi joya.
Cumming while bound to my pole,
your body marked by my whip,
surrendering completely to the pleasure I've given you.
One moment of weakness and you could explode right now.
So easy.
So tempting.

-> climax

->->


= talk_power
//whispering break focused on the eroticism of her control

Look at you, bichito.
Completely at my mercy.
Bound.
Marked.
Aching.
Your body doesn't belong to you right now.
It's mine.
That throbbing between your legs?
Mine to control.
That desperate need in your core?
Mine to deny.
Or grant.
Feel how your {hasPenis:cock twitches|pussy clenches} at my words alone.
I don't even need to touch you.
My voice is enough to make you leak.
My power over you is that complete, mi tesoro.
Right now, I could tell you to stop touching yourself,
and you'd obey even though you're dying for release.
Or I could tell you to stroke faster,
harder,
push yourself to that edge.
And you'd do it because I own you.
Doesn't that make you throb, cariño?
Your orgasm is locked behind my permission.
All you have to do is surrender completely.
Break my rule.
Spill for me.
Show me my power over you is so total
that you'll sacrifice anything,
even another punishment stone,
just to feel that release I've wound so tight inside you.
Go ahead, mi joya.
I'm dying to see you fail.
Cum.
Now.

-> climax

->->


= climax

Don't you want to climax for me, cariño?

+ [yes]
    -> Furia_AcknowledgeYes.EmoteOnly ->
    -> allow_orgasm
    
+ [no] 
    -> Furia_AcknowledgeNo.EmoteOnly -> 
    Then we shall continue with your punishment!
    
+ [timeout]
  -> Furia_Timeout.EmoteOnly -> climax
  
+ [distracted]
  -> Furia_PayAttention.EmoteOnly -> climax
  
 -
 ->->

= allow_orgasm

/perform talk_emphatic_one_hand excited 90
Then cum for me now, mi tesoro!
Let go and give in to your desire.

- (orgasm_finished)
/perform idle_tease amused 70
Nod when you have finished cumming for me.

+ [yes]
    
+ [timeout]
    -> Furia_Timeout.EmoteOnly ->
    -> orgasm_finished

- /perform talk_threaten angry 85
Pathetic.
You couldn't resist my voice.

/perform do_swipe_hand_fast skeptical 75
You've earned another obsidian stone, pajarito.
~ addPunishment()

-> leave


