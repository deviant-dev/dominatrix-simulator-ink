=== nega_punishment_light_bootlick ===
//punishment_light
~ setScene(punishment_scene, Nega, "nega_punishment_light_bootlick", -> nega_punishment_light_bootlick)
~ licked_boots = true


/scene attic.chair
/music nega-punishment-light
/ambient howling-wind
/character nega sex_chair_openlegs_wide
TODO: add variation so sometimes the player can touch themselves and other times they can't. No orgasm

{yuki_task_puppyplay_intro == 1: 
    -> dog
    - else:
    -> start
}


= dog
You like being Yuki's little pup, don't you? #01

+ [yes]
    Well, now you can be my little flea bag. #02
    A dirty, little dog for me to use and abuse. #03
    -> start
    
+ [no]
    -> Nega_Unhappy.EmoteOnly ->
    -> start


= start
Get down on all fours like the filthy dog you are, maggot! #04

+ [pose:hands_and_knees]

+ [timeout] -> Nega_Timeout.EmoteOnly -> start

- That's right. #05

- (please)
/perform sex_chair_crosslegs_tease
You're a dirty beast, #06

willing to do anything to please me. #07

Isn't that right? #08

+ [yes]
    That's what I thought. #09
    Let's make you prove it! #10
    
+ [no]
    -> Nega_Unhappy.EmoteOnly ->
    I'm going to teach you your place. #11
    
+ [timeout] -> Nega_Timeout.EmoteOnly -> please

+ [distracted] -> Nega_PayAttention.EmoteOnly -> please

- (personal)
/perform sex_chair_crosslegs_left_present
You're going to get up close and personal #12
with the bottom of my boots. #13

Got it, {isBoy:dog|bitch}? #14

+ [yes]
    Good. #15
    
+ [no]
    -> Nega_CheckIfInterested.EmoteOnly ->
    
+ [timeout] -> Nega_Timeout.EmoteOnly -> personal

+ [distracted] -> Nega_PayAttention.EmoteOnly -> personal


- (kiss_feet)
Before I give you the pleasure of cleaning my boot, #16
you will thank me for the privilege. #17

/perform sex_chair_crosslegs_snap_right_fingers
Come here and kiss my boot, {isBoy: worm|cunt}! #18

+ [pose:kiss_feet]
    -> Nega_GoodJob.EmoteOnly ->
    
+ [no] 
    No?! #19
    -> Nega_CheckIfInterested.EmoteOnly ->

+ [timeout] -> kiss_feet


- (sit)
/perform sex_chair_crosslegs_tease
Now, sit up, {isBoy:dog|bitch}. #20

TODO: add variation with no puppy pose if they haven't done Yuki's intro yet - use present kneel

+ [pose:puppy_sit]

+ [timeout] -> Nega_Timeout.EmoteOnly -> personal


-
This is where you belong. #21
At my feet. #22
Accept your place beneath me. #23


- (worthy)
/perform sex_chair_openlegs_idle_tease
You're not worthy of being anything more than a {isBoy: filthy dog|dirty bitch} in my presence. #24

Are you? #25

+ [yes]
    If I say you're not worthy. #26
    Then you're not fucking worthy. #27
    Got it? #28
    
    ++ [yes]
            Good. #29
            Don't fucking forget it! #30
            
    ++ [no]
            You've obviously forgotten who you're talking to. #31
            
    ++ [timeout] -> Nega_PayAttention.EmoteOnly -> worthy
    
+ [no]
    -> Nega_AcknowledgeYes.EmoteOnly ->
    
+ [timeout] -> Nega_Timeout.EmoteOnly -> personal

+ [distracted] -> Nega_PayAttention.EmoteOnly -> personal


- 
/perform sex_chair_crosslegs_idle_left
Let's make you prove your loyalty to me, pooch. #32

I stepped outside earlier and got the bottoms of my boots dirty. #33

~ tries = 0
- (ask_clean_boot) 
~ tries++

{cycle:
    - Would you be a dear and clean that up for me with your tongue? #34
    - Wallow like a mutt and clean that up, won't you? #35
}
    
+ [yes]
    You're such a dirty {isBoy: dog|bitch}! #36
    I love it! #37
    Now get busy! #38
    -> accept_boot_lick
    
+ [no]
    {shuffle:
        - You ungrateful loser! #39
        - You're trying my patience. #40
    }
    
    {shuffle: 
        - Here's a piece of Obsidian. #41
        - You'll take a punishment for that. #42
    }
    
    ~ addPunishment()
    { tries < 2:
        -> ask_clean_boot
    - else:
        -> deny_boot_lick
    }

+ [timeout] -> Nega_Timeout.EmoteOnly -> ask_clean_boot

+ [distracted] -> Nega_PayAttention.EmoteOnly -> ask_clean_boot

- (accept_boot_lick)
//The idea here is that the player has to move their head in a "yes"-like motion to "lick" the boot. It would be really cool if there was actually dirt on the boot that disappeared/fell off as the player "licked"

TODO: Replace with head movement mini-game eventually
- (lick_1) 
//Player must move head up and down near the bottom of the boot.

~ temp lickCount = RANDOM(3,5)

- (lick_again)
~ lickCount--

+ [pose:lick_foot]
    -> lick_comment ->
    {lickCount == 0: -> licks_done}
    
+ [timeout] -> bad_lick ->

- 
{shuffle:
    - Lick it. #43
    - -> Nega_Keep_Going ->
    - Lick. #44
    - Use that tongue. #45
    - Again! #46
    - -> Nega_Keep_Going ->
}

-> lick_again

- (licks_done)
-> Nega_GoodJob ->

- (beg)
/perform sex_chair_openlegs_idle
Get up on your knees and beg me to let you clean my other boot. #47
Beg like the filthy whelp you are. #48

TODO: add variation with no puppy pose if they haven't done Yuki's intro yet - use present_kneel

+ [pose:puppy_beg]

+ [timeout] -> Nega_PayAttention.EmoteOnly -> beg 


- 
/perform sex_chair_openlegs_idle_tease
Hrmm. #49
I'm not convinced. #50

I want to see how horny you are for my filthy boots. #51

-> Nega_prepare_masturbate ->

/perform sex_chair_openlegs_wide
That's it. #52

-> Nega_Masturbation_Comments.EmoteOnly ->
-> Nega_Masturbation_Comments.EmoteOnly ->

Such a slutty, little {isBoy: mutt|tail} just begging to clean my boots. #53

- (switch_feet) 
/perform sex_chair_crosslegs_idle_right

Keep groping yourself until I tell you to stop. #54

{shuffle:
    - Now, for the other boot, {isBoy:mutt|bitch}! #55
    - My other boot deserves the same attention as the first. #56
}

~ temp lickCount2 = RANDOM(3,7)

- (lick_again2)
~ lickCount2--

+ [pose:lick_foot]
    -> lick_comment ->
    {lickCount2 == 0: -> licks_done2}
    
+ [timeout] -> bad_lick ->

- 
{cycle:
    - Lick it. #57
    - -> Nega_Keep_Going ->
    - Don't disappoint me now, doggy. #58
    - Lick. #59
    - Use that tongue. #60
    - Again! #61
    - Keep going! #62
    - Keep licking, {isBoy: dog|bitch}! #63
}

-> Nega_Masturbation_Comments.EmoteOnly ->
-> lick_again2

- (licks_done2)

{shuffle:
    - No more {isBoy: stroking|rubbing}. #64
    - Enough touching. #65
    - Stop grinding. #66
}

{shuffle:
    - Clean enough! #67
    - That's enough. I'm satisfied. #68
    - I'm bored of this. #69
}

/perform sex_chair_crosslegs_grind
/wait 1

- (inspect) //Nega inspects the bottoms of her boots.

If your tongue is this thorough, #70
It might someday be useful for other things as well... #71

- (deny_boot_lick)
/perform sex_chair_crosslegs_snap_right_fingers
But...too bad. #72
Playtime's over, {isBoy: mongrel|bitch}! #73
Get off the floor and go away. #74

- -> exit


= lick_comment

{shuffle:
    - Such a dirty dog. #75
    - You're so disgusting! #76
    - What a lowly pooch you are. #77
    - You're less important than the dirt beneath my heels. #78
    - You make a good shoe polisher! #79
    - Perhaps I'll have you clean up my shoe closet one of these days. #80
    - Get all that dirt! #81
    - Don't you miss a spot! #82
    - What a filthy mongrel. #83
    - You belong at the bottom of my feet. #84
}

->->


= bad_lick

{shuffle:
    - -> Nega_BadJob.EmoteOnly ->
    - -> Nega_BadJob.EmoteOnly ->
    - -> Nega_BadJob.EmoteOnly ->
    - -> Nega_BadJob.EmoteOnly ->
    - You should know how to do this by now, {isBoy:mutt|bitch}! #85
    - It's not that hard. Use your tongue! #86
    - I'm not convinced you want to serve me. #87
}

->->
