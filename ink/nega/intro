=== nega_intro ===

-> startScene(false)


// Support forcing the scene to restart via 'skip_checks'
= skip_checks

-> startScene(true)


= startScene(skipChecks)

-> select_gender_check ->

// Make sure we are set up as expected for Nega's intro.
~ rewards = 1
~ punishments = 0
~ reward_just_removed = false
~ tutorial_step = at_nega_intro
~ nega_shortcut = true
~ temp cameFromHub = current_mistress == None
~ setScene(intro_scene, Nega)

/scene Attic.Approach
/music nega
/ambient howling-wind

+ [move: Nega]

- /scene Attic.Ready
/character nega

{skipChecks: -> skip}
/log {cameFromHub}
// Return to previous location if we've been here before.
{check_reward: -> Nega_CheckIfInterested -> check_reward}
{stand_straight: -> Nega_CheckIfInterested -> stand_straight}
{present_ass || cameFromHub: -> Nega_CheckIfInterested -> present_ass}

- (skip)

{isWeb(): -> web_start | -> start}


= web_start

<blockquote><i>You are disoriented for a moment as everything turns black and the scene around you changes.</i></blockquote>

<blockquote><i>You find yourself in a dim and dusty attic. Old out-of-place furniture, scraps of paper, and trinkets litter the room. A sullen and attractive woman in a tattered maid's uniform smiles menacingly at you...</i></blockquote>

+ [Continue]
    -> start


//play some kind of dischordant sound when the player enters?
= start

/perform idle_bored amused 45
Don't worry, pretty {player_name}. #01
Expect that to happen once in a while. #02

/perform talk_lean_forward amused 75
Walk out one door, expecting to arrive somewhere, #03

/perform talk_one_finger happy 50
and suddenly find yourself somewhere else. #04

/perform talk_behold skeptical 60
You could think of them as ... #05

/perform do_air_quotes happy 60
"interlewds", I suppose. #06

/perform react_dismissive skeptical 75
You'll get used to it. #07

- /perform idle_bored aroused 70
I pulled in a little favor to make sure #08
that kiss-ass Yuki wasn't paying attention. #09

// If the player had the nega ending, skip the warning
{yuki_intro.Nega_finish: -> nega_tease }

- (yuki_warning)

/perform talk_lean_forward angry 75
I bet she told you not to talk to me, didn't she? #10

+ [yes]
    ~ addObey()

    /perform react_disappointed sad 35
    Don't waste your time with that simpering brat. #11

    /perform react_excited happy 75
    Oh sure, she'll be all sweet and attentive, #12

    /perform react_crazy_gesture sad 65
    But she'll bore the shit out of you eventually. #13

    /perform idle_sassy aroused 40
    Stick with me if you really want to have some fun. #14

+ [no]
    ~ addDisobey()

    /perform react_thankful skeptical 65
    No? I'm sure she would have. #15

    /perform talk_consider amused 50
    You must be some kind of idiot who doesn't listen. #16

    /perform react_point_at_player_angry angry 60
    Don't pull that shit with me. #17

    /perform react_dumb happy 45
    Try to play dumb all you want,  #18

    /perform idle_unhappy angry 30
    but you better do what you're fucking told. #19

+ [timeout]
    /perform idle_cocks_head_to_left
    Well? #20
    -> yuki_warning

+[distracted] -> Nega_PayAttention -> yuki_warning

- (nega_tease)

/perform do_inspect aroused 40
How's that cute little ass of yours? #21

/perform talk_consider aroused 45
I enjoyed the show. #22

- (nega_tease_again)

/perform tease_ass_caress amused 60
Does your poor tushy sting? #23


+ [yes]
    ~ addObey()

    /perform  react_thankful amused 60
    Poor wittle baby. #24

+ [no]
    ~ addDisobey()

    /perform talk_consider aroused 50
    Oh, a tough {isBoy:guy|girl}, huh? #25

    /perform do_fist_pump excited 65
    Nice. #26

+ [timeout] -> Nega_Timeout -> nega_tease_again

+ [distracted] -> Nega_PayAttention -> nega_tease_again

//have player present ass before she gets them to give up their stone
-{headmistress_masturbated_for_crowd:
    /perform talk_lean_forward aroused 60
    You're such a slut for an audience. #27

- else:

    /perform idle_bored sad 25
    Too bad you didn't want to perform more. #28
}

/perform talk_question skeptical 35
Let's have a look. #29

- (present_ass)

/perform react_point_at_player_angry angry 40
Turn around and bend over. #30

/perform talk_lean_forward angry 60
Hands on your knees and ass in the air! #31

+ [pose:present_ass_reverse]
+ [no] -> Nega_CheckIfInterested -> present_ass
+ [timeout] -> present_ass

- /perform do_inspect amused 75
My, my. Your ass turns a nice shade of pink. #32

/perform idle_unhappy amused 65
She does such good work. #33

TODO: Support 'wiggle' action

// - (wiggle)
// Wiggle it for me. #34

// //check to see if the headset wiggles?

// + [wiggle-success:5]
//     -> Nega_GoodJob ->
//     ++ [wiggle-success:5]
//         That's enough.  #35
        /perform idle_cocks_head_to_left amused 25
        Turn back around and present yourself.  #36
//         ~ addObey()
// + [wiggle-timeout]
//     -> Nega_BadJob ->
//     ~ addDisobey()
//     Did I tell you to stop? #37
//     -> wiggle
// // + [no] -> Nega_CheckIfInterested -> wiggle

~ tries = 0
- (stand_straight)
~ tries++

+ [pose:raised_chin]
+ { tries < 2 } [timeout] -> stand_straight
+ { tries >= 2 } [timeout] -> Nega_CheckIfInterested -> stand_straight
// + [no] -> CheckIfInterested -> stand_straight

- (check_reward)
/perform talk_question amused 35
Now. You have something I want. #38

/perform talk_one_finger happy 45
Once you give it to me, it's gone. No take-backsies! #39

/perform talk_consider aroused 50
You have to give it to me to play. #40

/perform talk_lean_forward skeptical 65
Those are the rules. #41

/perform react_point_at_player_angry angry 65
And I better warn you, I don't always play nice,  #42

/perform react_dumb amused 45
so you better think hard before you give it to me. #43

- (check_reward_loop)

/perform talk_lean_forward skeptical 50
What are you going to do, pretty {player_name}? #44

/perform idle_tease aroused 65
Do you want to try your luck? #45

+ [yes]
    ~ addObey()

    /perform do_fist_pump excited 65
    That's right. #46

    /perform do_tap_wrist happy 40
    You're going to give me that lovely little Bloodstone. #47

    /perform talk_lean_forward aroused 75
    Maybe you'll get lucky and get something nice in return. #48
    -> nega_reward_dice

+ [timeout]

    /perform idle skeptical skeptical 65
    Well. Are you going to give me what I want? #55
    {inTutorial:->check_reward|->check_reward_loop}
    //Can't find a way to test this logic statement, halp

+ [distracted] -> Nega_PayAttention -> check_reward_loop
    //Had to move timeout and distracted here to make them work properly


+ [no]
    ~ addDisobey()

    /perform talk_annoyed amused 35
    What a fucking pity. #49
    - (check_reward_again)

    /perform idle_sassy aroused 75
    Are you sure you don't want to give me that pretty Bloodstone? #50

    ++[yes]
        ~ addDisobey()

        /perform idle_unhappy_stomp angry 45
        Stop wasting my time! #51

        /perform do_get_the_fuck_out angry 95
        Get the fuck out of here. #52
        -> exit

    ++[no]
        ~ addObey()

        /perform tease_caress_breast_left aroused 40
        Very good.  #53

        /perform talk_behold aroused 75
        I can be a lot of fun with the right... toy. #54
         -> nega_reward_dice

    ++[distracted] -> Nega_PayAttention -> check_reward_again

    ++[timeout] -> Nega_Timeout -> check_reward_again



// When the Goddess interrupts Nega playing with the player.
= surprise_interrupt

TODO: Shake attic? Vibrate controllers with rumble?
/sound goddess_interruption
/ambient none
/wait 2

/perform sex_squat_idle angry 60
Damn it! I was just getting started. #132

/music goddess
/wait 1
/scene None

Don't worry. You'll get yours. #133

~ tutorial_nega_intro_surprise_complete = true
~ tutorial_nega_intro_complete = true
-> goddess_monitoring_intro.angry


// When the Goddess interrupts because Nega purposely tells on the player.
= spiteful_interrupted

/perform do_throat_cut angry 75
I'll make sure you pay one way or another. #56


TODO: Shake attic? Vibrate controllers with rumble?
/sound goddess_interruption
/ambient none
/wait 2
/perform talk_behold excited 57

Oh yeah. Hear that? You're gonna get yours now. #57

/perform idle_unhappy_stomp
/music goddess
/wait 1
/scene None
That's what you get for crossing me, you little bitch. #58

~ tutorial_nega_intro_spiteful_complete = true
~ tutorial_nega_intro_complete = true
-> goddess_monitoring_intro.angry
