=== goddess_intro ===
-> select_gender_check ->

~ setScene(intro_scene, Goddess, "goddess_intro", -> goddess_intro)
~ punishments = 0
~ rewards = 0
~ tutorial_step = at_goddess_intro
~ goddess_shortcut = true

- (flash_scenes)

- (flash_1)
/scene bedroom.flash1
/music goddess-intro-1
/ambient orgy

+ [wait:3]

- (flash_2)
/scene bedroom.flash2

+ [wait:3]

- (flash_3)
/scene bedroom.flash3

+ [wait:4]

- /scene none
/wait 2

- (debug_jump)

/scene bedroom.intro
/ambient rain
/music goddess-intro-2
/character goddess idle_sassy angry 70

- Oh... How rude. #01

-> continue ->

~ addPunishmentWithPath(-> load_at_interview)

/perform react_disappointed angry 50
And I was having so much fun. #02

/emotion amused 60
Ah, well... #03

-> interview


= load_at_interview
/scene bedroom.intro
/ambient rain
/music goddess-intro-2
-> interview


= interview

/perform talk_question amused 60
How did you get in here? #04

~ tries = 0
/perform idle_sassy angry 70
Did Mistress Yuki-Onna let you in? #05

- (ask_entry)
~ tries++

+ [yes]
    /perform talk_annoyed angry 50
    I've told that girl not to interrupt me #06
    when I'm playing with my toys. #07
    -> stay
    
+ [no]
    /perform idle_sassy amused 60
    No? Another curious guest, then. #08
    -> stay
    
+ { tries  <=1 } [timeout] 
     /perform react_nod_big disgusted 60
    It's a simple question. Nod your head for yes. #09
    /perform react_head_toss_casual disgusted 60
    Shake your head for no. #10
    //Attempting to get the goddess to shake her head in time with the lines
    /wait 1
    -> ask_again

+ {tries >=2 } [timeout] 
    -> Goddess_Timeout -> 
    /wait
    -> ask_again

+ [distracted] -> Goddess_PayAttention -> ask_again

= ask_again

{ shuffle:

    - /perform talk_lean_forward amused 35
    Did my Yuki let you in? #15

    - /perform react_scoffs amused 35
    Did that naughty little maid let you in? #16

    - /perform talk_consider amused 35
    Did an overly enthusiastic maid bring you to me? #17

    - /perform talk_annoyed amused 35
    Yuki-Onna is disobeying me again. She let you in, didn't she? #18
}
    -> ask_entry

= stay  
- /perform talk_consider amused 35
Well, you can't stay here. #19

/perform idle_skeptical amused 35
Only the truly exceptional may stay in my presence. #20

/perform talk_one_finger aroused 65
And you are <i>obviously</i> not exceptional. #21

/perform idle_sassy skeptical 55
Are you? #22

- (ask_exceptional)

+ [yes]
   /perform react_laugh skeptical 70
   Oh how absolutely delusional you are! #23

   /perform react_shrug_long
   You are nowhere near worthy. #24

+ [no]
    /perform talk_one_finger happy 50
    Now, that is a very honest answer. #25

    /perform talk_consider amused 35
    Perhaps I will give you a chance to prove your worth. #26
    /wait

+ (ask_exceptional_again)[timeout]
    /perform idle_cocks_head_to_left disgusted 50
    Do you think you're exceptional? #27
    -> ask_exceptional

+ [distracted] -> Goddess_PayAttention -> ask_exceptional_again

- /perform tease_caress_breasts_large aroused 60
I bet that turned you on. Didn't it?<br/>Spying on me and my precious pets. #28

- (ask_spying)
+ [yes]
    /perform idle_tease amused 55
    Of course it did. #29
    I bet you'd like to see more of that, wouldn't you? #30

    /perform tease_bend_over aroused 80
    Perhaps you'd like to be one some day? #31
    
    /perform idle_bored skeptical 40
    Well, too bad. #11
    You haven't earned that priviledge yet. #12

+ [no]
    /perform talk_thoughtful disgusted 40
    No? How disappointing. #33

    /perform talk_bashful sad 50
    I don't think you're going to be much fun at all. #34
    -> ask_spying_done

+ (ask_spying_again)[timeout]
    /perform do_inspect aroused 35
    Be honest now. Spying on me turned you on, didn't it? #35
    -> ask_spying

+ [distracted] -> Goddess_PayAttention -> ask_spying_again


- (ask_spying_done)
/perform do_inspect aroused 50
I bet you have no idea where you are, do you? #36

- (ask_where)

/perform idle_sassy excited 50

+ [yes]
    /perform react_laugh amused 70
    You think you know where you are? #37
    You have no clue. #38

+ [no]
    /perform react_scoffs amused 70
    Of course you don't. #39

+ (ask_where_again)[timeout]
    /perform idle_unhappy_stomp angry 50
    Well... do you have any idea where you are? #40
    -> ask_where

+ [distracted] -> Goddess_PayAttention -> ask_where_again

- /perform talk_lean_forward amused 55
You're just a tiny little mouse stuck in a maze. #41

/perform talk_behold scared 65
You may never find your way out again. #42

/perform do_shake_finger amused 70
This is no ordinary house, mind you. #43

/perform talk_consider aroused 65
Even so. You are here now. #44

/perform do_inspect skeptical 50
And you won't be going anywhere for quite a while. #45

/perform talk_consider happy 45
That means I'll have to give you a name. #46

- (name)
/perform idle_tease amused 70
Would you like that? #47

+[yes]
    /emotion happy 50
    Well, well. I must say I'm pleased. #48

    /perform talk_behold excited 45
    I think we are off to a good start. #49

    /perform idle_fake_annoyed happy 50
    Don't you? #50

    ++[yes]
        /perform idle_cocks_head_to_left amused 30
        Keep this up and I might just take an interest in you. #51

    ++[no] -> DemandApology ->
        /perform do_inspect skeptical 50
        Now... as for a name. #52
        
    ++ [timeout] -> Goddess_Timeout -> name

+ [no]
    /perform idle_fake_annoyed disgusted 65
    Too bad. #53
    Whatever name you had before is gone. #54

+ [timeout]
    /emotion angry 50
    Cat got your tongue? #55
    Well... I'll ignore that for now. #56

- /perform talk_one_finger amused 50
I will name you... "{player_name}". #57

/perform talk_lean_forward aroused 60
I expect you to respond to "{player_name}" from now on. #58

/perform idle_unhappy aroused 45
Until you earn a new name, that is. #59

/perform idle_fake_annoyed aroused 30
Do you understand? #60

+ [yes]
    -> Goddess_GoodJob ->

+ [no]
    /perform talk_annoyed disgusted 45
    Let's try this again. Your name is now "{player_name}". #62

    /perform idle_unhappy disgusted 50
    Everyone who meets you will refer to you as "{player_name}". #63

    /perform talk_one_finger disgusted 55
    One day, if you are very <i>very</i> good, you may earn a new name. #64

    /perform talk_consider disgusted 60
    But for now, your name is "{player_name}". #65

+ [timeout]
    /perform react_shrug_long amused 40
    Well, perhaps you're a little overwhelmed. #66

    /perform talk_lean_forward amused 55
    That's to be expected. #67

    /perform talk_lean_forward aroused 65
    Just remember your name is "{player_name}" #68

    /perform idle_clasp_hands angry 40
    until I decide otherwise. #69

- /wait

/perform idle_bored amused 35
I really must send you away soon. #70

/perform talk_question amused 45
But first, let me have a good look at you. #71

+ [continue]

~ tries = 0
- (inspection)
~ tries++

/perform do_point_down amused 45
{ cycle:
    - Come here. #72
    - Come here, {player_name}. #73
    - /emotion angry
    I <i>said</i> come here! #74
}

+ [move]
    /wait
    -> Goddess_GoodJob ->
+ { tries < 3 } [timeout]
    -> inspection
+ { tries >= 3 } [timeout]
    -> CheckIfInterested -> inspection

- /music goddess-intro-3
/perform idle_fake_annoyed amused 30
I'm always curious about our new guests. #75

/perform do_inspect aroused 50
I so rarely take the time to inspect them personally #76
when they first arrive. #77

/perform react_thankful excited 30
This is a bit of an unexpected treat. #78

/perform talk_lean_forward aroused 50
You don't mind, do you? #79

/perform idle_sassy aroused 30

+ [yes] How unfortunate. #80
    /perform talk_annoyed disgusted 30
    Honestly, I don't even know why I bother asking. #81
+ [no]
    /perform talk_one_finger skeptical 30
    Mmm... I think someone might be enjoying this. #82
+ [timeout]
    /perform react_scoffs amused 60
    That's alright. It's not like you have a choice anyway. #83
+ [distracted] -> Goddess_PayAttention -> stand_straight

~ tries = 0
- (stand_straight)
~ tries++

{ shuffle:
    - Stand up straight. #84
    - Come on. Lift that head a bit for me. #85
    - Chin up, little one. Let me inspect you. #86
}

/perform idle_sassy aroused 25

+ [pose:raised_chin]
+ { tries < 3 } [timeout] -> stand_straight
+ { tries >= 3 } [timeout] -> CheckIfInterested -> stand_straight
// + [no] -> CheckIfInterested -> stand_straight

- -> Goddess_GoodJob ->

// /perform circle_player

/emotion excited 45
Push your shoulders back and arch that lovely spine. #88
/wait

/perform talk_lean_forward amused 45
Stick that pretty little ass out where I can see it. #89

/perform idle_sassy aroused 60
That's it. #87

///wait for:circle_player
/perform idle_sassy amused 75
Now then... #90

~tries = 0
- (kneel)
~tries++

{ cycle:

    - /perform do_point_down aroused 70
    Kneel. #91

    - /perform do_point_down amused 50
    Get down on your knees. #92

    - /perform do_point_down angry 45
    You belong on your knees. Down. #93
}

+ [pose:kneel]
+ { tries < 3 } [timeout] -> kneel
+ { tries >= 3 } [timeout] -> CheckIfInterested -> kneel
// + [no] -> CheckIfInterested -> kneel

- -> Goddess_GoodJob ->

/perform do_inspect aroused 30
Mmmmm... so much delicious flesh to work with. #94

/emotion excited 25
Strong bones and muscles. #95

/perform talk_one_finger excited 45
You'll need to be flexible, as well. #96

/emotion aroused 70
Oh yes... There is much here we can work with. #97

-> continue ->
~ addRewardWithPath(-> load_at_present_self)
-> present_self


= load_at_present_self

/scene bedroom.close
/ambient rain
/music goddess-intro-3

-> present_self


= present_self
~ tries = 0
- (present_self_again)
~ tries++

/perform idle_unhappy amused 45
Arms behind your head. #98

/emotion happy 55
Thrust your chest out. Knees apart. #99

/perform idle_sassy aroused 45
I want to see you present yourself more fully to me. #100


+ [pose:present_kneel]
+ { tries < 2 } [timeout] -> present_self_again
//+ { tries >= 2 } [timeout]
+ { tries >= 2 } [timeout] -> CheckIfInterested -> present_self_again
// + [no] -> CheckIfInterested -> present_self_again

- -> Goddess_GoodJob ->

/perform idle_fake_annoyed aroused 45
Look at you. So obedient. So ready. #101
/wait 1

/emotion amused 45
You may go. But first, a reward. #102

You may show me how much you appreciate my instruction. #103

/perform do_point_down aroused 60
You may bow before me and kiss my feet. #104

~ tries = 0
- (kiss_feet)
~ tries++

/perform idle_present_foot amused 60

Bow down, kiss, then back to your pose like a good little {player_name}. #105

+ [pose:kiss_feet]
+ { tries < 3 } [timeout] -> kiss_feet
+ { tries >= 3 } [timeout]
+ [no]
    /perform react_whatever skeptical 60
    No?! #106

    /perform react_thankful skeptical 70
    Turning down such a reward. You are a strange one. #107

- /perform idle_skeptical amused 60
That's enough. #108

~ tries = 0
- (stand_up)
~ tries++

//new line with vo
/perform talk_question amused 60
Come back up to your feet, {player_name}. #158

+ [pose:raised_chin]
+ { tries < 3} [timeout] -> stand_up
+ { tries >= 3} [timeout]

- /perform do_inspect skeptical 60
You might just prove useful one day, #109

/perform idle_bored disgusted 70
but you'll need much more training first. #110

/perform do_now_leave amused 60
Off to Mistress Yuki-Onna with you. #111

/perform idle_skeptical amused 75
I'm sure she can show you the ropes, so to speak. #112


+ [wait]

- (video_jump)

-> ByeForNow ->

- ~ yuki_shortcut = true
~ tutorial_step = at_yuki_intro
~ tutorial_goddess_intro_complete = true

// If this is the demo with just the Goddess, end the demo.
{deviation == demo:
    -> game_outro
- else:
    -> exit
}



= DemandApology

-/wait

{ cycle:

    - /perform react_point_at_player_angry angry 60
    Don't pretend to toy with me. #140

    - /perform talk_threaten angry 60
    I am not amused. #141

    - /perform react_scoffs angry 60
    Such behaviour is unacceptable. #142

    - /perform react_disappointed angry 60
    You are trying my patience, worm. #143
}

/emotion angry 50
You will apologize this instant. #144

/perform idle_unhappy_stomp

Tell me: Do you beg for my forgiveness? #145

+ [yes] -> Goddess_GoodJob ->
+ [no] -> CheckIfInterested ->

- /perform idle_skeptical angry 45
You are forgiven. #146

/perform react_point_at_player_angry angry 65
Don't let it happen again. #147
->->


= CheckIfInterested

{CheckIfInterested > 1: -> check_terse}

/perform idle_unhappy disgusted

These are the sorts of tasks demanded of you here. #148
- (check_again)

/perform talk_behold skeptical

Are you willing to simply do as I say like a good {player_name}? #149

/perform idle

+ [yes] -> IsInterested
+ [no]
+ [timeout] -> check_again

- (check_terse)

/emotion skeptical

Really? #150

/emotion angry 50

We will need to be rid of you if you can't perform. #151

Your willingness to serve is, after all, why you are here. #152

/perform talk_annoyed neutral

So I'll ask one last time: #153

/perform talk_lean_forward angry 50
Are you willing to perform for your goddess? #154

+ [yes] -> IsInterested
+ [no] -> NotInterested


= IsInterested

-> Goddess_GoodJob ->
/perform react_shrug_long skeptical
There is hope for you yet. #157

->->


= NotInterested

/perform talk_annoyed disgusted
Then away with you. #155
Perhaps another time when you are ready to learn your place. #156

-> ByeForNow -> game_outro


= ByeForNow

/perform do_snap_fingers

/scene bedroombroken.pissed

+ [wait 1.5]

- /character goddess idle_looming_huge angry 50

Bye for now, {player_name}. #113
+ [wait:5] ->->
