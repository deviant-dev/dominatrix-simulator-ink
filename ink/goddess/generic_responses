=== Goddess_GoodJob ===
-> DoEmoteOnly(false)


= EmoteOnly
-> DoEmoteOnly(true)


= DoEmoteOnly(emoteOnly)

{ shuffle:
    -{emoteOnly: /emotion happy 50 | /perform idle happy 50 }
    There's a good {player_name}. #114

    -{emoteOnly: /emotion aroused 65 | /perform idle_tease aroused 65 }
    What a good little {player_name}. #115

    -{emoteOnly: /emotion amused 55 | /perform talk_lean_forward amused 55 }
    That's my {player_name}. #116

    -{emoteOnly: /emotion happy 50 | /perform idle_clasp_hands happy 50 }
    Aren't you the sweetest little thing? #117

    -{emoteOnly: /emotion happy 50 | /perform talk_consider happy 50 }
    Very nice. #118

    -{emoteOnly: /emotion happy 45 | /perform react_thankful happy 45 }
    That will do just fine. #119

    -{emoteOnly: /emotion happy 40 | /perform do_inspect happy 40 }
    Quite satisfying. #120

    -{emoteOnly: /emotion aroused 65 | /perform idle_sassy aroused 65 }
    Mmmm. I like that. #121

    -{emoteOnly: /emotion happy 50 | /perform react_thankful happy 50 }
    Someone is being a good {player_name}. #122

    -{emoteOnly: /emotion happy 50 | /perform talk_consider happy 50 }
    Well done. #123

    -{emoteOnly: /emotion aroused 40 | /perform idle_tease aroused 40 }
    That's very pleasing. #124

    -{emoteOnly: /emotion aroused 60 | /perform do_inspect aroused 60 }
    I am pleased. #61
}

->->




=== Goddess_AcknowledgeYes ===
-> DoEmoteOnly(false)


= EmoteOnly
-> DoEmoteOnly(true)


= DoEmoteOnly(emoteOnly)

{shuffle:
    -{emoteOnly: /emotion aroused 65 | /perform idle_sassy aroused 65 }
    How delightful. #01

    -{emoteOnly: /emotion happy 50 | /perform talk_consider happy 50 }
    Very well. #02

    -{emoteOnly: /emotion happy 50 | /perform idle_clasp_hands happy 50 }
    So be it. #03

    -{emoteOnly: /emotion amused 60 | /perform talk_one_finger amused 60 }
    Alright then. #04

    -{emoteOnly: /emotion happy 50 | /perform do_inspect happy 50 }
    Excellent. #05
}

->->




=== Goddess_AcknowledgeNo ===
-> DoEmoteOnly(false)


= EmoteOnly
-> DoEmoteOnly(true)


= DoEmoteOnly(emoteOnly)

{shuffle:
    -{emoteOnly: /emotion aroused 65 | /perform idle_sassy skeptical 65 }
    I see. #01

    -{emoteOnly: /emotion happy 50 | /perform talk_consider amused 50 }
    A pity. #02

    -{emoteOnly: /emotion happy 50 | /perform idle_clasp_hands amused 50 }
    How unfortunate. #03

    -{emoteOnly: /emotion amused 60 | /perform talk_one_finger amused 60 }
    Too bad. #04

    -{emoteOnly: /emotion happy 50 | /perform do_inspect skeptical 50}
    As you wish. #05
}

->->




=== Goddess_Unhappy ===
-> DoEmoteOnly(false)


= EmoteOnly
-> DoEmoteOnly(true)


= DoEmoteOnly(emoteOnly)

{shuffle:
    -{emoteOnly: /emotion sad 55 | /perform idle_bored sad 55 }
    How disappointing. #01

    -{emoteOnly: /emotion skeptical 65 | /perform react_shake_head_disbelief skeptical 65 }
    Really? #02

    -{emoteOnly: /emotion disgusted 60 | /perform idle_unhappy disgusted 60 }
    I see. #03
}

->->




=== Goddess_Timeout ===
-> DoEmoteOnly(false)


= EmoteOnly
-> DoEmoteOnly(true)


= DoEmoteOnly(emoteOnly)


{ shuffle:
    -{emoteOnly: /emotion angry 45 | /perform idle_clasp_hands angry 45 }
    I asked you a question. #11

    -{emoteOnly: /emotion angry 55 | /perform talk_emphatic_one_hand angry 55 }
    Hurry up. I'm waiting. #12

    -{emoteOnly: /emotion skeptical 35 | /perform talk_lean_forward skeptical 35 }
    I don't like it when people make me wait. #13

    -{emoteOnly: /emotion angry 65 | /perform idle_skeptical angry 65 }
    What makes you think you can ignore me? Answer me. #14
}

->->




=== Goddess_PayAttention ===

{ cycle:
    - /perform idle_skeptical angry 80
    Look at me when I'm speaking to you, {player_name}. #125
    /wait 1

    - /perform talk_annoyed angry 80
    I expect your eyes on me at all times, #126
    unless I tell you otherwise. #127
    /wait 1

    - /perform idle_fake_annoyed angry 80
    Did I give you permission to look elsewhere? #128
    /wait 1

    - /perform talk_behold angry 80
    Didn't anyone ever teach you to look at people #129
    when they're speaking to you? #130
    /wait 1

    - /perform talk_one_finger angry 80
    Pay attention to me when you're in my presence. #131
    /wait 1

    - /perform idle_skeptical angry 80
    You're trying my patience. Pay attention. #132
    /wait 1

    - /perform idle skeptical 80
    Eyes on me, {player_name}. #133
    /wait 1
}

// Added in waits, as the lines felt rushed when transitioning

+ [pay-attention]
+ [timeout] -> Goddess_PayAttention

// TODO: Make # of cycle elements equal or change to cycle/shuffle
- { cycle:

    - /perform talk_consider happy 60
    Where was I? Oh yes... #134
    /wait 1

    - /perform react_scoffs amused 60
    That's better. Let's continue. #135
    /wait 1

    - /perform idle_clasp_hands happy 40
    Good {player_name}. #136
    /wait 1

    - /perform talk_annoyed amused 30
    Let's try that again. #137
    /wait 1

    - /perform idle_unhappy amused 30
    As I was saying... #138
    /wait 1

    - /perform idle_sassy amused 30
    Now... Let's start over. #139
    /wait 1
}

->->




=== Goddess_BadJob ===

/emotion angry 50

{ shuffle:
    - How disappointing. #01
    - Perhaps you do not belong here, after all. #02
    - I tire of your presence. #03
    - Try harder, little one. #04
    - That's not quite right. #05
    - Tiresome. #06
    - I expect more. #07
}

->->




=== Goddess_CheckIfInterested ===

{checked_if_interested: -> check_terse}

~checked_if_interested = true

/perform idle_unhappy disgusted

These are the sorts of tasks demanded of you here. #148
- (check_again)

/perform talk_behold skeptical

Are you willing to simply do as I say like a good {player_name}? #149

/perform idle

+ [yes] -> IsInterested
+ [no]
+ [timeout] -> check_again

- (check_terse)

/emotion skeptical

Really? #150

/emotion angry 50

We will need to be rid of you if you can't perform. #151

Your willingness to serve is, after all, why you are here. #152

/perform talk_annoyed neutral

So I'll ask one last time: #153

/perform talk_lean_forward angry 50
Are you willing to perform for your goddess? #154

+ [yes] -> IsInterested
+ [no] -> NotInterested


= IsInterested

// -> Goddess_GoodJob ->
/perform react_shrug_long skeptical
There is hope for you yet. #157

->->


= NotInterested

/perform talk_annoyed disgusted
Then away with you. #155
Perhaps another time when you are ready to learn your place. #156

-> Goddess_ByeForNow -> exit




=== Goddess_ByeForNow ===

/perform do_snap_fingers

/scene bedroombroken.pissed

+ [wait 1.5]

- (again)
/character goddess idle_looming_huge angry 50

Bye for now, {player_name}. #113
+ [wait:5]
-

// {debug: -> again}

// If showing the short demo,
// show the 'thank you for playing' screen
// before starting over.
{deviation == demo_short: -> demo_end ->}

->->




=== Goddess_SayNumberRangeOfType(-> sayNumber, start, end, wait) ===
~ temp current = start

- (loop)
-> sayNumber(current) ->

{
    - current == end:
        ->->
    - current < end:
        ~ current++
    - current > end:
        ~ current--
}

/wait {wait}
// /log {current}/{end}
-> loop




=== Goddess_SayNumberRange(start, end, wait) ===
-> Goddess_SayNumberRangeOfType(-> Goddess_SayNumber, start, end, wait) ->->




=== Goddess_SayNumberRangeSlow(start, end, wait) ===
-> Goddess_SayNumberRangeOfType(-> Goddess_SayNumberSlow, start, end, wait) ->->




=== Goddess_SayNumberRangeFast(start, end, wait) ===
-> Goddess_SayNumberRangeOfType(-> Goddess_SayNumberFast, start, end, wait) ->->




=== Goddess_SayNumber(n) ===
// NOTE: Keep these on separate lines for the subtitle system. -deviant
{
    - n == 1:
        One. #01
    - n == 2:
        Two. #02
    - n == 3:
        Three. #03
    - n == 4:
        Four. #04
    - n == 5:
        Five. #05
    - n == 6:
        Six. #06
    - n == 7:
        Seven. #07
    - n == 8:
        Eight. #08
    - n == 9:
        Nine. #09
    - n == 10:
        Ten. #10
    - n == 11:
        Eleven. #11
    - n == 12:
        Twelve. #12
    - n == 13:
        Thirteen. #13
    - n == 14:
        Fourteen. #14
    - n == 15:
        Fifteen. #15
    - n == 16:
        Sixteen. #16
    - n == 17:
        Seventeen. #17
    - n == 18:
        Eighteen. #18
    - n == 19:
        Nineteen. #19
    - n == 20:
        Twenty. #20
    - else:
        /logError Can't say number '{n}'.
}
->->




=== Goddess_SayNumberFast(n) ===
// NOTE: Keep these on separate lines for the subtitle system. -deviant
{
    - n == 1:
        One. #01
    - n == 2:
        Two. #02
    - n == 3:
        Three. #03
    - n == 4:
        Four. #04
    - n == 5:
        Five. #05
    - n == 6:
        Six. #06
    - n == 7:
        Seven. #07
    - n == 8:
        Eight. #08
    - n == 9:
        Nine. #09
    - n == 10:
        Ten. #10
    - n == 11:
        Eleven. #11
    - n == 12:
        Twelve. #12
    - n == 13:
        Thirteen. #13
    - n == 14:
        Fourteen. #14
    - n == 15:
        Fifteen. #15
    - n == 16:
        Sixteen. #16
    - n == 17:
        Seventeen. #17
    - n == 18:
        Eighteen. #18
    - n == 19:
        Nineteen. #19
    - n == 20:
        Twenty. #20
    - else:
        /logError Can't say number '{n}'.
}
->->




=== Goddess_SayNumberSlow(n) ===
// NOTE: Keep these on separate lines for the subtitle system. -deviant
{
    - n == 1:
        One. #01
    - n == 2:
        Two. #02
    - n == 3:
        Three. #03
    - n == 4:
        Four. #04
    - n == 5:
        Five. #05
    - n == 6:
        Six. #06
    - n == 7:
        Seven. #07
    - n == 8:
        Eight. #08
    - n == 9:
        Nine. #09
    - n == 10:
        Ten. #10
    - n == 11:
        Eleven. #11
    - n == 12:
        Twelve. #12
    - n == 13:
        Thirteen. #13
    - n == 14:
        Fourteen. #14
    - n == 15:
        Fifteen. #15
    - n == 16:
        Sixteen. #16
    - n == 17:
        Seventeen. #17
    - n == 18:
        Eighteen. #18
    - n == 19:
        Nineteen. #19
    - n == 20:
        Twenty. #20
    - else:
        /logError Can't say number '{n}'.
}
->->




=== Goddess_Take_Reward_Prompt ===

// don't use performances

{shuffle:
    - Present your wrist, little mouse. #01
    - This will cost you one Bloodstone. #02
    - Time to hand over your Bloodstone. #03
    - Give me your wrist, {player_name}. #04
}

->->




=== Goddess_Take_Reward(-> load) ===

- (give_reward)

/character goddess idle_hand_out happy 75

-> Goddess_Take_Reward_Prompt ->

+ [pose:present_wrist_reward]
    /perform do_swipe_hand excited 50
    /wait 1
    ~removeReward(load)
    -> Goddess_AcknowledgeYes.EmoteOnly ->->

+ [no] -> Goddess_CheckIfInterested -> give_reward
+ [timeout] -> Goddess_Timeout -> give_reward
+ [distracted] -> Goddess_PayAttention -> give_reward
