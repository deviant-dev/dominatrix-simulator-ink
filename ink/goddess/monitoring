// writing status: in progress




=== goddess_monitoring ===
-> select_gender_check ->

{ shuffle:
    - /scene Huge.InHand
    - /scene Huge.InHand
    - /scene Huge.InHand
    - /scene Huge.InHand
}
/music goddess-intro-1
/ambient huge-space
/character goddess

//acknowledging player
{ shuffle:
    - Hello {player_name}. #01
    - Greetings little one. #02
    - My, my, {player_name}. #03
}

/wait 1
//comment that she's watching them
{ shuffle:
    - I've been watching you. #04
    - I like to keep an eye on my playthings. #05
    - I know what you've been up to. #06
}
/wait 1
//check for type of scene
{
    - just_changed == reward_removed: -> reward_monitor
    - just_changed == punishment_removed: -> punishment_monitor
    - else: -> task_monitor
}


//comment on reward
= reward_monitor
/music goddess
/wait 1
{ shuffle:
    - You are performing quite well. #07
    - Obedience will earn my favor. #08
    - A full wrist of ten will grant you greater time with me. #09
}
/wait 1
{ shuffle:
    - For now, the insatiable hunger is appeased. #10
    - A house of order is so intoxicating. #11
    - There are so many playthings to keep track of. #12
}
/wait 1
//say Goodbye
{ shuffle:
    - I am satisfied. #13
    Until next time. #14
    - Now go. Please me further. #15
    - Goodbye {player_name}. #16
    I know you will not disappoint me. #17
}
/wait 1
-> entry_hub


//comment on punishment
= punishment_monitor
/music goddess-giant-punishment
/wait 1
{ shuffle:
    - I hope you enjoyed the punishment I arranged for you. #18
    - I'm starting to wonder if you'll ever be worthy. #19
    - Disobedience will not be tolerated. #20
}
/wait 1
//say Warning
{ shuffle:
    - Passion is the price for existence. #21
    - Order must be maintained. #22
    - Haven't you noticed the frozen failures? #23
}
/wait 1
//say Goodbye
{ shuffle:
    - Think on this. Now out of my sight. #24
    - Time for you to return. #25
    - You have been warned. Begone. #26
}
/wait 1
-> entry_hub


//comment on task
= task_monitor
/music goddess-intro-2
/wait 1
{ shuffle:
    - Practice makes perfect, {player_name}. #27
    - So many tasks. So little time. #28
    - Service beckons you, I see. #29
}

/wait 1
//say encouragement
{shuffle:
    - The more you serve, the more valuable you are. #30
    - Pain awaits those who disobey. #31
    - Put your heart and soul into everything you do. #32
    As if your life depends on it. #33
}
/wait 1 
//say Goodbye
{ shuffle:
    - Remember... I'm always watching. #34
    - Leave me. #35
    - You have more to learn. #36
    }
    
/wait 1   
-> entry_hub



TODO: Add in monitoring that happens after a player finishes watching a vignette

//make it so that after a player walks into a Mistress's room and watches a vignette, there is an ingame story reason they are there and an in-game consequence for what happens when they leave
//The player getting a glimpse of the private lives of Mistress's is a glitch in the balance of the House
//Different Mistresses pull them out for different reasons.
// Goddess pulls them into her hand (as in 'monitoring' above). Instead of commenting on their progress, she corrects them. etc, they are not ready yet, Mistress's need their privacy, it's just a little slip of the House that has been reset 
//Ereshkigal whispers to them that everything is not what it seems to be, that the Mistresses struggle with each other, that the player must see these things in order to be awake and conscious of what they are experiencing
//Miss Treatment pulls them out and gentle chastises them. There are things they are not ready to see yet. Being distracted by private moments with the Mistress's can pull them away from their responsibilities.

